import utils

namespace stage()
{
	func install()
	{
		scoreboard objectives add game_id dummy
		
		scoreboard players set #PLAYER_MODE number 1
		scoreboard players set #SELECTED_STAGE number 1
		scoreboard players set #ZONE_MAX number 7800
		# 破壞後7秒回復
		scoreboard players operation #ZONE_RECOVER number = #ZONE_MAX number
		scoreboard players operation #ZONE_RECOVER number /= #140 number
		
		data modify storage stage:stage1 DefaultPose set value [300f,335f,0f]
		data modify storage stage:stage1 ZonePose set value [[300f,0f,40f],[300f,0f,75f],[311f,22f,174f]]
		
		bossbar add boss "BOSS"
		bossbar set minecraft:boss color red
		bossbar add zone {"translate":"Zone"}
		execute store result bossbar minecraft:zone max run scoreboard players get #ZONE_MAX number
	}
	
	func main()
	{
		execute if score #CURRENT_STAGE number matches 1 run function ARG(_PATH)stage1/main
		execute if score #CURRENT_STAGE number matches 2 run function ARG(_PATH)stage2/main
		execute if score #CURRENT_STAGE number matches 3 run function ARG(_PATH)stage3/main
	}
	
	# activated by sign click
	func start()
	{
		scoreboard players set @s player 1
		scoreboard players set @s weapon_mode 1
		item replace entity @s weapon.mainhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		item replace entity @s weapon.offhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		
		# swap mode
		execute if score #PLAYER_MODE number matches 3 as @p[scores={player=0},gamemode=adventure] run function ARG(_PATH)set_player2
		gamemode spectator @a[scores={player=0}]
		# reset beam CD
		function skill:gun/beam/timer/start
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/start
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/start
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/start
		
		bossbar set minecraft:boss players @a
		bossbar set minecraft:boss visible true
		
		execute store result score @a[scores={player=1..}] game_id run scoreboard players add #GLOBAL game_id 1
	}
	
	func set_player2()
	{
		scoreboard players set @s player 2
		scoreboard players set @s weapon_mode -1
		item replace entity @s weapon.mainhand with minecraft:spyglass{display:{Name:'{"translate":"Supreme Pulse","bold":true,"italic":false}'},weapon:1b}
	}
	
	func end()
	{
		bossbar set minecraft:boss visible false
		kill @e[tag=object]
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/end
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/end
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/end
	}
	
	# 真白之刃
	folder stage1()
	{
		func start()
		{
			bossbar set minecraft:boss max 3300
			bossbar set minecraft:boss value 3300
			bossbar set minecraft:boss name {"translate":"Sacred Knight","color":"white","bold":true}
			
			bossbar set minecraft:zone players @a
			bossbar set minecraft:zone visible true
			function ARG(_PATH)skills/zone/reset
			# tp @a x y z rx ry
			# summon armor_stand x y z {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,ShowArms:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:2}}],HandItems:[{id:"minecraft:potion",Count:1b,tag:{CustomModelData:3}},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:4}}],Pose:[RightArm:[300f,0f,40f],LeftArm:[350f,0f,350f]],Tags:["boss","object"],UUID:[I;1,1,1,1]}
			scoreboard players set 00000001-0000-0001-0000-000100000001 number 1
			# 飛劍
			# summon armor_stand x y z {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Tags:["effect","object"],UUID:[I;2,2,2,2]}
			# 翅膀裝飾
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;4,4,4,4]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;5,5,5,5]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;6,6,6,6]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;7,7,7,7]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;8,8,8,8]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;9,9,9,9]}
			effect give @e[type=husk,tag=wing] invisibility 1000000 0 true
			
			schedule function ARG(_PATH)skills/loop 7s
			schedule function ARG(_PATH)skills/fly_sword/start 3s
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
			schedule clear ARG(_PATH)skills/zone/auto_reset_pose
			schedule clear ARG(_PATH)skills/fly_sword/stop
			schedule clear ARG(_PATH)skills/fly_sword/aim
			schedule clear ARG(_PATH)skills/fly_sword/start
			schedule clear ARG(_PATH)skills/revert_action
			
			bossbar set minecraft:zone visible false
			scoreboard players set #ZONE_ACTIVE number 0
			scoreboard players set #FLYING_SWORD number 0
			scoreboard players set #SWORD_DANCE number 0
			scoreboard players set #WING_OPEN number 0
			scoreboard players set #SLASH_COUNTDOWN number 0
		}
		
		func main()
		{
			execute if score #ZONE_ACTIVE number matches 1 if score #ZONE number < #ZONE_MAX number run function ARG(_PATH)skills/zone/recover
			execute if score #FLYING_SWORD number matches 1 as 00000002-0000-0002-0000-000200000002 at @s run function ARG(_PATH)skills/fly_sword/move
			
			execute if score #SWORD_DANCE number matches 1.. as 00000003-0000-0003-0000-000300000003 at @a[tag=sword_dance_target,limit=1] facing entity 00000001-0000-0001-0000-000100000001 eyes run tp @s ~ ~ ~ ~ 0
			execute if score #SWORD_DANCE number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/sword_dance/trace
			
			execute if score #SLASH_COUNTDOWN number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/slash/slash_countdown
			
			execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)wing/check
			execute if score #WING_OPEN number matches 1..10 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/sword_dance/open_wing/anim
		}
		
		folder wing()
		{
			func check()
			{
				execute if score #WING_OPEN number matches 0 run function ARG(_PATH)normal
				execute if score #WING_OPEN number matches 11.. run function ARG(_PATH)fly
			}
			
			func normal()
			{
				execute rotated ~65 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000004-0000-0004-0000-000400000004 ~ ~-1.2 ~ ~ -60
				execute rotated ~39 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000005-0000-0005-0000-000500000005 ~ ~-1.1 ~ ~ -60
				execute rotated ~13 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000006-0000-0006-0000-000600000006 ~ ~-1 ~ ~ -60
				execute rotated ~-13 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000007-0000-0007-0000-000700000007 ~ ~-1 ~ ~ -60
				execute rotated ~-39 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000008-0000-0008-0000-000800000008 ~ ~-1.1 ~ ~ -60
				execute rotated ~-65 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000009-0000-0009-0000-000900000009 ~ ~-1.2 ~ ~ -60
			}
			
			func fly()
			{
				execute rotated ~60 ~ positioned ^ ^ ^-0.7 facing entity @s feet run tp 00000004-0000-0004-0000-000400000004 ~ ~0.8 ~ ~ 50
				execute rotated ~45 ~ positioned ^ ^ ^-1.3 facing entity @s feet run tp 00000005-0000-0005-0000-000500000005 ~ ~0.2 ~ ~ 10
				execute rotated ~30 ~ positioned ^ ^ ^-1.6 facing entity @s feet run tp 00000006-0000-0006-0000-000600000006 ~ ~-0.3 ~ ~ -10
				execute rotated ~-30 ~ positioned ^ ^ ^-1.6 facing entity @s feet run tp 00000007-0000-0007-0000-000700000007 ~ ~-0.3 ~ ~ -10
				execute rotated ~-45 ~ positioned ^ ^ ^-1.3 facing entity @s feet run tp 00000008-0000-0008-0000-000800000008 ~ ~0.2 ~ ~ 10
				execute rotated ~-60 ~ positioned ^ ^ ^-0.7 facing entity @s feet run tp 00000009-0000-0009-0000-000900000009 ~ ~0.8 ~ ~ 50
			}
		}
		
		folder skills()
		{
			func loop()
			{
				execute if score #ZONE_ACTIVE number matches 1 unless score #SWORD_DANCE number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)rand_skill
				
				schedule function ARG(_PATH)loop 7s
			}
			
			func rand_skill()
			{
				function random:rand_rate
				execute store success score #NEAR_SKILL number if entity @a[scores={player=1..},distance=..4,limit=1]
				execute if score #NEAR_SKILL number matches 1 run function ARG(_PATH)near_judge
				execute if score #NEAR_SKILL number matches 0 run function ARG(_PATH)far_judge
			}
			
			func near_judge()
			{
				execute if score @s health matches ..1000 if score #RAND_RESULT number matches 40..59 run function ARG(_PATH)slash/cast
				execute if score #RAND_RESULT number matches 0..49 run function ARG(_PATH)flaw/cast
				execute if score #RAND_RESULT number matches 50..99 run function ARG(_PATH)tornado/cast
			}
			
			func far_judge()
			{
				execute if score @s health matches ..1000 if score #RAND_RESULT number matches 80..99 run function ARG(_PATH)slash/cast
				execute if score #RAND_RESULT number matches 0..19 run function ARG(_PATH)sword_dance/cast
			}
			
			func reset_pose()
			{
				data modify entity @s ArmorItems[3].tag.CustomModelData set value 2
				data modify entity @s Pose.RightArm set from storage stage:stage1 DefaultPose
				data modify entity @s Pose.LeftArm set value [10f,0f,350f]
			}
				
			func revert_action()
			{
				execute if score #ZONE_ACTIVE number matches 1 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)inner_revert_action
				
				scoreboard players set #REVERT_ACTION number 0
				scoreboard players set #WING_OPEN number 0
				schedule clear ARG(_PATH)revert_action
			}
			
			func inner_revert_action()
			{
				function ARG(_PATH)reset_pose
				
				# particle minecraft:cloud ~ ~1 ~ 0.7 0.7 0.7 0.1 100
				particle minecraft:flash ~ ~ ~ 0 0 0 1 1
			}
			
			folder dodge()
			{
				func try()
				{
					scoreboard players set #DODGE_SUCCESS number 0
					execute positioned ^2 ^ ^ if block ~ ~ ~ #skill:canpass if block ~ ~1 ~ #skill:canpass unless block ~ ~-1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SUCCESS number matches 0 positioned ^-2 ^ ^ if block ~ ~ ~ #skill:canpass if block ~ ~1 ~ #skill:canpass unless block ~ ~-1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SUCCESS number matches 0 run function ARG(_PATH)random_tp
					
					scoreboard players set #IGNORE_DAMAGE number 1
				}
				
				func tp()
				{
					scoreboard players set #DODGE_SUCCESS number 1
					tp @s ~ ~ ~
				}
				
				func random_tp()
				{
					```
					raise Warning("TODO: set max height for spreadplayers")
					```
					spreadplayers ~ ~ 0 7 under 10 false @s
					execute at @s unless entity @e[type=armor_stand,tag=blast_target,distance=..2,limit=1] unless entity @a[distance=..5,limit=1] run scoreboard players set #DODGE_SUCCESS number 1
					execute if score #DODGE_SUCCESS number matches 0 run function ARG(_PATH)random_tp
				}
			}
			
			folder zone()
			{
				func recover()
				{
					execute if score #ZONE_ACTIVE number matches 0 run scoreboard players operation #ZONE number += #ZONE_RECOVER number
					execute if score #ZONE_ACTIVE number matches 1 run scoreboard players add #ZONE number 20
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number < #ZONE_MAX number
					
					execute if score #ZONE_ACTIVE number matches 0 if score #ZONE number >= #ZONE_MAX number run function ARG(_PATH)reset
				}
				
				func reset()
				{
					bossbar set minecraft:zone color white
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number = #ZONE_MAX number
					
					execute as 00000001-0000-0001-0000-000100000001 run function ARG(__PATH)reset_pose
					execute at 00000001-0000-0001-0000-000100000001 run particle flash ~ ~ ~ 0 0 0 1 1
					
					scoreboard players set #ZONE_ACTIVE number 1
				}
				
				func hit()
				{
					execute if entity @s[tag=beam_hit] run function ARG(__PATH)dodge/try
					execute if entity @s[tag=beam_hit] run function ARG(__PATH)reset_pose
					execute if entity @s[tag=!beam_hit] run function ARG(_PATH)decrease
					scoreboard players set #IGNORE_DAMAGE number 1
					
					# 換回平常站立姿勢模型
					execute if score #ZONE_ACTIVE number matches 1 if score #REVERT_ACTION number matches 1 run function ARG(__PATH)revert_action
				}
				
				func decrease()
				{
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number -= @s display_number
					execute if score @s display_number matches ..110 run playsound minecraft:enemy.sword.strike_small player @a ~ ~ ~ 3 1 0
					execute if score @s display_number matches 111.. run playsound minecraft:enemy.sword.strike_big player @a ~ ~ ~ 3 1 0
					
					data modify entity @s Pose.RightArm set from storage stage:stage1 ZonePose[0]
					data modify storage stage:stage1 ZonePose append from storage stage:stage1 ZonePose[0]
					data remove storage stage:stage1 ZonePose[0]
					
					execute positioned ~ ~0.5 ~ run function ARG(_PATH)strike_effect
					execute if score #ZONE number matches ..0 run function ARG(_PATH)break
					
					execute if score #ZONE_ACTIVE number matches 1 run schedule function ARG(_PATH)auto_reset_pose 2s
				}
				
				func auto_reset_pose()
				{
					execute if score #ZONE_ACTIVE number matches 1 unless score #ANIM_LOCK number matches 1 run function ARG(__PATH)reset_pose
				}
				
				func break()
				{
					# 更換暈眩姿勢模型
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 9
					data modify entity @s Pose.RightArm set value [85f,150f,0f]
					data modify entity @s Pose.LeftArm set value [95f,210f,0f]
					particle flash ~ ~ ~ 0 0 0 1 2
					
					bossbar set minecraft:zone color blue
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ZONE number 0
					
					schedule clear ARG(_PATH)auto_reset_pose
				}
				
				func strike_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:strike/0"}',CustomNameVisible:1,Duration:4,Tags:['{"text":"","font":"font_animation:strike/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
			}
			
			folder fly_sword()
			{
				func move()
				{
					tp @s ^ ^ ^0.5
					execute at @s as @a[scores={player=1..},distance=..1,tag=!sword_hit] run function ARG(_PATH)hit
					execute at @s unless block ~ ~ ~ #skill:canpass run function ARG(_PATH)stop
				}
				
				func hit()
				{
					effect give @s minecraft:instant_damage 1 0 true
					tag @s add sword_hit
				}
				
				func stop()
				{
					schedule clear ARG(_PATH)stop
					tag @a remove sword_hit
					
					scoreboard players set #FLYING_SWORD number 0
					schedule function ARG(_PATH)aim 1s
				}
				
				func aim()
				{
					execute as @e[type=armor_stand,tag=fly_sword,limit=1] at @s facing entity @p[scores={player=1..}] eyes run tp @s ~ ~ ~ ~ ~
					schedule function ARG(_PATH)start 1s
				}
				
				func start()
				{
					scoreboard players set #FLYING_SWORD number 1
					schedule function ARG(_PATH)stop 2s
				}
			}
			
			folder sword_dance()
			{
				func cast()
				{
					# 暫時關閉ZONE
					scoreboard players set #ZONE_ACTIVE number 0
					execute as @a[scores={player=1..},limit=1,sort=random] at @s run function ARG(_PATH)mark
					
					scoreboard players set #ANIM_LOCK number 1
					function ARG(_PATH)open_wing/start
				}
				
				func mark()
				{
					# 頭上標記特效
					execute positioned ~ ~1.5 ~ run function ARG(_PATH)mark_effect
					summon minecraft:armor_stand ~ ~ ~ {UUID:[I;3,3,3,3],Invisible:1,NoGravity:1,Invulnerable:1,Marker:1,Fire:100s,Tags:["object","effect","slash","skill_effect"],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:26}}]}
					scoreboard players set 00000003-0000-0003-0000-000300000003 number 32
					scoreboard players set 00000003-0000-0003-0000-000300000003 life -100000
					tag @s add sword_dance_target
				}
				
				func mark_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:mark/1"}',CustomNameVisible:1,Duration:11,Tags:['{"text":"","font":"font_animation:mark/1"}', '{"text":"","font":"font_animation:mark/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 10
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				folder open_wing()
				{
					func start()
					{
						scoreboard players set #WING_OPEN number 1
						function stage:stage1/wing/fly
						tp @s ~ ~-0.4 ~
						# 計算右手姿勢
						execute store result score #R_ARM_X number run data get entity @s Pose.RightArm[0] 10
						execute store result score #R_ARM_Y number run data get entity @s Pose.RightArm[1] 10
						execute store result score #R_ARM_Z number run data get entity @s Pose.RightArm[2] 10
						scoreboard players set #R_ARM_DIFF_X number 2400
						scoreboard players set #R_ARM_DIFF_Y number 3500
						scoreboard players set #R_ARM_DIFF_Y number 800
						scoreboard players operation #R_ARM_DIFF_X number -= #R_ARM_X number
						scoreboard players operation #R_ARM_DIFF_Y number -= #R_ARM_Y number
						scoreboard players operation #R_ARM_DIFF_Z number -= #R_ARM_Z number
						scoreboard players operation #R_ARM_DIFF_X number /= #5 number
						scoreboard players operation #R_ARM_DIFF_Y number /= #5 number
						scoreboard players operation #R_ARM_DIFF_Z number /= #5 number
						# 計算左手姿勢 (10->-70, 0->30, --)
						scoreboard players set #L_ARM_X number 10
						scoreboard players set #L_ARM_Y number 0
						scoreboard players set #L_ARM_DIFF_X number -16
						scoreboard players set #L_ARM_DIFF_Y number 6
						
						function ARG(_PATH)anim
					}
					
					func anim()
					{
						execute if score #WING_OPEN number matches 1..5 run function ARG(_PATH)calc_pose
						execute if score #WING_OPEN number matches 2 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 6
						execute if score #WING_OPEN number matches 4 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 7
						execute if score #WING_OPEN number matches 10 run function ARG(_PATH)end
						
						scoreboard players add #WING_OPEN number 1
					}
					
					func calc_pose()
					{
						# 右手
						scoreboard players operation #R_ARM_X number += #R_ARM_DIFF_X number
						scoreboard players operation #R_ARM_Y number += #R_ARM_DIFF_Y number
						scoreboard players operation #R_ARM_Z number += #R_ARM_DIFF_Z number
						execute store result entity @s Pose.RightArm[0] float 0.1 run scoreboard players get #R_ARM_X
						execute store result entity @s Pose.RightArm[1] float 0.1 run scoreboard players get #R_ARM_Y
						execute store result entity @s Pose.RightArm[2] float 0.1 run scoreboard players get #R_ARM_Z
						# 左手
						execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
						execute store result entity @s Pose.LeftArm[1] float 1 run scoreboard players operation #L_ARM_Y number += #L_ARM_DIFF_Y number
					}
					
					func end()
					{
						particle minecraft:explosion ~ ~ ~ 0 0 0 3 5
						playsound minecraft:entity.wither.shoot player @a ~ ~ ~ 2 1.3 0
						
						data modify entity @s ArmorItems[3].tag.CustomModelData set value 8
						scoreboard players set #SWORD_DANCE number 1
					}
				}
				
				func trace()
				{
					execute facing entity @a[tag=sword_dance_target,limit=1] feet run tp @s ^ ^ ^0.5 ~ ~
					
					# 約 8 tick 的反應時間
					execute if score #SWORD_DANCE number matches 2 at @s if entity @a[tag=sword_dance_target,distance=..1,limit=1] run function ARG(_PATH)attack
					execute if score #SWORD_DANCE number matches 1 at @s if entity @a[tag=sword_dance_target,distance=..5,limit=1] run function ARG(_PATH)prepare
				}
				
				func prepare()
				{
					# 反擊ICON
					title @a[tag=sword_dance_target] times 0 10000 0
					title @a[tag=sword_dance_target] title {"text":"1","font":"ui:main"}
					# 切換攻擊預備動作模型
					data modify entity @s Pose.RightArm set value [260f,350f,100f]
					particle flash ~ ~1 ~ 0 0 0 1 1
					
					scoreboard players set #SWORD_DANCE number 2
				}
				
				func attack()
				{
					execute as @a[tag=sword_dance_target,limit=1] run function ARG(_PATH)damage
					# 切換揮刀模型
					data modify entity @s Pose.RightArm set value [30f,350f,100f]
					
					scoreboard players set 00000003-0000-0003-0000-000300000003 life 0
					execute as 00000003-0000-0003-0000-000300000003 store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get @s number
					playsound minecraft:enemy.sword.dance_attack player @a
					
					function ARG(_PATH)end
				}
				
				func damage()
				{
					# 讓 BOSS 穿過玩家 1.5 格
					execute facing entity @s feet positioned as @s rotated ~ 0 run tp 00000001-0000-0001-0000-000100000001 ^ ^ ^1.5 ~ ~
					effect give @s minecraft:instant_damage 1 2 true
				}
				
				func counter()
				{
					# 切換揮刀模型
					data modify entity @s Pose.RightArm set value [30f,350f,100f]
					execute at @a[tag=sword_dance_target,limit=1] facing entity @s feet rotated ~ 0 run function ARG(_PATH)set_counter_effect
					
					function ARG(_PATH)end
				}
				
				func set_counter_effect()
				{
					tp @s ^ ^ ^1 ~180 ~
					execute positioned ^ ^1 ^0.5 run function ARG(_PATH)counter_effect
					execute positioned ^ ^1 ^0.5 run playsound minecraft:enemy.sword.clash player @a ~ ~ ~ 3 1 0
				}
				
				func counter_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:sword_clash/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:sword_clash/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 4
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					
					scoreboard players set #REVERT_ACTION number 1
					schedule function ARG(__PATH)revert_action 1s
					scoreboard players set #SWORD_DANCE number 0
					scoreboard players set #ANIM_LOCK number 0
					
					title @a[tag=sword_dance_target] title ""
					tag @a remove sword_dance_target
				}
			}
			
			folder slash()
			{
				func cast()
				{
					scoreboard players set #RAND_RESULT number -1
					# 暫時關閉ZONE
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ANIM_LOCK number 1
					scoreboard players set #WING_OPEN number 11
					# 準備姿勢
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 10
					data modify entity @s Pose.RightArm set value [300f,330f,345f]
					data modify entity @s Pose.LeftArm set value [45f,205f,15f]
					# 腳邊旋風 & 身邊閃電特效
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,UUID:[I;11,11,11,11],ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:13}}],Tags:["object"]}
					
					# 計算右手姿勢 (300->260, --, 345->367)
					scoreboard players set #R_ARM_X number 3000
					scoreboard players set #R_ARM_DIFF_X number -80
					scoreboard players set #R_ARM_Z number 3450
					scoreboard players set #R_ARM_DIFF_Z number 44
					# 計算左手姿勢 (45->70, --, --)
					scoreboard players set #L_ARM_X number 45
					scoreboard players set #L_ARM_DIFF_X number 5
					
					execute as @r[scores={player=1..}] at @s run function ARG(_PATH)mark
					function ARG(_PATH)rand_slash_time
				}
				
				func rand_slash_time() from random().generate(60, 100, "#SLASH_COUNTDOWN number");
				
				func mark()
				{
					tag @s add slash_target
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,UUID:[I;10,10,10,10],ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:11,CustomPotionColor:16777215}}]}
				}
				
				func slash_countdown()
				{
					execute if score #SLASH_COUNTDOWN number matches 2..6 run function ARG(_PATH)calc_pose
					execute if score #SLASH_COUNTDOWN number matches 8 run data modify entity 0000000a-0000-000a-0000-000a0000000a ArmorItems[3].tag.CustomPotionColor set value 16711680
					execute if score #SLASH_COUNTDOWN number matches 8.. run tp 0000000a-0000-000a-0000-000a0000000a @a[tag=slash_target,limit=1]
					
					scoreboard players remove #SLASH_COUNTDOWN number 1
					execute if score #SLASH_COUNTDOWN number matches 0 run function ARG(_PATH)end
				}
				
				func calc_pose()
				{
					# 右手
					execute store result entity @s Pose.RightArm[0] float 0.1 run scoreboard players operation #R_ARM_X number += #R_ARM_DIFF_X number
					execute store result entity @s Pose.RightArm[2] float 0.1 run scoreboard players operation #R_ARM_Z number += #R_ARM_DIFF_Z number
					# 左手
					execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					scoreboard players set #ANIM_LOCK number 0
					scoreboard players set #REVERT_ACTION number 1
					schedule function ARG(__PATH)revert_action 1s
					
					# 更換劈砍姿勢模型
					data modify @s ArmorItems[3].tag.CustomModelData set value 12
					data modify @s Pose.RightArm set value [350f,350f,320f]
					data modify @s Pose.LeftArm set value [350f,200f,30f]
					
					particle minecraft:explosion ~ ~ ~ 0 0 0 3 5
					kill 0000000b-0000-000b-0000-000b0000000b
					execute as 0000000a-0000-000a-0000-000a0000000a at @s run function ARG(_PATH)slash
					execute facing entity 0000000a-0000-000a-0000-000a0000000a feet rotated ~ 0 positioned as 0000000a-0000-000a-0000-000a0000000a run tp @s ^ ^ ^4
					tag @a remove slash_target
				}
				
				func slash()
				{
					playsound minecraft:enemy.sword.slash player @a
					execute positioned ~ ~1 ~ run function ARG(_PATH)slash_effect
					execute as @a[scores={player=1..},distance=..3] run function ARG(_PATH)damage
					
					schedule function ARG(_PATH)activate_shader/run 1t
				}
				
				func slash_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:slash/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:slash/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 4
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func damage()
				{
					function skill:reset_action
					team join blue @s
					effect give @s minecraft:glowing 1000000 0 true
					tag @s add already_dead
				}
				
				folder activate_shader()
				{
					func run()
					{
						execute as @a[tag=already_dead] run function ARG(_PATH)set
						schedule function ARG(_PATH)reset 1s
					}
					
					func set()
					{
						effect give @s minecraft:night_vision 1000000 0 true
						team leave @s
						# tp @s 小黑屋
					}
					
					func reset()
					{
						execute as @a[tag=already_dead] run function ARG(_PATH)player_reset
					}
					
					func player_reset()
					{
						effect clear @s minecraft:night_vision
						effect clear @s minecraft:glowing
						kill @s
						tag @s remove already_dead
					}
				}
			}
			
			folder flaw()
			{
				func cast()
				{
					scoreboard players set #RAND_RESULT number -1
					# 暫時關閉ZONE
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ANIM_LOCK number 1
					
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.RightArm set value [240f,350f,80f]
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.LeftArm set value [70f,30f,0f]
					data modify entity 00000001-0000-0001-0000-000100000001 ArmorItems[3].tag.CustomModelData set value 7
					
					execute at @r[scores={player=1..}] run function ARG(_PATH)positioned_effect
					schedule function ARG(_PATH)attack 10t
				}
				
				func positioned_effect()
				{
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,UUID:[I;12,12,12,12],ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:14}}]}
					scoreboard players set 0000000c-0000-000c-0000-000c0000000c number 36
					scoreboard players set 0000000c-0000-000c-0000-000c0000000c life -100000
					
					execute rotated ~ 0 run tp @s ^ ^-0.4 ^-1.5 ~ ~
				}
				
				func attack()
				{
					scoreboard players set 0000000c-0000-000c-0000-000c0000000c life 0
					execute as 0000000c-0000-000c-0000-000c0000000c store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get @s number
					
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)attack_effect
					
					# 給半徑3格沒跳起來的玩家傷害
				}
				
				func attack_effect()
				{
					# 播斬擊音效
					data modify entity @s Pose.RightArm set value [30f,350f,100f]
					tp @s ^ ^ ^1.5
				}
			}
			
			folder tornado()
			{
				func cast()
				{
					
				}
			}
		}
	}
	
	# 至尊魔導
	folder stage2()
	{
		func start()
		{
			bossbar set minecraft:boss name {"translate":"Arch Magician","color":"white","bold":true}
			# tp @a x y z rx ry
			# summon ...
			schedule function ARG(_PATH)skills/loop 7s
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
		}
		
		func main()
		{
			
		}
		
		folder skills()
		{
			func loop()
			{
				schedule function ARG(_PATH)loop 7s
			}
		}
	}
	
	# 深淵恐懼
	folder stage3()
	{
		func start()
		{
			bossbar set minecraft:boss name {"translate":"Root of Fear","color":"white","bold":true}
			# tp @a x y z rx ry
			# summon ...
			schedule function ARG(_PATH)skills/loop 7s
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
		}
		
		func main()
		{
			
		}
		
		folder skills()
		{
			func loop()
			{
				schedule function ARG(_PATH)loop 7s
			}
		}
	}
}