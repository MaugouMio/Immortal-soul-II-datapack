import utils

namespace stage()
{
	func install()
	{
		scoreboard objectives add game_id dummy
		
		scoreboard players set #PLAYER_MODE number 1
		scoreboard players set #SELECTED_STAGE number 1
		
		data modify storage stage:stage1 ZonePose set value [[300f,0f,40f],[300f,0f,75f],[311f,22f,174f]]
		
		bossbar add boss "BOSS"
		bossbar set minecraft:boss color red
		bossbar add zone {"translate":"Zone"}
		bossbar set minecraft:zone max 2000
	}
	
	func main()
	{
		execute if score #CURRENT_STAGE number matches 1 run function ARG(_PATH)stage1/main
		execute if score #CURRENT_STAGE number matches 2 run function ARG(_PATH)stage2/main
		execute if score #CURRENT_STAGE number matches 3 run function ARG(_PATH)stage3/main
	}
	
	# activated by sign click
	func start()
	{
		scoreboard players set @s player 1
		scoreboard players set @s weapon_mode 1
		item replace entity @s weapon.mainhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		item replace entity @s weapon.offhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		
		# swap mode
		execute if score #PLAYER_MODE number matches 3 as @p[scores={player=0},gamemode=adventure] run function ARG(_PATH)set_player2
		gamemode spectator @a[scores={player=0}]
		# reset beam CD
		function skill:gun/beam/timer/start
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/start
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/start
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/start
		
		bossbar set minecraft:boss players @a
		bossbar set minecraft:boss visible true
		
		execute store result score @a[scores={player=1..}] game_id run scoreboard players add #GLOBAL game_id 1
	}
	
	func set_player2()
	{
		scoreboard players set @s player 2
		scoreboard players set @s weapon_mode -1
		item replace entity @s weapon.mainhand with minecraft:spyglass{display:{Name:'{"translate":"Supreme Pulse","bold":true,"italic":false}'},weapon:1b}
	}
	
	func end()
	{
		bossbar set minecraft:boss visible false
		kill @e[tag=object]
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/end
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/end
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/end
	}
	
	# 真白之刃
	folder stage1()
	{
		func start()
		{
			bossbar set minecraft:boss max 2000
			bossbar set minecraft:boss value 2000
			bossbar set minecraft:boss name {"translate":"Sacred Knight","color":"white","bold":true}
			
			bossbar set minecraft:zone players @a
			function ARG(_PATH)skills/zone/reset
			# tp @a x y z rx ry
			# summon armor_stand x y z {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,ShowArms:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:2}}],HandItems:[{id:"minecraft:potion",Count:1b,tag:{CustomModelData:3}},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:4}}],Pose:[RightArm:[300f,0f,40f],LeftArm:[350f,0f,350f]],Tags:["boss","object"],UUID:[I;1,1,1,1]}
			scoreboard players set 00000001-0000-0001-0000-000100000001 number 1
			# 飛劍
			# summon armor_stand x y z {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Tags:["effect","object"],UUID:[I;2,2,2,2]}
			# 翅膀裝飾
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;4,4,4,4]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;5,5,5,5]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;6,6,6,6]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;7,7,7,7]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;8,8,8,8]}
			# summon husk x y z {Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;9,9,9,9]}
			effect give @e[type=husk,tag=wing] invisibility 1000000 0 true
			
			schedule function ARG(_PATH)skills/loop 7s
			schedule function ARG(_PATH)skills/fly_sword/start 3s
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
			schedule clear ARG(_PATH)skills/zone/reset
			schedule clear ARG(_PATH)skills/fly_sword/stop
			schedule clear ARG(_PATH)skills/fly_sword/aim
			schedule clear ARG(_PATH)skills/fly_sword/start
			schedule clear ARG(_PATH)skills/sword_dance/revert_action
			
			bossbar set minecraft:zone visible false
			scoreboard players set #FLYING_SWORD number 0
			scoreboard players set #SWORD_DANCE number 0
			scoreboard players set #WING_OPEN number 0
		}
		
		func main()
		{
			execute if score #FLYING_SWORD number matches 1 as 00000002-0000-0002-0000-000200000002 at @s run function ARG(_PATH)skills/fly_sword/move
			execute if score #SWORD_DANCE number matches 1.. as 00000003-0000-0003-0000-000300000003 at @a[tag=sword_dance_target,limit=1] facing entity 00000001-0000-0001-0000-000100000001 eyes run tp @s ~ ~ ~ ~ 0
			execute if score #SWORD_DANCE number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/sword_dance/trace
			execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)wing/check
			execute if score #WING_OPEN number matches 1..10 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/sword_dance/open_wing/anim
		}
		
		folder wing()
		{
			func check()
			{
				execute if score #WING_OPEN number matches 0 run function ARG(_PATH)normal
				execute if score #WING_OPEN number matches 11.. run function ARG(_PATH)fly
			}
			
			func normal()
			{
				execute rotated ~65 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000004-0000-0004-0000-000400000004 ~ ~-1.2 ~ ~ -60
				execute rotated ~39 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000005-0000-0005-0000-000500000005 ~ ~-1.1 ~ ~ -60
				execute rotated ~13 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000006-0000-0006-0000-000600000006 ~ ~-1 ~ ~ -60
				execute rotated ~-13 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000007-0000-0007-0000-000700000007 ~ ~-1 ~ ~ -60
				execute rotated ~-39 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000008-0000-0008-0000-000800000008 ~ ~-1.1 ~ ~ -60
				execute rotated ~-65 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000009-0000-0009-0000-000900000009 ~ ~-1.2 ~ ~ -60
			}
			
			func fly()
			{
				execute rotated ~60 ~ positioned ^ ^ ^-0.7 facing entity @s feet run tp 00000004-0000-0004-0000-000400000004 ~ ~0.8 ~ ~ 50
				execute rotated ~45 ~ positioned ^ ^ ^-1.3 facing entity @s feet run tp 00000005-0000-0005-0000-000500000005 ~ ~0.2 ~ ~ 10
				execute rotated ~30 ~ positioned ^ ^ ^-1.6 facing entity @s feet run tp 00000006-0000-0006-0000-000600000006 ~ ~-0.3 ~ ~ -10
				execute rotated ~-30 ~ positioned ^ ^ ^-1.6 facing entity @s feet run tp 00000007-0000-0007-0000-000700000007 ~ ~-0.3 ~ ~ -10
				execute rotated ~-45 ~ positioned ^ ^ ^-1.3 facing entity @s feet run tp 00000008-0000-0008-0000-000800000008 ~ ~0.2 ~ ~ 10
				execute rotated ~-60 ~ positioned ^ ^ ^-0.7 facing entity @s feet run tp 00000009-0000-0009-0000-000900000009 ~ ~0.8 ~ ~ 50
			}
		}
		
		folder skills()
		{
			func loop()
			{
				execute unless score #SWORD_DANCE number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)rand_skill
				
				schedule function ARG(_PATH)loop 7s
			}
			
			func rand_skill()
			{
				function random:rand_rate
				execute store success score #NEAR_SKILL number if entity @a[scores={player=1..},distance=..4,limit=1]
				execute if score #NEAR_SKILL number matches 1 run function ARG(_PATH)near_judge
				execute if score #NEAR_SKILL number matches 0 run function ARG(_PATH)far_judge
			}
			
			func near_judge()
			{
				execute if score @s health matches ..1000 if score #RAND_RESULT number matches 40..59 run function ARG(_PATH)slash/cast
				execute if score #RAND_RESULT number matches 0..49 run function ARG(_PATH)flaw/cast
				execute if score #RAND_RESULT number matches 50..99 run function ARG(_PATH)tornado/cast
			}
			
			func far_judge()
			{
				execute if score @s health matches ..1000 if score #RAND_RESULT number matches 80..99 run function ARG(_PATH)slash/cast
				execute if score #RAND_RESULT number matches 0..19 run function ARG(_PATH)sword_dance/cast
			}
			
			folder dodge()
			{
				func try()
				{
					scoreboard players set #DODGE_SUCCESS number 0
					execute positioned ^2 ^ ^ if block ~ ~ ~ #skill:canpass if block ~ ~1 ~ #skill:canpass unless block ~ ~-1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SUCCESS number matches 0 positioned ^-2 ^ ^ if block ~ ~ ~ #skill:canpass if block ~ ~1 ~ #skill:canpass unless block ~ ~-1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SUCCESS number matches 0 run function ARG(_PATH)random_tp
					
					scoreboard players set #IGNORE_DAMAGE number 1
				}
				
				func tp()
				{
					scoreboard players set #DODGE_SUCCESS number 1
					tp @s ~ ~ ~
				}
				
				func random_tp()
				{
					```
					raise Warning("TODO: set max height for spreadplayers")
					```
					spreadplayers ~ ~ 0 7 under 10 false @s
					execute at @s unless entity @e[type=armor_stand,tag=blast_target,distance=..2,limit=1] unless entity @a[distance=..5,limit=1] run scoreboard players set #DODGE_SUCCESS number 1
					execute if score #DODGE_SUCCESS number matches 0 run function ARG(_PATH)random_tp
				}
			}
			
			folder zone()
			{
				func reset()
				{
					execute store result score #ZONE number run bossbar get minecraft:zone max
					execute store result bossbar minecraft:zone value run bossbar get minecraft:zone max
					bossbar set minecraft:zone visible true
				}
				
				func hit()
				{
					execute if entity @s[tag=beam_hit] run function ARG(__PATH)dodge/try
					execute if entity @s[tag=!beam_hit] run function ARG(_PATH)decrease
					scoreboard players set #IGNORE_DAMAGE number 1
					
					# 換回平常站立姿勢模型
					execute if score #REVERT_ACTION number matches 1 run function ARG(__PATH)sword_dance/revert_action
				}
				
				func decrease()
				{
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number -= @s display_number
					execute if score @s display_number matches ..110 run playsound minecraft:enemy.sword.strike_small player @a ~ ~ ~ 3 1 0
					execute if score @s display_number matches 111.. run playsound minecraft:enemy.sword.strike_big player @a ~ ~ ~ 3 1 0
					
					data modify entity @s Pose.RightArm set from storage stage:stage1 ZonePose[0]
					data modify storage stage:stage1 ZonePose append from storage stage:stage1 ZonePose[0]
					data remove storage stage:stage1 ZonePose[0]
					
					execute positioned ~ ~0.5 ~ run function ARG(_PATH)strike_effect
					execute if score #ZONE number matches ..0 run function ARG(_PATH)break
				}
				
				func break()
				{
					bossbar set minecraft:zone visible false
					schedule function ARG(_PATH)reset 10s
				}
				
				func strike_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:strike/0"}',CustomNameVisible:1,Duration:4,Tags:['{"text":"","font":"font_animation:strike/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
			}
			
			folder fly_sword()
			{
				func move()
				{
					tp @s ^ ^ ^0.5
					execute at @s as @a[scores={player=1..},distance=..1,tag=!sword_hit] run function ARG(_PATH)hit
					execute at @s unless block ~ ~ ~ #skill:canpass run function ARG(_PATH)stop
				}
				
				func hit()
				{
					effect give @s minecraft:instant_damage 1 0 true
					tag @s add sword_hit
				}
				
				func stop()
				{
					schedule clear ARG(_PATH)stop
					tag @a remove sword_hit
					
					scoreboard players set #FLYING_SWORD number 0
					schedule function ARG(_PATH)aim 1s
				}
				
				func aim()
				{
					execute as @e[type=armor_stand,tag=fly_sword,limit=1] at @s facing entity @p[scores={player=1..}] eyes run tp @s ~ ~ ~ ~ ~
					schedule function ARG(_PATH)start 1s
				}
				
				func start()
				{
					scoreboard players set #FLYING_SWORD number 1
					schedule function ARG(_PATH)stop 2s
				}
			}
			
			folder sword_dance()
			{
				func cast()
				{
					# 暫時關閉ZONE
					scoreboard players set #ZONE number 0
					execute as @a[scores={player=1..},limit=1,sort=random] at @s run function ARG(_PATH)mark
					
					function ARG(_PATH)open_wing/start
				}
				
				func mark()
				{
					# 頭上標記特效
					execute positioned ~ ~1.5 ~ run function ARG(_PATH)mark_effect
					summon minecraft:armor_stand ~ ~ ~ {UUID:[I;3,3,3,3],Invisible:1,NoGravity:1,Invulnerable:1,Marker:1,Fire:100s,Tags:["object","effect","slash","skill_effect"],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:26}}]}
					scoreboard players set 00000003-0000-0003-0000-000300000003 number 32
					scoreboard players set 00000003-0000-0003-0000-000300000003 life -100000
					tag @s add sword_dance_target
				}
				
				func mark_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:mark/1"}',CustomNameVisible:1,Duration:11,Tags:['{"text":"","font":"font_animation:mark/1"}', '{"text":"","font":"font_animation:mark/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 10
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				folder open_wing()
				{
					func start()
					{
						scoreboard players set #WING_OPEN number 1
						function stage:stage1/wing/fly
						# 計算右手姿勢
						execute store result score #R_ARM_X number run data get entity @s Pose.RightArm[0] 10
						execute store result score #R_ARM_Y number run data get entity @s Pose.RightArm[1] 10
						execute store result score #R_ARM_Z number run data get entity @s Pose.RightArm[2] 10
						execute if score #R_ARM_X number matches 180.. run scoreboard players set #R_ARM_DIFF_X number 2400
						execute if score #R_ARM_X number matches ..179 run scoreboard players set #R_ARM_DIFF_X number 1200
						execute if score #R_ARM_Y number matches 180.. run scoreboard players set #R_ARM_DIFF_Y number 3500
						execute if score #R_ARM_Y number matches ..179 run scoreboard players set #R_ARM_DIFF_Y number 100
						execute if score #R_ARM_Z number matches 180.. run scoreboard players set #R_ARM_DIFF_Y number 2800
						execute if score #R_ARM_Z number matches ..179 run scoreboard players set #R_ARM_DIFF_Y number 800
						scoreboard players operation #R_ARM_DIFF_X number -= #R_ARM_X number
						scoreboard players operation #R_ARM_DIFF_Y number -= #R_ARM_Y number
						scoreboard players operation #R_ARM_DIFF_Z number -= #R_ARM_Z number
						scoreboard players operation #R_ARM_DIFF_X number /= #5 number
						scoreboard players operation #R_ARM_DIFF_Y number /= #5 number
						scoreboard players operation #R_ARM_DIFF_Z number /= #5 number
						# 處理正負號
						execute store success score #R_ARM_NEG_X number if score #R_ARM_DIFF_X number matches ..-1
						execute store success score #R_ARM_NEG_Y number if score #R_ARM_DIFF_Y number matches ..-1
						execute store success score #R_ARM_NEG_Z number if score #R_ARM_DIFF_Z number matches ..-1
						execute if score #R_ARM_DIFF_X number matches ..-1 run scoreboard players operation #R_ARM_DIFF_X number *= #-1 number
						execute if score #R_ARM_DIFF_Y number matches ..-1 run scoreboard players operation #R_ARM_DIFF_Y number *= #-1 number
						execute if score #R_ARM_DIFF_Z number matches ..-1 run scoreboard players operation #R_ARM_DIFF_Z number *= #-1 number
						# 計算左手姿勢 (350->290, 0->30, --)
						scoreboard players set #L_ARM_X number 350
						scoreboard players set #L_ARM_Y number 0
						scoreboard players set #L_ARM_DIFF_X number 12
						scoreboard players set #L_ARM_DIFF_Y number 6
						
						function ARG(_PATH)anim
					}
					
					func anim()
					{
						execute if score #WING_OPEN number matches 1..5 run function ARG(_PATH)calc_pose
						execute if score #WING_OPEN number matches 2 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 6
						execute if score #WING_OPEN number matches 4 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 7
						execute if score #WING_OPEN number matches 10 run function ARG(_PATH)end
						
						scoreboard players add #WING_OPEN number 1
					}
					
					func calc_pose()
					{
						# 右手
						execute if score #R_ARM_NEG_X number matches 0 run scoreboard players operation #R_ARM_X number += #R_ARM_DIFF_X number
						execute if score #R_ARM_NEG_X number matches 1 run scoreboard players operation #R_ARM_X number -= #R_ARM_DIFF_X number
						execute if score #R_ARM_NEG_Y number matches 0 run scoreboard players operation #R_ARM_Y number += #R_ARM_DIFF_Y number
						execute if score #R_ARM_NEG_Y number matches 1 run scoreboard players operation #R_ARM_Y number -= #R_ARM_DIFF_Y number
						execute if score #R_ARM_NEG_Z number matches 0 run scoreboard players operation #R_ARM_Z number += #R_ARM_DIFF_Z number
						execute if score #R_ARM_NEG_Z number matches 1 run scoreboard players operation #R_ARM_Z number -= #R_ARM_DIFF_Z number
						execute store result entity @s Pose.RightArm[0] float 0.1 run scoreboard players get #R_ARM_X
						execute store result entity @s Pose.RightArm[1] float 0.1 run scoreboard players get #R_ARM_Y
						execute store result entity @s Pose.RightArm[2] float 0.1 run scoreboard players get #R_ARM_Z
						# 左手
						execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number -= #L_ARM_DIFF_X number
						execute store result entity @s Pose.LeftArm[1] float 1 run scoreboard players operation #L_ARM_Y number += #L_ARM_DIFF_Y number
					}
					
					func end()
					{
						particle minecraft:explosion ~ ~ ~ 0 0 0 3 5
						playsound minecraft:entity.wither.shoot player @a ~ ~ ~ 2 1.3 0
						
						data modify entity @s ArmorItems[3].tag.CustomModelData set value 8
						scoreboard players set #SWORD_DANCE number 1
					}
				}
				
				func trace()
				{
					execute facing entity @a[tag=sword_dance_target,limit=1] feet run tp @s ^ ^ ^0.5 ~ ~
					
					# 約 8 tick 的反應時間
					execute if score #SWORD_DANCE number matches 2 at @s if entity @a[tag=sword_dance_target,distance=..1,limit=1] run function ARG(_PATH)attack
					execute if score #SWORD_DANCE number matches 1 at @s if entity @a[tag=sword_dance_target,distance=..5,limit=1] run function ARG(_PATH)prepare
				}
				
				func prepare()
				{
					# 反擊ICON
					title @a[tag=sword_dance_target] times 0 10000 0
					title @a[tag=sword_dance_target] title {"text":"1","font":"ui:main"}
					# 切換攻擊預備動作模型
					# 小閃光特效
					scoreboard players set #SWORD_DANCE number 2
				}
				
				func attack()
				{
					execute as @a[tag=sword_dance_target,limit=1] run function ARG(_PATH)damage
					# 切換揮刀模型
					scoreboard players set 00000003-0000-0003-0000-000300000003 life 0
					execute as 00000003-0000-0003-0000-000300000003 store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get @s number
					playsound minecraft:enemy.sword.dance_attack player @a
					
					function ARG(_PATH)end
				}
				
				func damage()
				{
					# 讓 BOSS 穿過玩家 1.5 格
					execute facing entity @s feet positioned as @s rotated ~ 0 run tp 00000001-0000-0001-0000-000100000001 ^ ^ ^1.5 ~ ~
					effect give @s minecraft:instant_damage 1 2 true
				}
				
				func counter()
				{
					# 切換揮刀模型
					execute positioned ~ ~1 ~ run function ARG(_PATH)counter_effect
					playsound minecraft:enemy.sword.clash player @a ~ ~ ~ 3 1 0
					
					function ARG(_PATH)end
				}
				
				func counter_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:sword_clash/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:sword_clash/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 4
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					execute store result score #ZONE number run bossbar get minecraft:zone value
					
					scoreboard players set #REVERT_ACTION number 1
					schedule function ARG(_PATH)revert_action 1s
					scoreboard players set #SWORD_DANCE number 0
					
					title @a[tag=sword_dance_target] title ""
					tag @a remove sword_dance_target
				}
				
				func revert_action()
				{
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)inner_revert_action
					schedule clear ARG(_PATH)revert_action
				}
				
				func inner_revert_action()
				{
					# 換回平常站立姿勢模型
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 2
					data modify entity @s Pose.RightArm set from storage stage:stage1 ZonePose[0]
					data modify entity @s Pose.LeftArm set value [350f,0f,350f]
					
					particle minecraft:cloud ~ ~1 ~ 0.7 0.7 0.7 0.1 100
					particle minecraft:flash ~ ~ ~ 0 0 0 1 1
					
					scoreboard players set #REVERT_ACTION number 0
					scoreboard players set #WING_OPEN number 0
				}
			}
			
			folder slash()
			{
				func cast()
				{
					scoreboard players set #RAND_RESULT number -1
				}
			}
			
			folder flaw()
			{
				func cast()
				{
					
				}
			}
			
			folder tornado()
			{
				func cast()
				{
					
				}
			}
		}
	}
	
	# 至尊魔導
	folder stage2()
	{
		func start()
		{
			bossbar set minecraft:boss name {"translate":"Arch Magician","color":"white","bold":true}
			# tp @a x y z rx ry
			# summon ...
			schedule function ARG(_PATH)skills/loop 7s
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
		}
		
		func main()
		{
			
		}
		
		folder skills()
		{
			func loop()
			{
				schedule function ARG(_PATH)loop 7s
			}
		}
	}
	
	# 深淵恐懼
	folder stage3()
	{
		func start()
		{
			bossbar set minecraft:boss name {"translate":"Root of Fear","color":"white","bold":true}
			# tp @a x y z rx ry
			# summon ...
			schedule function ARG(_PATH)skills/loop 7s
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
		}
		
		func main()
		{
			
		}
		
		folder skills()
		{
			func loop()
			{
				schedule function ARG(_PATH)loop 7s
			}
		}
	}
}