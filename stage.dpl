import utils, loop

```
GROUND_Y = [4.0, 4.0, 4.0]
STAGE_CENTER = [(128, 0), (128, 0), (56.0, 0.0)]
LOBBY_POS = "0 4 0 0 40"
```

namespace stage()
{
	func install()
	{
		scoreboard objectives add death deathCount
		scoreboard objectives add game_id dummy
		scoreboard objectives add bullet_barrier dummy
		scoreboard objectives add special_id dummy
		
		scoreboard players set #PLAYER_MODE number 1
		scoreboard players set #SELECTED_STAGE number 1
		scoreboard players set #ZONE_MAX number 10000
		scoreboard players set #BULLET_BARRIER_MAX number 3000
		# 破壞後5秒回復
		scoreboard players operation #ZONE_RECOVER number = #ZONE_MAX number
		scoreboard players operation #ZONE_RECOVER number /= #100 number
		
		data modify storage stage:stage1 DefaultPose set value [324f,80f,0f]
		data modify storage stage:stage1 ZonePose set value [[280f,350f,40f],[280f,15f,80f],[300f,20f,130f]]
		
		bossbar add boss "BOSS"
		bossbar set minecraft:boss color red
		bossbar add zone {"translate":"Zone"}
		execute store result bossbar minecraft:zone max run scoreboard players get #ZONE_MAX number
		bossbar add bullet_barrier {"translate":"Barrier"}
		bossbar set minecraft:bullet_barrier visible false
		execute store result bossbar minecraft:bullet_barrier max run scoreboard players get #BULLET_BARRIER_MAX number
		bossbar add blind ""
		bossbar set minecraft:blind color purple
		
		# animated java 模型
		function miko:install
		function abyss:install
		function abyss_head:install
		
		```
		raise Warning("TODO: forceload BOSS chunks for anim")
		```
	}
	
	func main()
	{
		execute as @a[scores={player=1..,death=1..}] run function ARG(_PATH)check_end
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/main
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/main
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/main
	}
	
	func check_end()
	{
		scoreboard players reset @s death
		gamemode spectator @s
		execute unless entity @a[scores={player=1..},gamemode=adventure] run function ARG(_PATH)failed
	}
	
	func start_anim()
	{
		gamemode spectator @a
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/start_anim/play
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/start_anim/play
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/start_anim/play
	}
	
	func start()
	{
		# 挑戰計時
		execute store result score #CHALLENGE_BEGIN number run time query gametime
		
		scoreboard players set #GAME_START number 1
		
		# 懶得找為什麼沒重置，再重置一次
		scoreboard players reset #ZONE number
		data modify storage skill:main blind set value ""
		
		# 人數模式設定成指定模式
		scoreboard players operation #PLAYER_MODE number = #PLAYER_MODE_SET number
		
		clear @s
		function main:effect
		function ARG(_PATH)difficulty_effect
		
		scoreboard players set @s player 1
		scoreboard players set @s weapon_mode 1
		item replace entity @s weapon.mainhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		item replace entity @s weapon.offhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		
		gamemode adventure @s
		# execute if score #PLAYER_MODE number matches 3 run gamemode adventure @a[scores={player=2},limit=1]
		# reset beam CD
		data modify storage skill:main beam_bar set value ["4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4"]
		scoreboard players set #BEAM_CD number 10
		schedule function skill:gun/beam/timer/countdown 3s
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/start
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/start
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/start
		
		bossbar set minecraft:boss players @a
		bossbar set minecraft:boss visible true
		
		effect give @a instant_health 1 5 true
		execute as @a[scores={player=2}] run function ARG(_PATH)set_player2
		execute store result score @a[scores={player=1..}] game_id run scoreboard players add #GLOBAL game_id 1
	}
	
	func set_player2()
	{
		clear @s
		function main:effect
		function ARG(_PATH)difficulty_effect
		
		scoreboard players set @s weapon_mode -1
		item replace entity @s weapon.mainhand with minecraft:spyglass{display:{Name:'{"translate":"Supreme Pulse","bold":true,"italic":false}'},weapon:1b}
	}
	
	func difficulty_effect()
	{
		attribute @s minecraft:generic.max_health modifier remove c655e15b-1539-43df-80c9-55feb92b4e2b
		effect clear @s minecraft:regeneration
		
		execute if score #DIFFICULTY number matches 1 run attribute @s minecraft:generic.max_health modifier add c655e15b-1539-43df-80c9-55feb92b4e2b difficulty 10 add
		execute if score #DIFFICULTY number matches ..2 run effect give @s minecraft:regeneration 1000000 1 true
		execute if score #DIFFICULTY number matches 4 run attribute @s minecraft:generic.max_health modifier add c655e15b-1539-43df-80c9-55feb92b4e2b difficulty -14 add
	}
	
	func end()
	{
		# 挑戰計時
		execute store result score #CHALLENGE_END number run time query gametime
		scoreboard players operation #CHALLENGE_END number -= #CHALLENGE_BEGIN number
		
		scoreboard players reset #GAME_START number
		
		bossbar set minecraft:boss visible false
		kill @e[tag=object]
		kill @e[scores={font_animation=0..}]
		clear @a
		execute as @a run function skill:reset_action
		scoreboard players set @a weapon_mode 1
		title @a actionbar ""
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/end
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/end
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/end
		
		effect give @a instant_health 1 5 true
		```
		print(f"tp @a {LOBBY_POS}")
		```
	}
	
	func victory()
	{
		function ARG(_PATH)end
		
		execute if score #SELECTED_STAGE number matches 1 unless score #UNLOCK_STAGE number matches 2.. run function ARG(_PATH)stage2/unlock
		execute if score #SELECTED_STAGE number matches 2 unless score #UNLOCK_STAGE number matches 3.. run function ARG(_PATH)stage3/unlock
		
		title @a times 10 20 10
		title @a title ["",{"text":"FF","font":"space:default"},{"text":"0","font":"ui:main"},{"text":"FF","font":"space:default"},{"translate":"Victory","color":"yellow"}]
		schedule function lobby:return 39t
		
		# 戰鬥資訊
		tellraw @a ""
		execute if score #SELECTED_STAGE number matches 1 run data modify storage stage:main boss_name set value '{"translate":"Sacred Knight"}'
		execute if score #SELECTED_STAGE number matches 2 run data modify storage stage:main boss_name set value '{"translate":"Cursed Miko"}'
		execute if score #SELECTED_STAGE number matches 3 run data modify storage stage:main boss_name set value '{"translate":"Root of Fear"}'
		execute if score #PLAYER_MODE_SET number matches 1 run data modify storage stage:main player_mode set value '{"translate":"Single"}'
		execute if score #PLAYER_MODE_SET number matches 2 run data modify storage stage:main player_mode set value '{"translate":"Switch"}'
		tellraw @a [{"nbt":"boss_name","storage":"stage:main","interpret":true}," - ",{"nbt":"player_mode","storage":"stage:main","interpret":true}]
		scoreboard players operation #CHALLENGE_MINUTE number = #CHALLENGE_END number
		scoreboard players operation #CHALLENGE_MINUTE number /= #1200 number
		scoreboard players operation #CHALLENGE_SECOND number = #CHALLENGE_END number
		scoreboard players operation #CHALLENGE_SECOND number %= #1200 number
		scoreboard players operation #CHALLENGE_MILLI number = #CHALLENGE_SECOND number
		scoreboard players operation #CHALLENGE_SECOND number /= #20 number
		scoreboard players operation #CHALLENGE_MILLI number %= #20 number
		scoreboard players operation #CHALLENGE_MILLI number *= #50 number
		tellraw @a [{"translate":"Challenge Time: "},{"score":{"name":"#CHALLENGE_MINUTE","objective":"number"},"color":"aqua"},":",{"score":{"name":"#CHALLENGE_SECOND","objective":"number"},"color":"aqua"},".",{"score":{"name":"#CHALLENGE_MILLI","objective":"number"},"color":"aqua"}]
	}
	
	func failed()
	{
		function ARG(_PATH)end
		
		title @a times 10 20 10
		title @a title ["",{"text":"FF","font":"space:default"},{"text":"0","font":"ui:main"},{"text":"FF","font":"space:default"},{"translate":"Fail","color":"dark_red"}]
		schedule function lobby:return 39t
	}
	
	func start_tutorial()
	{
		# 懶得找為什麼沒重置，再重置一次
		scoreboard players reset #ZONE number
		data modify storage skill:main blind set value ""
		
		clear @a
		function main:effect
		scoreboard players set @a weapon_mode 1
		
		scoreboard players set #PLAYER_MODE number 1
		
		item replace entity @a weapon.mainhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		item replace entity @a weapon.offhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		
		# reset beam CD
		data modify storage skill:main beam_bar set value ["4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4"]
		scoreboard players set #BEAM_CD number -1
		
		effect give @a instant_health 1 5 true
		```
		raise Warning("TODO: tp all player to tutorial")
		```
		tp @a 0 4 -16 -90 40
	}
	
	func leave_tutorial()
	{
		```
		print(f"tp @a {LOBBY_POS}")
		```
		execute as @a run function skill:reset_action
		
		title @a times 0 10 5
		title @a title {"text":"0","font":"ui:main"}
		schedule function lobby:return 14t
	}
	
	# 真白之刃
	folder stage1()
	{
		folder start_anim()
		{
			func play()
			{
				```
				print(f"execute positioned {STAGE_CENTER[0][0]} {GROUND_Y[0]} {STAGE_CENTER[0][1] - 8} run function ARG(_PATH)summon")
				```
				scoreboard players reset #START_ANIM number
				function ARG(_PATH)loop
			}
			
			func loop()
			{
				scoreboard players add #START_ANIM number 1
				spectate 000000c8-0000-00c8-0000-00c8000000c8 @a
				
				execute if score #START_ANIM number matches ..20 as 000000c8-0000-00c8-0000-00c8000000c8 at @s run tp @s ~ ~0.05 ~
				execute if score #START_ANIM number matches 20 run function ARG(_PATH)swing/play
				execute if score #START_ANIM number matches 45 run function ARG(_PATH)fade
				execute unless score #START_ANIM number matches 65 run schedule function ARG(_PATH)loop
				execute if score #START_ANIM number matches 65 run function ARG(_PATH)end
			}
			
			func end()
			{
				kill 000000c8-0000-00c8-0000-00c8000000c8
				execute as @a[scores={player=1},limit=1] run function stage:start
			}
			
			folder swing()
			{
				func play()
				{
					execute as 000000c8-0000-00c8-0000-00c8000000c8 at @s run tp @s ~ ~ ~2.8 180 20
					data modify entity 00000001-0000-0001-0000-000100000001 ArmorItems[3].tag.CustomModelData set value 2
					data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 14
					execute as 00000001-0000-0001-0000-000100000001 at @s run function stage:stage1/wing/check
					
					execute at 000000c8-0000-00c8-0000-00c8000000c8 run playsound minecraft:enemy.sword.dance_attack player @a ~ ~ ~ 3 1 1
					
					schedule function ARG(_PATH)anim1 2t
				}
				
				func anim1()
				{
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.RightArm set value [166f,150f,0f]
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.LeftArm set value [45f,105f,355f]
					schedule function ARG(_PATH)anim2 2t
				}
				
				func anim2()
				{
					data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 15
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.RightArm set value [245f,120f,0f]
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.LeftArm set value [355f,0f,350f]
					schedule function ARG(_PATH)anim3 2t
				}
				
				func anim3()
				{
					data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 16
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.RightArm set value [324f,80f,0f]
					schedule function ARG(_PATH)end 2t
				}
				
				func end()
				{
					data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 14
				}
			}
			
			func fade()
			{
				title @a times 10 10 5
				title @a title {"text":"0","font":"ui:main"}
			}
			
			func summon()
			{
				summon area_effect_cloud ~ ~-0.37 ~ {Tags:["object"],Duration:2147483647,UUID:[I;100,100,100,100],Passengers:[{id:"minecraft:armor_stand",Invisible:1b,Invulnerable:1b,NoGravity:1b,ShowArms:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:30,CustomPotionColor:-1}}],HandItems:[{id:"minecraft:potion",Count:1b,tag:{CustomModelData:3,CustomPotionColor:-1}},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:4,CustomPotionColor:-1}}],Pose:{RightArm:[87f,150f,0f],LeftArm:[95f,210f,0f]},Tags:["boss","knockback_resist","object"],UUID:[I;1,1,1,1]}]}
				
				summon husk ~ ~-1 ~-1 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;4,4,4,4]}
				summon husk ~ ~-1 ~-1 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;5,5,5,5]}
				summon husk ~ ~-1 ~-1 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;6,6,6,6]}
				summon husk ~ ~-1 ~-1 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;7,7,7,7]}
				summon husk ~ ~-1 ~-1 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;8,8,8,8]}
				summon husk ~ ~-1 ~-1 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;9,9,9,9]}
				effect give @e[type=husk,tag=wing] invisibility 1000000 0 true
				
				# 攝影機
				summon armor_stand ~ ~-1 ~-2.3 {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,Tags:["object"],Rotation:[180f,-3f],UUID:[I;200,200,200,200]}
				spectate 000000c8-0000-00c8-0000-00c8000000c8 @a
			}
		}
		
		func start()
		{
			bossbar set minecraft:boss max 5000
			execute store result bossbar minecraft:boss value run bossbar get minecraft:boss max
			bossbar set minecraft:boss name {"translate":"Sacred Knight","color":"white","bold":true}
			
			bossbar set minecraft:zone players @a
			bossbar set minecraft:zone visible true
			function ARG(_PATH)skills/zone/reset
			```
			print(f"tp @a {STAGE_CENTER[0][0]} {GROUND_Y[0]} {STAGE_CENTER[0][1]} 180 50")
			print(f"execute positioned {STAGE_CENTER[0][0]} {GROUND_Y[0]} {STAGE_CENTER[0][1] - 8} run function ARG(_PATH)summon_boss")
			```
			
			schedule function ARG(_PATH)skills/loop 7s
			schedule function ARG(_PATH)skills/fly_sword/start 2s
			
			scoreboard players reset #LAST_SKILL number
		}
		
		func summon_boss()
		{
			# summon area_effect_cloud ~ ~-0.37 ~ {Tags:["object"],Duration:2147483647,UUID:[I;100,100,100,100],Passengers:[{id:"minecraft:armor_stand",Invisible:1b,Invulnerable:1b,NoGravity:1b,ShowArms:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:2,CustomPotionColor:-1}}],HandItems:[{id:"minecraft:potion",Count:1b,tag:{CustomModelData:14,CustomPotionColor:-1}},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:4,CustomPotionColor:-1}}],Pose:{RightArm:[324f,80f,0f],LeftArm:[355f,0f,350f]},Tags:["boss","knockback_resist","object"],UUID:[I;1,1,1,1]}]}
			summon armor_stand ~ ~0.8 ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,CustomNameVisible:1b,CustomName:'{"text":"0","font":"skill:zone"}',Tags:["effect","object"],UUID:[I;18,18,18,18]}
			summon marker ~ ~ ~ {Tags:["object"],UUID:[I;16,16,16,16]}
			scoreboard players set 00000001-0000-0001-0000-000100000001 number 1
			execute store result score 00000001-0000-0001-0000-000100000001 health run bossbar get minecraft:boss max
			# 飛劍
			summon armor_stand ~2 ~ ~ {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Tags:["effect","object"],UUID:[I;2,2,2,2],Rotation:[180f,0f]}
			# 翅膀裝飾
			# summon husk ~1.63135 ~-1.2 ~-0.76071 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;4,4,4,4]}
			# summon husk ~1.13278 ~-1.1 ~-1.39886 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;5,5,5,5]}
			# summon husk ~0.40491 ~-1 ~-1.75387 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;6,6,6,6]}
			# summon husk ~-0.40491 ~-1 ~-1.75387 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;7,7,7,7]}
			# summon husk ~-1.13278 ~-1.1 ~-1.39886 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;8,8,8,8]}
			# summon husk ~-1.63135 ~-1.2 ~-0.76071 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;9,9,9,9]}
			# effect give @e[type=husk,tag=wing] invisibility 1000000 0 true
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
			schedule clear ARG(_PATH)skills/zone/auto_reset_pose
			schedule clear ARG(_PATH)skills/fly_sword/stop
			schedule clear ARG(_PATH)skills/fly_sword/aim
			schedule clear ARG(_PATH)skills/fly_sword/start
			schedule clear ARG(_PATH)skills/revert_action
			schedule clear ARG(_PATH)skills/slash/reset_cd
			function ARG(_PATH)skills/sword_dance/shutdown
			function ARG(_PATH)skills/flaw/shutdown
			function ARG(_PATH)skills/chop/shutdown
			function ARG(_PATH)skills/sanction/shutdown
			
			bossbar set minecraft:zone visible false
			scoreboard players set #ZONE number 0
			scoreboard players set #ZONE_ACTIVE number 0
			scoreboard players set #ZONE_BREAK number 0
			scoreboard players set #FLYING_SWORD number 0
			scoreboard players set #SWORD_DANCE number 0
			scoreboard players set #WING_OPEN number 0
			scoreboard players set #SLASH_COUNTDOWN number 0
			scoreboard players set #CHOP_COUNTDOWN number 0
			scoreboard players set #BACKSTEP number 0
			scoreboard players set #ANIM_LOCK number 0
			scoreboard players set #REVERT_ACTION number 0
			scoreboard players reset #SLASH_CD number
			
			kill 00000013-0000-0013-0000-001300000013
			
			effect give @a instant_health 1 10 true
		}
		
		func main()
		{
			execute at 00000001-0000-0001-0000-000100000001 positioned ~ ~0.8 ~ run tp 00000012-0000-0012-0000-001200000012 ~ ~ ~
			execute unless score #ANIM_LOCK number matches 1 unless score #REVERT_ACTION number matches 1 unless score #SWORD_DANCE number matches 3 unless score #ZONE_BREAK number matches 1 as 00000064-0000-0064-0000-006400000064 at @s facing entity @p[scores={player=1..},gamemode=adventure] feet run function ARG(_PATH)face_player
			
			execute if score #ZONE number < #ZONE_MAX number run function ARG(_PATH)skills/zone/recover
			execute if score #FLYING_SWORD number matches 1 as 00000002-0000-0002-0000-000200000002 at @s run function ARG(_PATH)skills/fly_sword/move
			
			execute if score #SWORD_DANCE number matches 1..2 as 00000003-0000-0003-0000-000300000003 at @a[tag=boss_target,limit=1] facing entity 00000001-0000-0001-0000-000100000001 eyes run tp @s ~ ~ ~ ~180 0
			execute if score #SWORD_DANCE number matches 1..2 as 00000064-0000-0064-0000-006400000064 at @s run function ARG(_PATH)skills/sword_dance/trace
			
			execute if score #SLASH_COUNTDOWN number matches 1.. as 00000064-0000-0064-0000-006400000064 at @s run function ARG(_PATH)skills/slash/slash_countdown
			execute if score #CHOP_COUNTDOWN number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/chop/chop_countdown
			
			execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)wing/check
			execute if score #WING_OPEN number matches 1..10 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/sword_dance/open_wing/anim
			
			execute if score #WING_OPEN number matches -1 as 0000000e-0000-000e-0000-000e0000000e at @s run function ARG(_PATH)skills/sanction/follow
			execute if score #SANCTION_ANIM number matches 0.. as 00000001-0000-0001-0000-000100000001 run function ARG(_PATH)skills/sanction/hand_anim/run
		}
		
		func face_player()
		{
			tp @s ~ ~ ~ ~ 0
			data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity 00000064-0000-0064-0000-006400000064 Rotation
			execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
			```
			print(f"execute if score #BACKSTEP number matches 1 at @s positioned ~ {GROUND_Y[0] + 1} ~ rotated ~ 0 run function ARG(_PATH)backstep")
			```
		}
		
		func backstep()
		{
			execute if block ^ ^ ^-3 #skill:canpass run tp @s ^ ^-1.37 ^-3
			execute unless block ^ ^ ^-3 #skill:canpass if block ^ ^ ^-2 #skill:canpass run tp @s ^ ^-1.37 ^-2
			execute unless block ^ ^ ^-3 #skill:canpass unless block ^ ^ ^-2 #skill:canpass if block ^ ^ ^-1 #skill:canpass run tp @s ^ ^-1.37 ^-1
			
			data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity 00000064-0000-0064-0000-006400000064 Rotation
			execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1

			scoreboard players set #BACKSTEP number 0
		}
		
		folder wing()
		{
			func check()
			{
				execute if score #WING_OPEN number matches 0 run function ARG(_PATH)normal
				execute if score #WING_OPEN number matches 11.. run function ARG(_PATH)fly
			}
			
			func normal()
			{
				execute rotated ~65 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000004-0000-0004-0000-000400000004 ~ ~-0.5 ~ ~ -60
				execute rotated ~39 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000005-0000-0005-0000-000500000005 ~ ~-0.4 ~ ~ -60
				execute rotated ~13 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000006-0000-0006-0000-000600000006 ~ ~-0.3 ~ ~ -60
				execute rotated ~-13 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000007-0000-0007-0000-000700000007 ~ ~-0.3 ~ ~ -60
				execute rotated ~-39 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000008-0000-0008-0000-000800000008 ~ ~-0.4 ~ ~ -60
				execute rotated ~-65 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000009-0000-0009-0000-000900000009 ~ ~-0.5 ~ ~ -60
			}
			
			func fly()
			{
				execute rotated ~60 ~ positioned ^ ^ ^-0.7 facing entity @s feet run tp 00000004-0000-0004-0000-000400000004 ~ ~1.3 ~ ~ 50
				execute rotated ~45 ~ positioned ^ ^ ^-1.3 facing entity @s feet run tp 00000005-0000-0005-0000-000500000005 ~ ~0.5 ~ ~ 10
				execute rotated ~30 ~ positioned ^ ^ ^-1.6 facing entity @s feet run tp 00000006-0000-0006-0000-000600000006 ~ ~0.2 ~ ~ -10
				execute rotated ~-30 ~ positioned ^ ^ ^-1.6 facing entity @s feet run tp 00000007-0000-0007-0000-000700000007 ~ ~0.2 ~ ~ -10
				execute rotated ~-45 ~ positioned ^ ^ ^-1.3 facing entity @s feet run tp 00000008-0000-0008-0000-000800000008 ~ ~0.5 ~ ~ 10
				execute rotated ~-60 ~ positioned ^ ^ ^-0.7 facing entity @s feet run tp 00000009-0000-0009-0000-000900000009 ~ ~1.3 ~ ~ 50
			}
		}
		
		folder skills()
		{
			func loop()
			{
				```
				# 有要後退先後退
				print(f"execute if score #BACKSTEP number matches 1 as 00000064-0000-0064-0000-006400000064 at @s rotated ~ 0 positioned ~ {GROUND_Y[0] + 1} ~ run function ARG(__PATH)backstep")
				```
				execute if score #REVERT_ACTION number matches 1 run function ARG(_PATH)revert_action
				
				schedule function ARG(_PATH)loop 5s
				execute if score #ZONE_ACTIVE number matches 1 unless score #SWORD_DANCE number matches 1.. as 00000001-0000-0001-0000-000100000001 at 00000064-0000-0064-0000-006400000064 positioned ~ ~0.37 ~ run function ARG(_PATH)check_slash
			}
			
			func check_slash()
			{
				scoreboard players set #SLASH_USED number 0
				execute unless score #SLASH_CD number matches 1 if score @s health matches ..2500 run function ARG(_PATH)slash/cast
				execute if score #SLASH_USED number matches 0 run function ARG(_PATH)rand_skill
			}
			
			func rand_skill()
			{
				function random:rand_rate
				
				scoreboard players set #NEAR_SKILL number 0
				execute if entity @a[scores={player=1..},gamemode=adventure,distance=..5,limit=1] run scoreboard players add #NEAR_SKILL number 1
				execute if entity @a[scores={player=1..},gamemode=adventure,distance=5.01..,limit=1] run scoreboard players add #NEAR_SKILL number 2
				
				execute if score #NEAR_SKILL number matches 1 run function ARG(_PATH)near_judge
				execute if score #NEAR_SKILL number matches 2 run function ARG(_PATH)far_judge
				execute unless score #NEAR_SKILL number matches 1..2 run function ARG(_PATH)rand_range
			}
			
			func rand_range()
			{
				execute if score #RAND_RESULT number matches ..49 run function ARG(_PATH)near_judge
				execute if score #RAND_RESULT number matches 50.. run function ARG(_PATH)far_judge
				# 重骰一次避免隨機距離跟放的招相關
				function random:rand_rate
			}
			
			func near_judge()
			{
				# 近距離有機率3秒就放下一招
				execute if score #RAND_RESULT number matches 30..69 run schedule function ARG(_PATH)loop 3s
				
				execute if score #RAND_RESULT number matches 0..49 run function ARG(_PATH)flaw/cast
				execute if score #RAND_RESULT number matches 50..99 run function ARG(_PATH)chop/cast
			}
			
			func far_judge()
			{
				# 劍舞不連放
				execute if score #LAST_SKILL number matches 1 run scoreboard players set #RAND_RESULT number 99
				
				execute if score #RAND_RESULT number matches 0..29 run function ARG(_PATH)sword_dance/cast
				execute if score #RAND_RESULT number matches 30..99 run function ARG(_PATH)sanction/cast
			}
			
			func reset_pose()
			{
				data modify entity @s ArmorItems[3].tag.CustomModelData set value 2
				data modify entity @s HandItems[0].tag.CustomModelData set value 14
				data modify entity @s Pose.RightArm set from storage stage:stage1 DefaultPose
				data modify entity @s Pose.LeftArm set value [355f,0f,350f]
			}
				
			func revert_action()
			{
				execute if score #ZONE_ACTIVE number matches 1 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)inner_revert_action
				
				scoreboard players set #REVERT_ACTION number 0
				scoreboard players set #WING_OPEN number 0
				scoreboard players set #SWORD_DANCE number 0
				schedule clear ARG(_PATH)revert_action
			}
			
			func inner_revert_action()
			{
				```
				print(f"tp 00000064-0000-0064-0000-006400000064 ~ {GROUND_Y[0] - 0.37} ~")
				```
				data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity 00000064-0000-0064-0000-006400000064 Rotation
				execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
				
				function ARG(_PATH)reset_pose
				# data modify entity @s NoGravity set value 0b
				
				# particle minecraft:cloud ~ ~1 ~ 0.7 0.7 0.7 0.1 100
				particle minecraft:flash ~ ~1 ~ 0 0 0 1 1
			}
			
			folder dodge()
			{
				func try()
				{
					scoreboard players set #DODGE_SUCCESS number 0
					function ARG(_PATH)rand_side
					
					execute if score #DODGE_SIDE number matches 0 positioned ^1.8 ^ ^ if block ~ ~1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SIDE number matches 1 positioned ^-1.8 ^ ^ if block ~ ~1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SIDE number matches 0 if score #DODGE_SUCCESS number matches 0 positioned ^1.8 ^ ^ if block ~ ~1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SIDE number matches 1 if score #DODGE_SUCCESS number matches 0 positioned ^-1.8 ^ ^ if block ~ ~1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SUCCESS number matches 0 run function ARG(_PATH)random_tp
					
					scoreboard players set #IGNORE_DAMAGE number 1
				}
				func rand_side() from random().generate(0, 1, "#DODGE_SIDE number");
				
				func tp()
				{
					scoreboard players set #DODGE_SUCCESS number 1
					tp 00000064-0000-0064-0000-006400000064 ~ ~-0.37 ~
					execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
				}
				
				func random_tp()
				{
					```
					print(f"spreadplayers ~ ~ 0 7 under {int(GROUND_Y[0] + 3)} false 00000064-0000-0064-0000-006400000064")
					```
					execute at @s unless entity @e[type=armor_stand,tag=blast_target,distance=..2,limit=1] unless entity @a[distance=..5,limit=1] run scoreboard players set #DODGE_SUCCESS number 1
					execute if score #DODGE_SUCCESS number matches 1 as 00000064-0000-0064-0000-006400000064 at @s run tp @s ~ ~-0.37 ~
					execute if score #DODGE_SUCCESS number matches 1 store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
					execute if score #DODGE_SUCCESS number matches 0 run function ARG(_PATH)random_tp
				}
			}
			
			folder zone()
			{
				func recover()
				{
					execute if score #FAST_RECOVER number matches 1.. run scoreboard players remove #FAST_RECOVER number 1
					
					execute if score #ZONE_BREAK number matches 1 run scoreboard players operation #ZONE number += #ZONE_RECOVER number
					execute unless score #ZONE_BREAK number matches 1 run scoreboard players add #ZONE number 3
					execute unless score #ZONE_BREAK number matches 1 unless score #FAST_RECOVER number matches 1.. run scoreboard players add #ZONE number 22
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number < #ZONE_MAX number
					
					execute if score #ZONE_BREAK number matches 1 if score #ZONE number >= #ZONE_MAX number run function ARG(_PATH)reset
				}
				
				func reset()
				{
					bossbar set minecraft:zone color white
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number = #ZONE_MAX number
					
					execute if score #ZONE_BREAK number matches 1 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(__PATH)inner_revert_action
					execute if score #ZONE_BREAK number matches 1 run function ARG(__PATH)fly_sword/aim
					data modify entity 00000012-0000-0012-0000-001200000012 CustomNameVisible set value 1b
					
					scoreboard players set #ZONE_ACTIVE number 1
					scoreboard players set #ZONE_BREAK number 0
				}
				
				func hit()
				{
					execute if score #DIFFICULTY number matches 1 run scoreboard players set #FAST_RECOVER number 100
					execute if score #DIFFICULTY number matches 2 run scoreboard players set #FAST_RECOVER number 60
					execute if score #DIFFICULTY number matches 3 run scoreboard players set #FAST_RECOVER number 40
					execute if score #DIFFICULTY number matches 4 run scoreboard players set #FAST_RECOVER number 30
					# 子彈影響快速回復的時間較少
					execute if score @s display_number matches ..50 run scoreboard players operation #FAST_RECOVER number /= #2 number
					
					execute if score #REVERT_ACTION number matches 1 run function ARG(__PATH)revert_action
					
					execute if entity @s[tag=beam_hit] run function ARG(__PATH)dodge/try
					execute if entity @s[tag=beam_hit] run function ARG(__PATH)reset_pose
					execute if entity @s[tag=!beam_hit] run function ARG(_PATH)decrease
					scoreboard players set #IGNORE_DAMAGE number 1
				}
				
				func decrease()
				{
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number -= @s display_number
					execute if score @s display_number matches ..110 run playsound minecraft:enemy.sword.strike_small player @a ~ ~ ~ 3 1 0
					execute if score @s display_number matches 111.. run playsound minecraft:enemy.sword.strike_big player @a ~ ~ ~ 3 1 0
					
					data modify entity @s HandItems[0].tag.CustomModelData set value 3
					data modify entity @s Pose.RightArm set from storage stage:stage1 ZonePose[0]
					data modify storage stage:stage1 ZonePose append from storage stage:stage1 ZonePose[0]
					data remove storage stage:stage1 ZonePose[0]
					
					execute positioned ^ ^0.5 ^0.7 run function ARG(_PATH)strike_effect
					execute if score #ZONE number matches ..0 run function ARG(_PATH)break
					
					execute if score #ZONE_ACTIVE number matches 1 run schedule function ARG(_PATH)auto_reset_pose 2s
				}
				
				func auto_reset_pose()
				{
					execute if score #ZONE_ACTIVE number matches 1 unless score #ANIM_LOCK number matches 1 as 00000001-0000-0001-0000-000100000001 run function ARG(__PATH)reset_pose
				}
				
				func break()
				{
					```
					print(f"tp 00000064-0000-0064-0000-006400000064 ~ {GROUND_Y[0] - 0.82} ~")
					print("execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1")
					
					print(f"execute positioned ~ {GROUND_Y[0] + 0.35} ~ run function ARG(_PATH)zone_break_effect")
					print(f"execute positioned ~ {GROUND_Y[0]} ~ run function ARG(_PATH)zone_expand_effect")
					```
					data modify entity 00000012-0000-0012-0000-001200000012 CustomNameVisible set value 0b
					
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 9
					data modify entity @s Pose.RightArm set value [87f,150f,0f]
					data modify entity @s Pose.LeftArm set value [95f,210f,0f]
					# data modify entity @s NoGravity set value 1b
					particle flash ~ ~1 ~ 0 0 0 1 2
					playsound minecraft:enemy.sword.strike_big player @a ~ ~ ~ 3 1 0
					
					bossbar set minecraft:zone color blue
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ZONE number 0
					scoreboard players set #ZONE_BREAK number 1
					
					schedule clear ARG(_PATH)auto_reset_pose
					function ARG(__PATH)fly_sword/pause
					function ARG(__PATH)sanction/shutdown
				}
				
				func strike_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:strike/0"}',CustomNameVisible:1,Duration:4,Tags:['{"text":"","font":"font_animation:strike/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func zone_break_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:zone_break/0"}',CustomNameVisible:1,Duration:4,Tags:['{"text":"","font":"font_animation:zone_break/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func zone_expand_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {UUID:[I;19,19,19,19],CustomName:'{"text":"0","font":"font_animation:zone_expand/9"}',CustomNameVisible:1,Duration:101,Tags:['{"text":"","font":"font_animation:zone_expand/9"}', '{"text":"","font":"font_animation:zone_expand/8"}', '{"text":"","font":"font_animation:zone_expand/7"}', '{"text":"","font":"font_animation:zone_expand/6"}', '{"text":"","font":"font_animation:zone_expand/5"}', '{"text":"","font":"font_animation:zone_expand/4"}', '{"text":"","font":"font_animation:zone_expand/3"}', '{"text":"","font":"font_animation:zone_expand/2"}', '{"text":"","font":"font_animation:zone_expand/1"}', '{"text":"","font":"font_animation:zone_expand/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 100
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
			}
			
			folder fly_sword()
			{
				func move()
				{
					# tp 看起來會有延遲，讓他攻擊晚一點到
					execute at @s as @a[scores={player=1..},gamemode=adventure,distance=..1,tag=!sword_hit] unless score @s beamtime matches 0.. run function ARG(_PATH)hit
					tp @s ^ ^ ^-0.5
					execute at @s unless block ^ ^ ^-0.5 #skill:canpass run function ARG(_PATH)stop
				}
				
				func hit()
				{
					effect give @s minecraft:instant_damage 1 0 true
					tag @s add sword_hit
				}
				
				func stop()
				{
					schedule clear ARG(_PATH)stop
					tag @a remove sword_hit
					
					scoreboard players set #FLYING_SWORD number 0
					schedule function ARG(_PATH)aim 5t
				}
				
				func aim()
				{
					execute at 00000002-0000-0002-0000-000200000002 as @p[scores={player=1..},gamemode=adventure] run function ARG(_PATH)aim_effect
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)start 15t
					execute if score #DIFFICULTY number matches 2.. run schedule function ARG(_PATH)start 10t
				}
				
				func aim_effect()
				{
					execute facing entity @s eyes run tp 00000002-0000-0002-0000-000200000002 ~ ~ ~ ~180 0
					# 一次移動最多10格，有在範圍內再提示就好
					execute at @s[distance=..10] run function ARG(_PATH)summon_hint
					execute as 0000000f-0000-000f-0000-000f0000000f at @s facing entity 00000002-0000-0002-0000-000200000002 feet run tp @s ~ ~ ~ ~ 0
				}
				
				func summon_hint()
				{
					summon marker ~ ~ ~ {UUID:[I;21,21,21,21]}
					execute as 00000015-0000-0015-0000-001500000015 facing entity 00000002-0000-0002-0000-000200000002 feet run tp @s ~ ~ ~ ~ 0
					execute store result score #ROTATION_X number run data get entity 00000015-0000-0015-0000-001500000015 Rotation[0]
					kill 00000015-0000-0015-0000-001500000015
					```
					for i in range(-7, 8):
						print('execute if score #ROTATION_X number matches %d..%d run summon armor_stand ~ %.2f ~ {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:17}}],Tags:["object","effect"],UUID:[I;15,15,15,15],Rotation:[%df,0f]}' %(i*24-11, i*24+12, GROUND_Y[0], i*24+60))
					```
				}
				
				func start()
				{
					kill 0000000f-0000-000f-0000-000f0000000f
					scoreboard players set #FLYING_SWORD number 1
					schedule function ARG(_PATH)stop 1s
				}
				
				# 暫時中斷飛劍使用
				func pause()
				{
					kill 0000000f-0000-000f-0000-000f0000000f
					scoreboard players set #FLYING_SWORD number 0
					
					schedule clear ARG(_PATH)start
					schedule clear ARG(_PATH)aim
					schedule clear ARG(_PATH)stop
				}
			}
			
			folder sword_dance()
			{
				func cast()
				{
					scoreboard players set #LAST_SKILL number 1
					# 暫時關閉ZONE，延後快速回復
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players add #FAST_RECOVER number 15
					execute as @r[scores={player=1..},gamemode=adventure,limit=1,distance=5..] at @s run function ARG(_PATH)mark
					execute facing entity @a[tag=boss_target,limit=1] feet run tp 00000064-0000-0064-0000-006400000064 ~ ~-0.37 ~ ~ 0
					
					scoreboard players set #ANIM_LOCK number 1
					function ARG(_PATH)open_wing/start
				}
				
				func mark()
				{
					# 頭上標記特效
					execute positioned ~ ~1.5 ~ run function ARG(_PATH)mark_effect
					summon minecraft:armor_stand ~ ~ ~ {UUID:[I;3,3,3,3],Invisible:1,NoGravity:1,Invulnerable:1,Marker:1,Fire:100s,Tags:["object","effect","slash","skill_effect"],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:26}}]}
					scoreboard players set 00000003-0000-0003-0000-000300000003 number 32
					scoreboard players set 00000003-0000-0003-0000-000300000003 life -100000
					tag @s add boss_target
				}
				
				func mark_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:mark/1"}',CustomNameVisible:1,Duration:11,Tags:['{"text":"","font":"font_animation:mark/1"}', '{"text":"","font":"font_animation:mark/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 10
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				folder open_wing()
				{
					func start()
					{
						scoreboard players set #WING_OPEN number 1
						function stage:stage1/wing/fly
						
						tp 00000064-0000-0064-0000-006400000064 ~ ~-0.82 ~
						data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity 00000064-0000-0064-0000-006400000064 Rotation
						execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
						# 計算右手姿勢
						execute store result score #R_ARM_X number run data get entity @s Pose.RightArm[0] 10
						execute store result score #R_ARM_Y number run data get entity @s Pose.RightArm[1] 10
						execute store result score #R_ARM_Z number run data get entity @s Pose.RightArm[2] 10
						scoreboard players set #R_ARM_DIFF_X number 2400
						scoreboard players set #R_ARM_DIFF_Y number 3500
						scoreboard players set #R_ARM_DIFF_Z number 800
						scoreboard players operation #R_ARM_DIFF_X number -= #R_ARM_X number
						scoreboard players operation #R_ARM_DIFF_Y number -= #R_ARM_Y number
						scoreboard players operation #R_ARM_DIFF_Z number -= #R_ARM_Z number
						scoreboard players operation #R_ARM_DIFF_X number /= #5 number
						scoreboard players operation #R_ARM_DIFF_Y number /= #5 number
						scoreboard players operation #R_ARM_DIFF_Z number /= #5 number
						# 計算左手姿勢 (-5->70, 0->30, --)
						scoreboard players set #L_ARM_X number -5
						scoreboard players set #L_ARM_Y number 0
						scoreboard players set #L_ARM_DIFF_X number 15
						scoreboard players set #L_ARM_DIFF_Y number 6
						
						function ARG(_PATH)anim
					}
					
					func anim()
					{
						execute if score #WING_OPEN number matches 1..5 run function ARG(_PATH)calc_pose
						execute if score #WING_OPEN number matches 2 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 6
						execute if score #WING_OPEN number matches 4 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 7
						execute if score #WING_OPEN number matches 10 run function ARG(_PATH)end
						
						scoreboard players add #WING_OPEN number 1
					}
					
					func calc_pose()
					{
						# 右手
						execute store result entity @s Pose.RightArm[0] float 0.1 run scoreboard players operation #R_ARM_X number += #R_ARM_DIFF_X number
						execute store result entity @s Pose.RightArm[1] float 0.1 run scoreboard players operation #R_ARM_Y number += #R_ARM_DIFF_Y number
						execute store result entity @s Pose.RightArm[2] float 0.1 run scoreboard players operation #R_ARM_Z number += #R_ARM_DIFF_Z number
						# 左手
						execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
						execute store result entity @s Pose.LeftArm[1] float 1 run scoreboard players operation #L_ARM_Y number += #L_ARM_DIFF_Y number
					}
					
					func end()
					{
						particle minecraft:explosion ~ ~ ~ 0 0 0 3 5
						playsound minecraft:entity.wither.shoot player @a ~ ~ ~ 3 1.3 0
						
						data modify entity @s ArmorItems[3].tag.CustomModelData set value 8
						data modify entity @s NoGravity set value 1
						scoreboard players set #SWORD_DANCE number 1
					}
				}
				
				func trace()
				{
					execute if score #DIFFICULTY number matches 1 facing entity @a[tag=boss_target,limit=1] feet run tp @s ^ ^ ^0.55 ~ ~
					execute if score #DIFFICULTY number matches 2..3 facing entity @a[tag=boss_target,limit=1] feet run tp @s ^ ^ ^0.7 ~ ~
					execute if score #DIFFICULTY number matches 4 facing entity @a[tag=boss_target,limit=1] feet run tp @s ^ ^ ^0.8 ~ ~
					
					data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity @s Rotation
					execute store result entity @s Air short 1 run scoreboard players add #AIR number 1
					
					# 約 8 tick 的反應時間
					execute if score #SWORD_DANCE number matches 2 at @s positioned ~ ~0.37 ~ if entity @a[tag=boss_target,distance=..1,limit=1] run function ARG(_PATH)attack
					execute if score #SWORD_DANCE number matches 1 at @s positioned ~ ~0.37 ~ if entity @a[tag=boss_target,distance=..7,limit=1] run function ARG(_PATH)prepare
					
					# 追蹤過程持續延後快速回復
					scoreboard players add #FAST_RECOVER number 1
				}
				
				func prepare()
				{
					# 反擊ICON
					title @a[tag=boss_target] times 0 10000 0
					title @a[tag=boss_target] title {"text":"1","font":"ui:main"}
					# 切換攻擊預備動作模型
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.RightArm set value [260f,350f,100f]
					particle flash ~ ~1 ~ 0 0 0 1 1
					
					scoreboard players set #SWORD_DANCE number 2
				}
				
				func attack()
				{
					scoreboard players set #SWORD_DANCE number 3
					
					execute as @a[tag=boss_target,limit=1] run function ARG(_PATH)damage
					# 切換揮刀模型
					function ARG(_PATH)sword_wave/start
					
					scoreboard players set 00000003-0000-0003-0000-000300000003 life 0
					execute as 00000003-0000-0003-0000-000300000003 store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get @s number
					playsound minecraft:enemy.sword.dance_attack player @a ~ ~ ~ 3 1 0
					
					function ARG(_PATH)end
				}
				
				func damage()
				{
					# 讓 BOSS 穿過玩家 3 格
					execute facing entity @s feet positioned as @s run function ARG(_PATH)set_boss_motion
					execute unless score @s beamtime matches 0.. run effect give @s minecraft:instant_damage 1 2 true
					execute at @s run particle flash ~ ~1 ~ 0 0 0 1 1
				}
				
				func set_boss_motion()
				{
					# 設定穿過去後的動能
					# execute positioned 0 0 0 run summon marker ^ ^ ^1.5 {UUID:[I;0,3,0,3]}
					# data modify entity 00000001-0000-0001-0000-000100000001 Motion set from entity 00000000-0000-0003-0000-000000000003 Pos
					# kill 00000000-0000-0003-0000-000000000003
					
					execute positioned ~ ~2 ~ if block ^ ^ ^3 #skill:canpass positioned ~ ~-2 ~ run tp 00000064-0000-0064-0000-006400000064 ^ ^ ^3 ~ ~
					execute positioned ~ ~2 ~ unless block ^ ^ ^3 #skill:canpass if block ^ ^ ^2 #skill:canpass positioned ~ ~-2 ~ run tp 00000064-0000-0064-0000-006400000064 ^ ^ ^2 ~ ~
					execute positioned ~ ~2 ~ unless block ^ ^ ^3 #skill:canpass unless block ^ ^ ^2 #skill:canpass if block ^ ^ ^2 #skill:canpass positioned ~ ~-1 ~ run tp 00000064-0000-0064-0000-006400000064 ^ ^ ^1 ~ ~
					
					data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity 00000064-0000-0064-0000-006400000064 Rotation
					execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
				}
				
				func counter()
				{
					scoreboard players set #SWORD_DANCE number 3
					# 切換揮刀模型
					execute at @a[tag=boss_target,limit=1] facing entity @s feet rotated ~ 0 run function ARG(_PATH)set_counter_effect
					function ARG(_PATH)sword_wave/start
					schedule function stage:stage1/skills/loop 11t
					
					scoreboard players set 00000003-0000-0003-0000-000300000003 life 0
					scoreboard players set 00000003-0000-0003-0000-000300000003 number 23
					execute as 00000003-0000-0003-0000-000300000003 store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get @s number
				}
				
				folder sword_wave()
				{
					func start()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 ArmorItems[3].tag.CustomModelData set value 7
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 15
						data modify entity 00000001-0000-0001-0000-000100000001 Pose.RightArm set value [30f,350f,100f]
					
						title @a[tag=boss_target] title ""
						tag @a remove boss_target
						
						schedule function ARG(_PATH)anim1 2t
					}
					
					func anim1()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 16
						schedule function ARG(_PATH)anim2 2t
					}
					
					func anim2()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 14
						schedule function ARG(__PATH)end 1t
					}
				}
				
				func set_counter_effect()
				{
					tp 00000064-0000-0064-0000-006400000064 ^ ^-0.37 ^1 ~180 ~
					data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity @s Rotation
					execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
					
					execute positioned ^ ^1 ^0.5 run function ARG(_PATH)counter_effect
					execute positioned ^ ^1 ^0.5 run playsound minecraft:enemy.sword.clash player @a ~ ~ ~ 3 1 0
				}
				
				func counter_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:sword_clash/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:sword_clash/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 4
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					
					# data modify entity 00000001-0000-0001-0000-000100000001 NoGravity set value 0b
					# ``
					# print(f"execute as 00000001-0000-0001-0000-000100000001 at @s positioned ~ 0.0 ~ if entity @s[dx=0,dy={GROUND_Y[0] - 1},dz=0] run tp @s ~ {GROUND_Y[0]} ~")
					# ``
					scoreboard players set #REVERT_ACTION number 1
					schedule function ARG(__PATH)revert_action 1s
					schedule clear ARG(__PATH)zone/auto_reset_pose
					scoreboard players set #BACKSTEP number 1
					scoreboard players set #ANIM_LOCK number 0
				}
				
				func shutdown()
				{
					schedule clear ARG(_PATH)sword_wave/anim1
					schedule clear ARG(_PATH)sword_wave/anim2
					schedule clear ARG(_PATH)end
				}
			}
			
			folder slash()
			{
				func cast()
				{
					scoreboard players set #SLASH_USED number 1
					scoreboard players set #LAST_SKILL number 2
					scoreboard players set #RAND_RESULT number -1
					# 暫時關閉ZONE
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ANIM_LOCK number 1
					scoreboard players set #WING_OPEN number 11
					# 準備姿勢
					data modify entity @s HandItems[0].tag.CustomModelData set value 3
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 10
					data modify entity @s Pose.RightArm set value [300f,330f,346f]
					data modify entity @s Pose.LeftArm set value [45f,205f,15f]
					data modify entity @s NoGravity set value 1
					# 腳邊旋風 & 身邊閃電特效
					playsound minecraft:enemy.sword.slash_wind player @a ~ ~ ~ 3 1 0
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,UUID:[I;11,11,11,11],ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:13}}],Tags:["object","effect"]}
					
					# 計算右手姿勢 (300->260, --, 346->367)
					scoreboard players set #R_ARM_X number 3000
					scoreboard players set #R_ARM_DIFF_X number -80
					scoreboard players set #R_ARM_Z number 3460
					scoreboard players set #R_ARM_DIFF_Z number 42
					# 計算左手姿勢 (45->70, --, --)
					scoreboard players set #L_ARM_X number 45
					scoreboard players set #L_ARM_DIFF_X number 5
					
					execute as @r[scores={player=1..},gamemode=adventure] at @s run function ARG(_PATH)mark
					function ARG(_PATH)rand_slash_time
					# 過程中不要快速回復
					scoreboard players operation #FAST_RECOVER number += #SLASH_COUNTDOWN number
					
					execute if score #DIFFICULTY number matches 1..2 run scoreboard players set #SLASH_HINT_TIME number 18
					execute if score #DIFFICULTY number matches 3 run scoreboard players set #SLASH_HINT_TIME number 16
					execute if score #DIFFICULTY number matches 4 run scoreboard players set #SLASH_HINT_TIME number 14
					
					# 設定 CD
					scoreboard players set #SLASH_CD number 1
					schedule function ARG(_PATH)reset_cd 30s
				}
				
				func reset_cd()
				{
					scoreboard players reset #SLASH_CD number
				}
				
				func rand_slash_time() from random().generate(40, 50, "#SLASH_COUNTDOWN number");
				
				func mark()
				{
					tag @s add boss_target
					```
					print('summon armor_stand ~ %.1f ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,UUID:[I;10,10,10,10],Tags:["object","effect"],Passengers:[{id:"minecraft:item",Item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:11,CustomPotionColor:-1}},PickupDelay:32767,HasVisualFire:1b,UUID:[I;17,17,17,17],Tags:["object","effect"]}]}' %GROUND_Y[0])
					```
				}
				
				func slash_countdown()
				{
					execute facing entity @a[tag=boss_target,limit=1] feet run function stage:stage1/face_player
					
					execute if score #SLASH_COUNTDOWN number matches 2..6 run function ARG(_PATH)calc_pose
					execute if score #SLASH_COUNTDOWN number = #SLASH_HINT_TIME number run function ARG(_PATH)set_hint
					```
					print(f"execute if score #SLASH_COUNTDOWN number > #SLASH_HINT_TIME number at @a[tag=boss_target,limit=1] positioned ~ {GROUND_Y[0]} ~ run tp 0000000a-0000-000a-0000-000a0000000a ~ ~ ~ ~ 0")
					```
					
					scoreboard players remove #SLASH_COUNTDOWN number 1
					execute if score #SLASH_COUNTDOWN number matches 0 run function ARG(_PATH)end
				}
				
				func set_hint()
				{
					data modify entity 00000011-0000-0011-0000-001100000011 Item.tag.CustomPotionColor set value 16711680
					playsound minecraft:enemy.sword.slash_hint player @a ~ ~ ~ 3 1 0
				}
				
				func calc_pose()
				{
					# 右手
					execute store result entity 00000001-0000-0001-0000-000100000001 Pose.RightArm[0] float 0.1 run scoreboard players operation #R_ARM_X number += #R_ARM_DIFF_X number
					execute store result entity 00000001-0000-0001-0000-000100000001 Pose.RightArm[2] float 0.1 run scoreboard players operation #R_ARM_Z number += #R_ARM_DIFF_Z number
					# 左手
					execute store result entity 00000001-0000-0001-0000-000100000001 Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					scoreboard players set #ANIM_LOCK number 0
					scoreboard players set #REVERT_ACTION number 1
					schedule function ARG(__PATH)revert_action 1s
					schedule clear ARG(__PATH)zone/auto_reset_pose
					
					# 更換劈砍姿勢模型
					data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 14
					data modify entity 00000001-0000-0001-0000-000100000001 ArmorItems[3].tag.CustomModelData set value 12
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.RightArm set value [350f,350f,320f]
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.LeftArm set value [350f,200f,30f]
					
					particle minecraft:explosion ~ ~ ~ 0 0 0 3 5
					kill 0000000b-0000-000b-0000-000b0000000b
					execute as 0000000a-0000-000a-0000-000a0000000a at @s run function ARG(_PATH)slash
					execute facing entity 0000000a-0000-000a-0000-000a0000000a feet rotated ~ 0 positioned as 0000000a-0000-000a-0000-000a0000000a run tp @s ~ ~-0.37 ~ ~ ~
					execute at @s run function ARG(_PATH)move/run
					data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity 00000064-0000-0064-0000-006400000064 Rotation
					execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
					
					kill 0000000a-0000-000a-0000-000a0000000a
					kill 00000011-0000-0011-0000-001100000011
					tag @a remove boss_target
				}
			
				folder move() from for_loop().for(8, "if block ^ ^ ^0.5 #skill:canpass positioned ^ ^ ^0.5")
				{
					func execute()
					{
						tp @s ~ ~ ~
					}
				}
				
				func slash()
				{
					playsound minecraft:enemy.sword.slash player @a ~ ~ ~ 3 1 0
					execute positioned ~ ~1 ~ run function ARG(_PATH)slash_effect
					execute as @a[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. if predicate stage:slash_range run function ARG(_PATH)damage
					
					schedule function ARG(_PATH)activate_shader/run 3t
				}
				
				func slash_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:slash/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:slash/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 4
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func damage()
				{
					function skill:reset_action
					# scoreboard players reset @s player
					# clear @s
					
					team join blue @s
					effect give @s minecraft:glowing 1000000 0 true
					tag @s add already_dead
				}
				
				folder activate_shader()
				{
					func run()
					{
						execute as @a[tag=already_dead] run function ARG(_PATH)set
						schedule function ARG(_PATH)reset 1s
					}
					
					func set()
					{
						effect give @s minecraft:night_vision 1000000 0 true
						team leave @s
						# tp @s 小黑屋
					}
					
					func reset()
					{
						execute as @a[tag=already_dead] run function ARG(_PATH)player_reset
					}
					
					func player_reset()
					{
						effect clear @s minecraft:night_vision
						effect clear @s minecraft:glowing
						effect give @s instant_damage 1 2 true
						tag @s remove already_dead
					}
				}
			}
			
			folder flaw()
			{
				func cast()
				{
					scoreboard players set #LAST_SKILL number 3
					# 暫時關閉ZONE
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ANIM_LOCK number 1
					
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.RightArm set value [240f,350f,80f]
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.LeftArm set value [70f,30f,0f]
					data modify entity 00000001-0000-0001-0000-000100000001 ArmorItems[3].tag.CustomModelData set value 7
					data modify entity 00000001-0000-0001-0000-000100000001 NoGravity set value 1
					```
					print('execute at @r[scores={player=1..},gamemode=adventure,distance=..5,limit=1] positioned ~ %.2f ~ run function ARG(_PATH)positioned_effect' %GROUND_Y[0])
					```
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)attack 17t
					execute if score #DIFFICULTY number matches 2..3 run schedule function ARG(_PATH)attack 15t
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)attack 13t
				}
				
				func positioned_effect()
				{
					summon area_effect_cloud ~ ~-0.37 ~ {Duration:1000000,Tags:["object"],UUID:[I;101,101,101,101],Passengers:[{id:"minecraft:armor_stand",Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,UUID:[I;12,12,12,12],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:35}}],Tags:["object","effect","slash","skill_effect"]}]}
					scoreboard players set 0000000c-0000-000c-0000-000c0000000c number 36
					scoreboard players set 0000000c-0000-000c-0000-000c0000000c life -100000
					
					execute as 00000064-0000-0064-0000-006400000064 rotated ~ 0 run function ARG(_PATH)tp_back
					
					tp 00000065-0000-0065-0000-006500000065 ~ ~-0.37 ~ ~180 0
					data modify entity 0000000c-0000-000c-0000-000c0000000c Rotation set from entity 00000065-0000-0065-0000-006500000065 Rotation
					execute store result entity 00000065-0000-0065-0000-006500000065 Air short 1 run scoreboard players add #AIR number 1
				}
				
				func tp_back()
				{
					execute if block ^ ^ ^-3 #skill:canpass run tp @s ^ ^-0.37 ^-3 ~ 0
					execute unless block ^ ^ ^-3 #skill:canpass if block ^ ^ ^-2 #skill:canpass run tp @s ^ ^-0.37 ^-2 ~ 0
					execute unless block ^ ^ ^-3 #skill:canpass unless block ^ ^ ^-2 #skill:canpass if block ^ ^ ^-1 #skill:canpass run tp @s ^ ^-0.37 ^-1 ~ 0
					execute unless block ^ ^ ^-3 #skill:canpass unless block ^ ^ ^-2 #skill:canpass unless block ^ ^ ^-1 #skill:canpass run tp @s ^ ^-0.37 ^-0.5 ~ 0
					
					data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity 00000064-0000-0064-0000-006400000064 Rotation
					execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
				}
				
				func attack()
				{
					scoreboard players set 0000000c-0000-000c-0000-000c0000000c life 0
					execute as 0000000c-0000-000c-0000-000c0000000c store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get @s number
					
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)attack_effect
					execute as 00000064-0000-0064-0000-006400000064 at 0000000c-0000-000c-0000-000c0000000c facing entity @s feet rotated ~ 0 run function ARG(_PATH)attack_move
					execute as @a[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. at 0000000c-0000-000c-0000-000c0000000c if predicate stage:flaw_range run effect give @s instant_damage 1 0 true
				}
				
				func attack_move()
				{
					execute if block ^ ^ ^0.8 #skill:canpass run tp @s ^ ^-0.37 ^0.8 ~180 ~
					execute unless block ^ ^ ^0.8 #skill:canpass if block ^ ^ ^0.4 #skill:canpass run tp @s ^ ^-0.37 ^0.4 ~180 ~
					execute unless block ^ ^ ^0.8 #skill:canpass unless block ^ ^ ^0.4 #skill:canpass run tp @s ~ ~-0.37 ~ ~180 ~
					
					execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
				}
				
				func attack_effect()
				{
					function ARG(_PATH)sword_wave/start
					playsound minecraft:skill.rush_attack player @a ~ ~ ~ 3 1 0
				}
				
				folder sword_wave()
				{
					func start()
					{
						data modify entity @s ArmorItems[3].tag.CustomModelData set value 7
						data modify entity @s HandItems[0].tag.CustomModelData set value 15
						data modify entity @s Pose.RightArm set value [30f,350f,100f]
						
						schedule function ARG(_PATH)anim1 2t
					}
					
					func anim1()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 16
						schedule function ARG(_PATH)anim2 2t
					}
					
					func anim2()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 14
						schedule function ARG(__PATH)end 1t
					}
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					scoreboard players set #REVERT_ACTION number 1
					scoreboard players set #BACKSTEP number 1
					schedule function ARG(__PATH)revert_action 1s
					schedule clear ARG(__PATH)zone/auto_reset_pose
					scoreboard players set #ANIM_LOCK number 0
					
					kill 00000065-0000-0065-0000-006500000065
				}
				
				func shutdown()
				{
					schedule clear ARG(_PATH)attack
					schedule clear ARG(_PATH)sword_wave/anim1
					schedule clear ARG(_PATH)sword_wave/anim2
					schedule clear ARG(_PATH)end
				}
			}
			
			folder chop()
			{
				func cast()
				{
					scoreboard players set #LAST_SKILL number 4
					# 劈砍有機率接破綻
					execute if score #RAND_RESULT number matches 50..74 run function ARG(_PATH)follow_skill
					
					# 暫時關閉ZONE
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ANIM_LOCK number 1
					# 準備姿勢
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 10
					data modify entity @s HandItems[0].tag.CustomModelData set value 3
					data modify entity @s Pose.RightArm set value [300f,330f,346f]
					data modify entity @s Pose.LeftArm set value [45f,205f,15f]
					data modify entity @s NoGravity set value 1
					# 計算右手姿勢 (300->260, --, 346->367)
					scoreboard players set #R_ARM_X number 3000
					scoreboard players set #R_ARM_DIFF_X number -80
					scoreboard players set #R_ARM_Z number 3460
					scoreboard players set #R_ARM_DIFF_Z number 42
					# 計算左手姿勢 (45->70, --, --)
					scoreboard players set #L_ARM_X number 45
					scoreboard players set #L_ARM_DIFF_X number 5
					
					execute if score #DIFFICULTY number matches 1 run scoreboard players set #CHOP_COUNTDOWN number 16
					execute if score #DIFFICULTY number matches 2..3 run scoreboard players set #CHOP_COUNTDOWN number 14
					execute if score #DIFFICULTY number matches 4 run scoreboard players set #CHOP_COUNTDOWN number 12
					
					# 攻擊半徑只有三格，所以三格內沒玩家就要往前一點再攻擊(近距離最遠5格)
					execute unless entity @a[scores={player=1..},gamemode=adventure,distance=..3,limit=1] facing entity @p[scores={player=1..},gamemode=adventure] feet rotated ~ 0 positioned ^ ^ ^2 run function ARG(_PATH)set_target
					execute unless entity 0000000d-0000-000d-0000-000d0000000d facing entity @p[scores={player=1..},gamemode=adventure] feet rotated ~ 0 run function ARG(_PATH)set_target
				}
				
				func set_target()
				{
					tp 00000064-0000-0064-0000-006400000064 ~ ~-0.37 ~ ~ ~
					data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity 00000064-0000-0064-0000-006400000064 Rotation
					execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
					
					summon armor_stand ^ ^ ^1.5 {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,UUID:[I;13,13,13,13],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:39}}],Tags:["object","effect"]}
				}
				
				func follow_skill()
				{
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)cast_follow_skill 22t
					execute if score #DIFFICULTY number matches 2..3 run schedule function ARG(_PATH)cast_follow_skill 20t
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)cast_follow_skill 18t
					
					schedule function ARG(__PATH)loop 120t
					schedule clear ARG(__PATH)revert_action
					scoreboard players set #REVERT_ACTION number 0
				}
				
				func cast_follow_skill()
				{
					execute as 00000001-0000-0001-0000-000100000001 at @s if entity @a[scores={player=1..},gamemode=adventure,distance=..5,limit=1] run function ARG(__PATH)flaw/cast
				}
				
				func chop_countdown()
				{
					execute if score #CHOP_COUNTDOWN number matches 2..6 run function ARG(_PATH)calc_pose
					
					scoreboard players remove #CHOP_COUNTDOWN number 1
					execute if score #CHOP_COUNTDOWN number matches 0 run function ARG(_PATH)attack
				}
				
				func calc_pose()
				{
					# 右手
					execute store result entity @s Pose.RightArm[0] float 0.1 run scoreboard players operation #R_ARM_X number += #R_ARM_DIFF_X number
					execute store result entity @s Pose.RightArm[2] float 0.1 run scoreboard players operation #R_ARM_Z number += #R_ARM_DIFF_Z number
					# 左手
					execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
				}
				
				func attack()
				{
					execute at 0000000d-0000-000d-0000-000d0000000d facing entity @s feet rotated ~180 0 run tp 00000064-0000-0064-0000-006400000064 ^ ^-0.52 ^-1
					data modify entity 00000001-0000-0001-0000-000100000001 Rotation set from entity 00000064-0000-0064-0000-006400000064 Rotation
					execute store result entity 00000064-0000-0064-0000-006400000064 Air short 1 run scoreboard players add #AIR number 1
					# 更換劈砍姿勢模型
					function ARG(_PATH)sword_wave/start
					playsound minecraft:skill.basic2 player @a ~ ~ ~ 3 1 0
					
					execute at 0000000d-0000-000d-0000-000d0000000d as @a[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. if predicate stage:chop_range run effect give @s instant_damage 1 1 true
					kill 0000000d-0000-000d-0000-000d0000000d
				}
				
				folder sword_wave()
				{
					func start()
					{
						data modify entity @s ArmorItems[3].tag.CustomModelData set value 18
						data modify entity @s HandItems[0].tag.CustomModelData set value 15
						data modify entity @s Pose.RightArm set value [330f,350f,310f]
						data modify entity @s Pose.LeftArm set value [100f,220f,300f]
						
						schedule function ARG(_PATH)anim1 2t
					}
					
					func anim1()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 16
						schedule function ARG(_PATH)anim2 2t
					}
					
					func anim2()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 14
						schedule function ARG(__PATH)end 1t
					}
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					scoreboard players set #ANIM_LOCK number 0
					scoreboard players set #BACKSTEP number 1
					scoreboard players set #REVERT_ACTION number 1
					schedule function ARG(__PATH)revert_action 1s
					schedule clear ARG(__PATH)zone/auto_reset_pose
				}
				
				func shutdown()
				{
					schedule clear ARG(_PATH)cast_follow_skill
					schedule clear ARG(_PATH)sword_wave/anim1
					schedule clear ARG(_PATH)sword_wave/anim2
					schedule clear ARG(_PATH)end
				}
			}
			
			folder sanction()
			{
				func cast()
				{
					scoreboard players set #LAST_SKILL number 5
					
					function ARG(_PATH)flip
					```
					print(f"execute if score #SANCTION_FLIP number matches 0 as @r[scores={{player=1..}},gamemode=adventure] at @s positioned ~ {GROUND_Y[0] + 0.7} ~ run function ARG(_PATH)surround")
					print(f"execute if score #SANCTION_FLIP number matches 1 as @r[scores={{player=1..}},gamemode=adventure] at @s positioned ~ {GROUND_Y[0] - 0.7} ~ run function ARG(_PATH)surround")
					```
					execute if score #DIFFICULTY number matches 1 run function ARG(_PATH)schedule_sting/easy
					execute if score #DIFFICULTY number matches 2..3 run function ARG(_PATH)schedule_sting/normal
					execute if score #DIFFICULTY number matches 4 run function ARG(_PATH)schedule_sting/hell
					
					function ARG(_PATH)hand_anim/raise/start
				}
				
				folder schedule_sting()
				{
					func easy()
					{
						execute if score #SANCTION_FLIP number matches 0 run schedule function ARG(__PATH)sting 25t
						execute if score #SANCTION_FLIP number matches 1 run schedule function ARG(__PATH)sting 23t
					}
					
					func normal()
					{
						execute if score #SANCTION_FLIP number matches 0 run schedule function ARG(__PATH)sting 20t
						execute if score #SANCTION_FLIP number matches 1 run schedule function ARG(__PATH)sting 18t
					}
					
					func hell()
					{
						execute if score #SANCTION_FLIP number matches 0 run schedule function ARG(__PATH)sting 19t
						execute if score #SANCTION_FLIP number matches 1 run schedule function ARG(__PATH)sting 17t
					}
				}
				
				# 原則上左手平常都是擺著(放技能才會動，但是技能之間不重疊)
				folder hand_anim()
				{
					func run()
					{
						scoreboard players add #SANCTION_ANIM number 1
						
						execute if score #SANCTION_ANIM number matches 1..5 run function ARG(_PATH)raise/loop
						execute if score #SANCTION_ANIM number matches 11..12 run function ARG(_PATH)swing/loop
					}
					
					folder raise()
					{
						func start()
						{
							# 5 tick (355->265, -, 350->360)
							scoreboard players set #SANCTION_ANIM number 0
							
							scoreboard players set #L_ARM_X number 355
							scoreboard players set #L_ARM_Z number 350
							
							scoreboard players set #L_ARM_DIFF_X number -18
							scoreboard players set #L_ARM_DIFF_Z number 2
						}
						
						func loop()
						{
							execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
							execute store result entity @s Pose.LeftArm[2] float 1 run scoreboard players operation #L_ARM_Z number += #L_ARM_DIFF_Z number
							
							execute if score #SANCTION_ANIM number matches 5 run scoreboard players reset #SANCTION_ANIM number
						}
					}
					
					folder swing()
					{
						func start()
						{
							# 2 tick (265->275, 360->300, -)
							scoreboard players set #SANCTION_ANIM number 10
							
							scoreboard players set #L_ARM_X number 265
							scoreboard players set #L_ARM_Y number 360
							
							scoreboard players set #L_ARM_DIFF_X number 5
							scoreboard players set #L_ARM_DIFF_Y number -30
						}
						
						func loop()
						{
							execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
							execute store result entity @s Pose.LeftArm[1] float 1 run scoreboard players operation #L_ARM_Y number += #L_ARM_DIFF_Y number
							
							execute if score #SANCTION_ANIM number matches 12 run scoreboard players reset #SANCTION_ANIM number
						}
					}
				}
				
				func flip() from random().generate(0, 1, "#SANCTION_FLIP number");
				
				func surround()
				{
					tag @s add boss_target
					
					playsound minecraft:enemy.sword.sanction_prepare player @a ~ ~ ~ 3 1 0
					scoreboard players set #WING_OPEN number -1
					summon marker ~ ~0.7 ~ {UUID:[I;14,14,14,14],Tags:["object"]}
					
					execute as 0000000e-0000-000e-0000-000e0000000e at @s run function ARG(_PATH)set_sword
				}
				
				func set_sword()
				{
					execute rotated ~30 0 run tp 00000004-0000-0004-0000-000400000004 ^ ^ ^4 ~ ~
					execute rotated ~90 0 run tp 00000005-0000-0005-0000-000500000005 ^ ^ ^4 ~ ~
					execute rotated ~150 0 run tp 00000006-0000-0006-0000-000600000006 ^ ^ ^4 ~ ~
					execute rotated ~210 0 run tp 00000007-0000-0007-0000-000700000007 ^ ^ ^4 ~ ~
					execute rotated ~270 0 run tp 00000008-0000-0008-0000-000800000008 ^ ^ ^4 ~ ~
					execute rotated ~330 0 run tp 00000009-0000-0009-0000-000900000009 ^ ^ ^4 ~ ~
					tp @s ~ ~ ~ ~4 ~
				}
				
				func follow()
				{
					execute as @a[tag=boss_target,limit=1] run function ARG(_PATH)horizonal_move
					execute at @s run function ARG(_PATH)set_sword
				}
				
				func horizonal_move()
				{
					data modify entity 0000000e-0000-000e-0000-000e0000000e Pos[0] set from entity @s Pos[0]
					data modify entity 0000000e-0000-000e-0000-000e0000000e Pos[2] set from entity @s Pos[2]
				}
				
				func sting()
				{
					execute as @e[type=husk,tag=wing] at @s run tp @s ^ ^ ^-8
					execute as 0000000e-0000-000e-0000-000e0000000e at @s run function ARG(_PATH)effect
					scoreboard players set #WING_OPEN number -2
					tag @a remove boss_target
					
					# 制裁有機率連放下一招
					function random:rand_rate
					execute if score #RAND_RESULT number matches ..49 run schedule function ARG(__PATH)loop 21t
					
					function ARG(_PATH)hand_anim/swing/start
					schedule function ARG(_PATH)reset_wing 1s
				}
				
				func effect()
				{
					playsound minecraft:enemy.sword.sanction player @a ~ ~ ~ 3 1 0
					execute as @a[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. if predicate stage:sanction_range positioned ~-10 ~0.2 ~-10 if entity @s[dx=20,dy=0,dz=20] positioned ~ ~-0.8 ~ if entity @s[dx=20,dy=0,dz=20] run effect give @s instant_damage 1 1 true
					kill @s
				}
				
				func reset_wing()
				{
					scoreboard players set #WING_OPEN number 0
					# 順便重置左手位置
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.LeftArm set value [355f,0f,350f]
				}
				
				func shutdown()
				{
					kill 0000000e-0000-000e-0000-000e0000000e
					tag @a remove boss_target
					
					scoreboard players reset #SANCTION_ANIM number
					scoreboard players set #WING_OPEN number 0
					schedule clear ARG(_PATH)sting
					schedule clear ARG(_PATH)reset_wing
				}
			}
		}
	}
	
	# 呪縛巫女
	folder stage2()
	{
		func unlock()
		{
			scoreboard players set #UNLOCK_STAGE number 2
			data modify storage lobby:main miko set value "2"
		}
		
		folder start_anim()
		{
			func play()
			{
				```
				print(f"execute positioned {STAGE_CENTER[1][0]} {GROUND_Y[1]} {STAGE_CENTER[1][1] - 8} run function ARG(_PATH)summon")
				```
				scoreboard players reset #START_ANIM number
				function ARG(_PATH)loop
			}
			
			func loop()
			{
				scoreboard players add #START_ANIM number 1
				spectate 000000c8-0000-00c8-0000-00c8000000c8 @a
				
				execute if score #START_ANIM number matches 10 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)animate
				execute if score #START_ANIM number matches 11..30 as 000000c8-0000-00c8-0000-00c8000000c8 at @s run tp @s ~ ~ ~-0.05 ~ ~-1.25
				execute if score #START_ANIM number matches 45 run function ARG(_PATH)fade
				execute unless score #START_ANIM number matches 65 run schedule function ARG(_PATH)loop
				execute if score #START_ANIM number matches 65 run function ARG(_PATH)end
			}
			
			func animate()
			{
				particle firework ~ ~2 ~ 0 0 0 0.2 30
				particle flash ~ ~2 ~ 0 0 0 1 1
				playsound minecraft:enemy.miko.sanka_shoot player @a ~ ~ ~ 3 1 1
				playsound minecraft:enemy.miko.swoosh player @a ~ ~ ~ 3 1 1
				
				function miko:set_variant/default
				function miko:animations/animation.miko.start_anim/resume
			}
			
			func end()
			{
				execute as 00000001-0000-0001-0000-000100000001 at @s run function miko:animations/animation.miko.start_anim/stop
				kill 000000c8-0000-00c8-0000-00c8000000c8
				execute as @a[scores={player=1},limit=1] run function stage:start
			}
			
			func fade()
			{
				title @a times 10 10 5
				title @a title {"text":"0","font":"ui:main"}
			}
			
			func summon()
			{
				function miko:summon/default
				execute as 00000001-0000-0001-0000-000100000001 run function miko:set_variant/invisible
				execute as 00000001-0000-0001-0000-000100000001 at @s run function miko:animations/animation.miko.start_anim/play
				execute as 00000001-0000-0001-0000-000100000001 at @s run function miko:animations/animation.miko.start_anim/pause
				
				# 攝影機
				summon armor_stand ~ ~-1 ~-1.5 {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,Tags:["object"],Rotation:[180f,40f],UUID:[I;200,200,200,200]}
				spectate 000000c8-0000-00c8-0000-00c8000000c8 @a
			}
		}
		
		func start()
		{
			bossbar set minecraft:boss max 50000
			execute store result bossbar minecraft:boss value run bossbar get minecraft:boss max
			bossbar set minecraft:boss name {"translate":"Cursed Miko","color":"white","bold":true}
			
			bossbar set minecraft:bullet_barrier players @a
			```
			print(f"tp @a {STAGE_CENTER[1][0]} {GROUND_Y[1]} {STAGE_CENTER[1][1]} 180 50")
			print(f"execute positioned {STAGE_CENTER[1][0]} {GROUND_Y[1]} {STAGE_CENTER[1][1] - 8} rotated 0 0 run function ARG(_PATH)summon_boss")
			```
			schedule function ARG(_PATH)skills/loop 5s
			
			schedule function ARG(_PATH)skills/dust/summon_loop 8s
			execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)skills/dust/attack 12s
			execute if score #DIFFICULTY number matches 2..3 run schedule function ARG(_PATH)skills/dust/attack 11.5s
			execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)skills/dust/attack 11s
			
			# 可攻擊的子彈編號從 10 開始，前面保留給骷髏跟分身
			scoreboard players set #BULLET_ID number 10
		}
		
		func summon_boss()
		{
			# function miko:summon/default
			tag 00000001-0000-0001-0000-000100000001 add boss
			tag 00000001-0000-0001-0000-000100000001 remove new_miko
			tag 00000001-0000-0001-0000-000100000001 remove effect
			
			scoreboard players set 00000001-0000-0001-0000-000100000001 number 1
			execute store result score 00000001-0000-0001-0000-000100000001 health run bossbar get minecraft:boss max
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
			schedule clear ARG(_PATH)skills/dust/summon_loop
			schedule clear ARG(_PATH)skills/dust/attack
			schedule clear ARG(_PATH)skills/dust/explode
			schedule clear ARG(_PATH)skills/dust/real_summon
			schedule clear ARG(_PATH)skills/flame/circle/summon
			schedule clear ARG(_PATH)skills/flame/bullet/shoot
			schedule clear ARG(_PATH)skills/thunder/strike
			schedule clear ARG(_PATH)skills/sacrifice/clear_levitation
			schedule clear ARG(_PATH)skills/sanka/sakura_draw
			schedule clear ARG(_PATH)skills/sanka/call_teleport
			schedule clear ARG(_PATH)skills/sikigami/reset_cd
			schedule clear ARG(_PATH)skills/sikigami/call_effect
			schedule clear ARG(_PATH)skills/sikigami/call_show_up
			
			scoreboard players reset #SIKIGAMI_CD number
			scoreboard players reset #FLAME_BULLET number
			scoreboard players reset #ANIM_LOCK number
			scoreboard players reset #FACING_TIMER number
			scoreboard players reset #EXIST_CLONE number
			scoreboard players reset #BARRIER_CHARGE number
			
			bossbar set minecraft:bullet_barrier visible false
			
			function miko:remove/all
		}
		
		func main()
		{
			execute as @e[type=armor_stand] run function ARG(_PATH)armor_stand_main
			execute as @e[type=marker,tag=dead_dust] at @s run function ARG(_PATH)skills/dust/dead_dust/move
			execute as 00000004-0000-0004-0000-000400000004 at @s run function ARG(_PATH)skills/sacrifice/check
			execute as 00000007-0000-0007-0000-000700000007 at @s run function ARG(_PATH)skills/hell/check
			execute as 00000009-0000-0009-0000-000900000009 run function ARG(_PATH)skills/sikigami/taiji/main
		}
		
		func armor_stand_main()
		{
			execute if entity @s[tag=miko] at @s facing entity @p[scores={player=1..},gamemode=adventure] feet run function ARG(_PATH)face_player
			execute if entity @s[tag=flame_circle] at @s run function ARG(_PATH)skills/flame/circle/spin
			execute at @s[tag=flame_bullet] run function ARG(_PATH)skills/flame/bullet/move
			execute at @s[tag=sanka_bullet] run function ARG(_PATH)skills/sanka/bullet/move
		}
		
		func face_player()
		{
			scoreboard players add #FACING_TIMER number 1
			tp @s ~ ~ ~ ~ 0
			execute if score #FACING_TIMER number matches 2 run function ARG(_PATH)facing_anim
		}
		
		func facing_anim()
		{
			execute unless score #ANIM_LOCK number matches 1.. run function miko:move
			execute if score #ANIM_LOCK number matches 2 run function miko:animations/animation.miko.flame_loop/play
			scoreboard players reset #FACING_TIMER number
		}
		
		folder skills()
		{
			func loop()
			{
				schedule function ARG(_PATH)loop 5s
				execute if score #EXIST_CLONE number matches 1.. as @e[type=armor_stand,tag=miko] at @s run function ARG(_PATH)flame/cast
				execute unless score #EXIST_CLONE number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)check_sikigami
			}
			
			func check_sikigami()
			{
				scoreboard players set #SIKIGAMI_USED number 0
				execute unless score #SIKIGAMI_CD number matches 1 if score @s health matches ..15000 run function ARG(_PATH)sikigami/cast
				execute unless score #SIKIGAMI_USED number matches 1 run function ARG(_PATH)rand_skill
			}
			
			func rand_skill()
			{
				function random:rand_rate
				
				scoreboard players set #NEAR_SKILL number 0
				execute if entity @a[scores={player=1..},gamemode=adventure,distance=..6,limit=1] run scoreboard players add #NEAR_SKILL number 1
				execute if entity @a[scores={player=1..},gamemode=adventure,distance=6.01..,limit=1] run scoreboard players add #NEAR_SKILL number 2
				
				execute if score #NEAR_SKILL number matches 1 run function ARG(_PATH)near_judge
				execute if score #NEAR_SKILL number matches 2 run function ARG(_PATH)far_judge
				execute unless score #NEAR_SKILL number matches 1..2 run function ARG(_PATH)rand_range
			}
			
			func rand_range()
			{
				# 近距離招權重比較大，因為威脅性比較高
				execute if score #RAND_RESULT number matches ..69 run function ARG(_PATH)near_judge
				execute if score #RAND_RESULT number matches 70.. run function ARG(_PATH)far_judge
				# 重骰一次避免隨機距離跟放的招相關
				function random:rand_rate
			}
			
			func near_judge()
			{
				execute if score @s health matches ..35000 if score #RAND_RESULT number matches 40..59 unless entity 00000007-0000-0007-0000-000700000007 run function ARG(_PATH)hell/cast
				
				execute if score #RAND_RESULT number matches 0..49 run function ARG(_PATH)sacrifice/cast
				execute if score #RAND_RESULT number matches 50..99 run function ARG(_PATH)sanka/cast
			}
			
			func far_judge()
			{
				execute if score @s health matches ..35000 if score #RAND_RESULT number matches 40..59 unless entity 00000007-0000-0007-0000-000700000007 run function ARG(_PATH)hell/cast
				
				execute if score #RAND_RESULT number matches 0..49 run function ARG(_PATH)flame/cast
				execute if score #RAND_RESULT number matches 50..99 run function ARG(_PATH)thunder/cast
			}
			
			folder dust()
			{
				func summon_loop()
				{
					```
					print(f"execute at @r[scores={{player=1..}},gamemode=adventure] positioned ~ {GROUND_Y[1] + 0.55} ~ run function ARG(_PATH)summon")
					```
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)summon_loop 8s
					execute if score #DIFFICULTY number matches 2..3 run schedule function ARG(_PATH)summon_loop 7s
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)summon_loop 6s
				}
				
				func summon()
				{
					# 向上飄出的生成特效
					summon armor_stand ~ ~-3 ~ {DisabledSlots:4144959,Invulnerable:1b,Invisible:1b,NoGravity:1b,CustomNameVisible:1b,CustomName:'{"text":"1","font":"skill:main"}',Tags:["object","effect"],UUID:[I;2,2,2,2]}
					tp 00000002-0000-0002-0000-000200000002 ~ ~-1.5 ~
					schedule function ARG(_PATH)real_summon 8t
				}
				
				func real_summon()
				{
					execute at 00000002-0000-0002-0000-000200000002 run summon area_effect_cloud ~ ~1.5 ~ {Duration:2100000000,CustomNameVisible:1b,CustomName:'{"text":"1","font":"skill:main"}',Tags:["object","dust","flame_target"]}
					kill 00000002-0000-0002-0000-000200000002
				}
				
				func attack()
				{
					execute as @e[type=area_effect_cloud,tag=dust] at @s run function ARG(_PATH)prepare_attack
					schedule function ARG(_PATH)explode 15t
					
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)attack 8s
					execute if score #DIFFICULTY number matches 2..3 run schedule function ARG(_PATH)attack 7s
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)attack 6s
				}
				
				func prepare_attack()
				{
					data modify entity @s CustomNameVisible set value 0b
					function ARG(_PATH)dust_prepare_effect
				}
				
				func dust_prepare_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:dust_prepare_attack/1"}',CustomNameVisible:1,Duration:38,Tags:['{"text":"","font":"font_animation:dust_prepare_attack/1"}', '{"text":"","font":"font_animation:dust_prepare_attack/0"}', '{"text":"","font":"font_animation:dust_prepare_attack/3"}', '{"text":"","font":"font_animation:dust_prepare_attack/2"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 37
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] special_id 1
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func explode()
				{
					kill @e[type=area_effect_cloud,scores={special_id=1}]
					execute as @e[type=area_effect_cloud,tag=dust] at @s run function ARG(_PATH)explode_effect
				}
				
				func explode_effect()
				{
					data modify entity @s CustomNameVisible set value 1b
					
					summon armor_stand ~ ~-0.8 ~ {Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,HasVisualFire:1,Tags:["object","effect","slash","skill_effect"],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:56}}]}
					particle minecraft:explosion ~ ~0.5 ~ 0 0 0 1 3
					playsound minecraft:enemy.miko.dust_explode player @a ~ ~ ~ 1 1 0
					execute as @a[gamemode=adventure,scores={player=1..},distance=..3] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 0 true
				}
				
				folder dead_dust()
				{
					func summon()
					{
						playsound minecraft:enemy.miko.dust_die player @a ~ ~ ~ 1 1 0
						summon marker ~ ~-0.5 ~ {Tags:["object","dead_dust","new_dead_dust"]}
						execute as @e[type=marker,tag=new_dead_dust,limit=1] at @s run function ARG(_PATH)setting
					}
					
					func setting()
					{
						execute facing entity 00000001-0000-0001-0000-000100000001 feet run tp @s ~ ~ ~ ~ 90
						tag @s remove new_dead_dust
					}
					
					func move()
					{
						# 要讓路徑密集一點，又想移動快一點，所以 1 tick 移動兩次
						function ARG(_PATH)step
						execute at @s run function ARG(_PATH)step
						execute at 00000001-0000-0001-0000-000100000001 positioned ~-0.6 ~ ~-0.6 if entity @s[dx=0.2,dy=1,dz=0.2] run function ARG(_PATH)charge
					}
					
					func step()
					{
						particle minecraft:squid_ink ~ ~1 ~ 0.1 0.1 0.1 0 1
						execute positioned ^ ^ ^-0.4 facing entity 00000001-0000-0001-0000-000100000001 feet positioned ^ ^ ^0.12 facing entity @s feet run tp @s ~ ~ ~ ~ ~
					}
					
					func charge()
					{
						execute as 00000001-0000-0001-0000-000100000001 unless score @s bullet_barrier matches 1.. at @s run function ARG(_PATH)do_charge
						kill @s
					}
					
					func do_charge()
					{
						execute positioned ~ ~0.3 ~ run function ARG(_PATH)charge_effect
						scoreboard players add #BARRIER_CHARGE number 1
						execute if score #BARRIER_CHARGE number matches 10 run function ARG(_PATH)spawn_barrier
					}
					
					func spawn_barrier()
					{
						execute store result bossbar minecraft:bullet_barrier value run scoreboard players operation @s bullet_barrier = #BULLET_BARRIER_MAX number
						bossbar set minecraft:bullet_barrier visible true
						scoreboard players set #BARRIER_CHARGE number 0
						# 生成防護罩特效
						particle flash ~ ~0.3 ~ 0 0 0 1 1
						summon area_effect_cloud ~ ~0.3 ~ {Duration:2147483647,Tags:["object","barrier"],CustomNameVisible:1b,CustomName:'{"text":"4","font":"skill:main"}'}
					}
					
					func charge_effect()
					{
						summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:barrier_charge/0"}',CustomNameVisible:1,Duration:6,Tags:['{"text":"","font":"font_animation:barrier_charge/0"}', 'new_font_anim']}
						scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 5
						tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
					}
				}
			}
			
			folder flame()
			{
				func cast()
				{
					# 總動作時間比較長，延後下次技能時間
					schedule function ARG(__PATH)loop 8s
					
					scoreboard players set #ANIM_LOCK number 1
					function miko:animations/animation.miko.flame/play
					playsound minecraft:enemy.miko.flame_cast player @a ~ ~ ~ 3 1 0
					execute rotated ~ 0 run function ARG(_PATH)circle/summon
					
					execute if score #DIFFICULTY number matches 1 run scoreboard players set #FLAME_BULLET number 16
					execute if score #DIFFICULTY number matches 2..3 run scoreboard players set #FLAME_BULLET number 18
					execute if score #DIFFICULTY number matches 4 run scoreboard players set #FLAME_BULLET number 20
					schedule function ARG(_PATH)bullet/shoot 1s
				}
				
				folder circle()
				{
					func summon()
					{
						# for i in range(-7, 8):
						#	print('execute if entity @s[y_rotation=%.3f..%d] run summon armor_stand ~ ~ ~ {Invisible:1b,NoGravity:1b,Invulnerable:1b,Marker:1b,HasVisualFire:1b,Pose:{Head:[90f,0f,0f]},ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:19,CustomPotionColor:0}}],Tags:["object","effect","flame_circle"],Rotation:[%df,0f]}' %(i*24-11.999, i*24+12, i*24))
						
						summon armor_stand ^ ^ ^0.7 {Invisible:1b,NoGravity:1b,Invulnerable:1b,Marker:1b,HasVisualFire:1b,Pose:{Head:[90f,0f,0f]},ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:19,CustomPotionColor:0}}],Tags:["object","effect","flame_circle"]}
					}
					
					func spin()
					{
						execute at @e[type=armor_stand,tag=miko,limit=1,sort=nearest] rotated ~ 0 run tp @s ^ ^ ^1.2 ~ 0
						
						# shader 抓 CustomPotionColor 控制[放大、透明](藍色)、當前面向(綠色)，並自動旋轉
						execute store result score #SHADER_INFO_COLOR number run data get entity @s Rotation[0] 0.71111
						scoreboard players add #SHADER_INFO_COLOR number 128
						scoreboard players operation #SHADER_INFO_COLOR number *= #256 number
						
						execute unless score @s life matches 250.. unless score @s number matches 1 run scoreboard players add @s life 25
						execute if score @s number matches 1 run scoreboard players remove @s life 50
						execute store result entity @s ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players operation #SHADER_INFO_COLOR number += @s life
						
						execute if score @s life matches 0 run kill @s
					}
				}
				
				folder bullet()
				{
					func shoot()
					{
						scoreboard players remove #FLAME_BULLET number 1
						execute as @e[tag=miko] at @s run function ARG(_PATH)summon
						execute if score #FLAME_BULLET number matches 1.. run schedule function ARG(_PATH)shoot 4t
						execute if score #FLAME_BULLET number matches 0 run function ARG(__PATH)end
					}
					
					func summon()
					{
						# summon armor_stand ^ ^0.7 ^2 {Invisible:1b,NoGravity:1b,Invulnerable:1b,Marker:1b,Health:4.8f,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:40}}],Tags:["object","flame_bullet","dead_event","new_flame"]}
						```
						for i in range(-7, 8):
							print('execute if entity @s[y_rotation=%.3f..%d] run summon armor_stand ^ ^0.7 ^1.9 {Invisible:1b,NoGravity:1b,Invulnerable:1b,Marker:1b,Health:4.8f,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:40}}],Tags:["object","flame_bullet","dead_event","new_flame"],Rotation:[%df,0f]}' %(i*24-11.999, i*24+12, i*24))
						```
						execute as @e[type=armor_stand,tag=new_flame,limit=1] positioned as @s run function ARG(_PATH)setting
					}
					
					func setting()
					{
						execute store result score @s number run scoreboard players add #BULLET_ID number 1
						scoreboard players set @s health 48
						tp @s ~ ~ ~ ~ 0
						tag @s remove new_flame
					}
					
					func move()
					{
						tp @s ^ ^ ^1
						scoreboard players set #EXPLODE number 0
						execute unless block ~ ~ ~ #skill:canpass run scoreboard players set #EXPLODE number 1
						execute unless score #EXPLODE number matches 1 positioned ~-0.5 ~ ~-0.5 if entity @e[type=#stage:flame_target,limit=1,tag=flame_target,dx=0,dy=0,dz=0] run scoreboard players set #EXPLODE number 1
						
						execute if score #EXPLODE number matches 1 run function ARG(_PATH)explode
					}
					
					func explode()
					{
						function ARG(_PATH)burst_effect
						playsound minecraft:enemy.miko.flame_burst player @a ~ ~ ~ 3 1 0
						execute as @e[type=#stage:flame_target,tag=flame_target,distance=..2] run function ARG(_PATH)damage
						kill @s
					}
					
					func burst_effect()
					{
						summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:flame/0"}',CustomNameVisible:1,Duration:11,Tags:['{"text":"","font":"font_animation:flame/0"}', '{"text":"","font":"font_animation:flame/1"}', 'new_font_anim']}
						scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 9
						tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
					}
					
					func damage()
					{
						execute if entity @s[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 0 true
						execute if entity @s[tag=dust] at @s run function stage:stage2/skills/dust/dead_dust/summon
						kill @s[tag=dust]
					}
					
					func bullet_hurt()
					{
						execute store result entity @s Health float 0.1 run scoreboard players get @s health
						execute if score @s health matches ..0 run function ARG(_PATH)explode
					}
				}
				
				func end()
				{
					scoreboard players set @e[type=armor_stand,tag=flame_circle] number 1
					scoreboard players set #ANIM_LOCK number 1
					execute as @e[type=armor_stand,tag=miko] run function miko:animations/animation.miko.flame_loop/stop
					schedule function ARG(_PATH)end_anim 5t
				}
				
				func end_anim()
				{
					execute as @e[type=armor_stand,tag=miko] run function miko:animations/animation.miko.flame_end/play
				}
			}
			
			folder thunder()
			{
				func cast()
				{
					scoreboard players set #ANIM_LOCK number 1
					function miko:animations/animation.miko.thunder/play
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:42}}],Tags:["object","effect"],UUID:[I;3,3,3,3]}
					playsound minecraft:enemy.miko.thunder_prepare player @a ~ ~ ~ 3 1 0
					execute at @a[scores={player=1..},gamemode=adventure] run function ARG(_PATH)summon
					
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)strike 30t
					execute if score #DIFFICULTY number matches 2 run schedule function ARG(_PATH)strike 26t
					execute if score #DIFFICULTY number matches 3 run schedule function ARG(_PATH)strike 23t
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)strike 20t
				}
				
				func summon()
				{
					```
					for i in range(5):
						print('summon marker ~ ~ ~ {Tags:["object","thunder_summon"]}')
					print(f"spreadplayers ~ ~ 4.8 7 under {int(GROUND_Y[1] + 3)} false @e[type=marker,tag=thunder_summon]")
					```
					execute at @e[tag=thunder_summon] run summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:41}}],Tags:["object","effect","thunder"]}
					kill @e[type=marker,tag=thunder_summon]
				}
				
				func strike()
				{
					kill 00000003-0000-0003-0000-000300000003
					execute as @e[type=armor_stand,tag=thunder] at @s run function ARG(_PATH)attack
					execute at 00000001-0000-0001-0000-000100000001 run playsound minecraft:enemy.miko.thunder_strike player @a ~ ~ ~ 16 1 1
					execute as 00000001-0000-0001-0000-000100000001 run function miko:animations/animation.miko.thunder_end/play
				}
				
				func attack()
				{
					execute positioned ~ ~-1 ~ run function ARG(_PATH)thunder_effect
					particle flash ~ ~ ~ 0 0 0 1 1
					execute as @e[type=#stage:flame_target,tag=flame_target] if predicate stage:thunder_range run function ARG(_PATH)damage
					kill @s
				}
					
				func damage()
				{
					execute if entity @s[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 1 true
					execute if entity @s[tag=dust] at @s run function stage:stage2/skills/dust/dead_dust/summon
					kill @s[tag=dust]
				}
				
				func thunder_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:thunder/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:thunder/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
					
					summon armor_stand ~ ~ ~ {Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,HasVisualFire:1,Tags:["object","effect","slash","skill_effect"],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:43}}]}
				}
			}
			
			folder sacrifice()
			{
				func cast()
				{
					playsound minecraft:enemy.miko.swoosh player @a ~ ~ ~ 1 1 0
					scoreboard players set #ANIM_LOCK number 1
					function miko:animations/animation.miko.sacrifice/play
					
					schedule function ARG(_PATH)call_knock_up 15t
				}
				
				func call_knock_up()
				{
					```
					print(f"execute at @p[gamemode=adventure,scores={{player=1..}}] positioned ~ {GROUND_Y[1]} ~ run function ARG(_PATH)knock_up")
					```
				}
				
				func knock_up()
				{
					# 先清掉舊的避免太快連續施放
					function ARG(_PATH)clear_effect
					
					particle flash ~ ~0.2 ~ 0 0 0 1 1
					playsound minecraft:enemy.miko.sacrifice player @a ~ ~ ~ 3 1 0
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:20,CustomPotionColor:41}}],Tags:["object","effect"],UUID:[I;4,4,4,4]}
					summon armor_stand ~ ~-1.9 ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:52}}],Tags:["object","effect"],UUID:[I;5,5,5,5]}
					
					execute as @a[gamemode=adventure,scores={player=1..}] if predicate stage:sacrifice_range run function ARG(_PATH)fly
					schedule function ARG(_PATH)clear_levitation 1t
				}
				
				func fly()
				{
					tp @s @s
					execute if score #DIFFICULTY number matches 1 run effect give @s levitation 1 127 true
					execute if score #DIFFICULTY number matches 2..3 run effect give @s levitation 1 110 true
					execute if score #DIFFICULTY number matches 4 run effect give @s levitation 1 102 true
				}
				
				func clear_levitation()
				{
					effect clear @a levitation
				}
				
				func check()
				{
					scoreboard players add @s life 1
					execute if score @s life matches ..19 as 00000005-0000-0005-0000-000500000005 at @s run tp @s ~ ~0.1 ~
					scoreboard players set #SKULL_BITE number 0
					execute if score @s life matches 25..59 as @a[gamemode=adventure,scores={player=1..}] if predicate stage:sacrifice_range run function ARG(_PATH)hurt
					execute if score #SKULL_BITE number matches 1 run function ARG(_PATH)bite
					execute if score @s life matches 60.. run function ARG(_PATH)sink
					
					execute if score @s life matches 30 as 00000001-0000-0001-0000-000100000001 at @s run function miko:animations/animation.miko.sacrifice_end/play
				}
				
				func hurt()
				{
					execute unless score @s beamtime matches 0.. run effect give @s instant_damage 1 1 true
					scoreboard players set #SKULL_BITE number 1
				}
				
				func bite()
				{
					playsound minecraft:enemy.miko.skeleton_bite player @a ~ ~ ~ 3 1 0
					data modify entity 00000005-0000-0005-0000-000500000005 ArmorItems[3].tag.CustomModelData set value 53
					execute as 00000005-0000-0005-0000-000500000005 at @s run tp @s ~ ~0.5 ~
					execute at 00000005-0000-0005-0000-000500000005 run particle minecraft:crit ~ ~1.2 ~-1.7 1 0 0 1.7 50
					
					execute if score @s life matches ..29 as 00000001-0000-0001-0000-000100000001 at @s run function miko:animations/animation.miko.sacrifice_end/play
					scoreboard players set @s life 59
				}
				
				func sink()
				{
					tp @s ~ ~-0.1 ~
					execute as 00000005-0000-0005-0000-000500000005 at @s run tp @s ~ ~-0.1 ~
					
					execute if score @s life matches 120 run function ARG(_PATH)clear_effect
				}
				
				func clear_effect()
				{
					kill 00000004-0000-0004-0000-000400000004
					kill 00000005-0000-0005-0000-000500000005
				}
			}
			
			folder sanka()
			{
				func cast()
				{
					scoreboard players set #ANIM_LOCK number 1
					playsound minecraft:enemy.miko.sanka_prepare player @a ~ ~ ~ 3 1 0
					function miko:animations/animation.miko.sanka/play
					```
					for i in range(5):
						print('summon armor_stand ~ ~ ~ {Rotation:[%df,0f],Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:46}}],Tags:["object","effect","sakura"]}' %(i * 72))
					```
					scoreboard players set #SAKURA_MODEL number 46
					schedule function ARG(_PATH)sakura_draw 1t
					
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)call_teleport 30t
					execute if score #DIFFICULTY number matches 2..3 run schedule function ARG(_PATH)call_teleport 24t
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)call_teleport 20t
				}
				
				func sakura_draw()
				{
					scoreboard players add #SAKURA_MODEL number 1
					execute as @e[type=armor_stand,tag=sakura] store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get #SAKURA_MODEL number
					
					execute if score #SAKURA_MODEL number matches ..50 run schedule function ARG(_PATH)sakura_draw 1t
				}
				
				func call_teleport()
				{
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)teleport
				}
				
				func teleport()
				{
					schedule clear skill:boss_hurt_reset
					# 隱形，暫時給 effect tag 避免被當攻擊對象
					function miko:animations/animation.miko.sanka/stop
					function miko:set_variant/invisible
					tag @s add effect
					
					kill @e[type=armor_stand,tag=sakura]
					execute if score @s bullet_barrier matches 1.. run kill @e[type=area_effect_cloud,tag=barrier,limit=1]
					
					scoreboard players set #BULLET_SPLIT number 2
					execute positioned ~ ~0.7 ~ run function ARG(_PATH)bullet/explode
					function ARG(_PATH)random_tp
					execute at @s run function miko:move
					
					schedule function ARG(_PATH)call_show_up 10t
				}
				
				func random_tp()
				{
					```
					print(f"spreadplayers {STAGE_CENTER[1][0]} {STAGE_CENTER[1][1]} 0 15 under {int(GROUND_Y[1] + 3)} false @s")
					```
					execute at @s if entity @a[gamemode=adventure,scores={player=1..},distance=..5,limit=1] run function ARG(_PATH)random_tp
				}
				
				func call_show_up()
				{
					scoreboard players reset #ANIM_LOCK number
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)show_up
				}
				
				func show_up()
				{
					# 不知道為什麼距離遠的時候 move 會錯位，再跑一次 move 才會正常
					function miko:move
					function miko:set_variant/default
					tag @s remove effect
					
					particle firework ~ ~1 ~ 0 0 0 0.2 30
					particle flash ~ ~1 ~ 0 0 0 1 1
					
					# 有結界的話補回特效
					execute if score @s bullet_barrier matches 1.. run summon area_effect_cloud ~ ~0.3 ~ {Duration:2147483647,Tags:["object","barrier"],CustomNameVisible:1b,CustomName:'{"text":"4","font":"skill:main"}'}
				}
				
				folder bullet()
				{
					func explode()
					{
						function ARG(_PATH)explode_effect
						execute if score #BULLET_SPLIT number matches 1.. run function ARG(_PATH)spawn
					}
					
					func explode_effect()
					{
						particle firework ~ ~1 ~ 0 0 0 0.2 30
						particle flash ~ ~1 ~ 0 0 0 1 1
					}
					
					func spawn()
					{
						playsound minecraft:enemy.miko.sanka_shoot player @a ~ ~ ~ 3 1 0
						```
						for i in range(6):
							print('''summon armor_stand ~ ~ ~ {Rotation:[%df,0f],Invisible:1b,Invulnerable:1b,NoGravity:1b,HasVisualFire:1b,Marker:1b,CustomNameVisible:1b,CustomName:'{"text":"3","font":"skill:main"}',Tags:["object","effect","sanka_bullet","new_sanka"]}''' %(i * 60))
						```
						execute as @e[type=armor_stand,tag=new_sanka] run function ARG(_PATH)setting
					}
					
					func setting()
					{
						tag @s remove new_sanka
						scoreboard players operation @s number = #BULLET_SPLIT number
						scoreboard players set @s life 100
					}
					
					func move()
					{
						tp @s ^ ^ ^0.2
						scoreboard players remove @s life 1
						
						scoreboard players set #SANKA_EXPLODED number 0
						execute if predicate stage:sanka_split positioned ^ ^ ^-1 run function ARG(_PATH)split
						# 沒分裂才檢查有沒有打到玩家
						execute if score #SANKA_EXPLODED number matches 0 positioned ~-0.5 ~ ~-0.5 as @a[gamemode=adventure,scores={player=1..},dx=0,dy=0,dz=0,limit=1] at @s run function ARG(_PATH)hit
						# 有打到玩家就自我清除
						execute if score #SANKA_EXPLODED number matches 2 run kill @s
					}
					
					func split()
					{
						scoreboard players set #SANKA_EXPLODED number 1
						
						scoreboard players operation #BULLET_SPLIT number = @s number
						scoreboard players remove #BULLET_SPLIT number 1
						
						function ARG(_PATH)explode
						kill @s
					}
					
					func hit()
					{
						scoreboard players set #SANKA_EXPLODED number 2
						
						function ARG(_PATH)explode_effect
						playsound minecraft:enemy.miko.sanka_shoot player @a ~ ~ ~ 3 1 0
						execute unless score @s beamtime matches 0.. run effect give @s instant_damage 1 0 true
					}
				}
			}
			
			folder hell()
			{
				func cast()
				{
					scoreboard players set #ANIM_LOCK number 1
					function miko:animations/animation.miko.hell/play
					scoreboard players reset #RAND_RESULT number
					execute positioned ^ ^ ^2 run function ARG(_PATH)summon
				}
				
				func summon()
				{
					# 先清掉舊的避免太快連續施放 UUID 重疊
					kill 00000006-0000-0006-0000-000600000006
					kill 00000007-0000-0007-0000-000700000007
					
					playsound minecraft:enemy.miko.hell_summon player @a ~ ~ ~ 3 1 0
					
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:21,CustomPotionColor:0}}],Tags:["object","effect"],UUID:[I;6,6,6,6]}
					```
					for i in range(-7, 8):
						print('execute if entity @s[y_rotation=%.3f..%d] run summon armor_stand ~ ~-3.2 ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:54}}],Tags:["object","effect","hell_skeleton","dead_event"],UUID:[I;7,7,7,7],Attributes:[{Name:"minecraft:generic.max_health",Base:1024.0}],Health:132.0f,Pose:{Head:[0.1f,0f,0f]},Rotation:[%df,0f]}' %(i*24-11.999, i*24+12, i*24))
					```
					execute if score #DIFFICULTY number matches 1 run data modify entity 00000007-0000-0007-0000-000700000007 Health set value 88.0f
					execute if score #DIFFICULTY number matches 2 run data modify entity 00000007-0000-0007-0000-000700000007 Health set value 110.0f
					execute if score #DIFFICULTY number matches 3 run data modify entity 00000007-0000-0007-0000-000700000007 Health set value 121.0f
					
					scoreboard players set #SKULL_ANGLE number 0
					scoreboard players set #SWAMP_SIZE number 0
				}
				
				func check()
				{
					execute unless score @s life matches 40.. run function ARG(_PATH)float
					execute if score @s life matches 40.. run function ARG(_PATH)chase
				}
				
				func float()
				{
					scoreboard players add @s life 1
					execute if score @s life matches ..8 store result entity 00000006-0000-0006-0000-000600000006 ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players add #SWAMP_SIZE number 3
					execute if score @s life matches ..20 run tp @s ~ ~0.2 ~ ~ 0
					execute if score @s life matches 6..20 store result entity @s Pose.Head[0] float 1 run scoreboard players add #SKULL_ANGLE number 6
					execute if score @s life matches 25..36 store result entity 00000006-0000-0006-0000-000600000006 ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players remove #SWAMP_SIZE number 2
					execute if score @s life matches 40 run function ARG(_PATH)start_chase
				}
				
				func start_chase()
				{
					kill 00000006-0000-0006-0000-000600000006
					execute as 00000001-0000-0001-0000-000100000001 run function miko:animations/animation.miko.hell_end/play
					playsound minecraft:enemy.miko.scream player @a ~ ~ ~ 3 1 0
					
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 55
					tag @s remove effect
					scoreboard players set @s number 2
				}
				
				func chase()
				{
					execute facing entity @p[gamemode=adventure,scores={player=1..}] feet rotated ~ 0 run tp @s ^ ^ ^0.45 ~ 0
					execute positioned ~-1.5 ~-1 ~-1.5 as @a[gamemode=adventure,scores={player=1..},dx=2,dy=3,dz=2] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 1 true
				}
				
				func skull_hurt()
				{
					tp @s ^ ^ ^-0.95
					execute if score @s health matches 1.. store result entity @s Health float 0.1 run scoreboard players get @s health
					execute if score @s health matches ..0 run function ARG(_PATH)explode
				}
				
				func explode()
				{
					particle minecraft:explosion_emitter ~ ~ ~ 0 0 0 1 1
					particle minecraft:item minecraft:bone_block ~ ~1 ~ 0 0 0 0.4 100
					playsound minecraft:enemy.miko.skull_break player @a ~ ~ ~ 3 1 0
					
					# execute positioned ~ ~-0.8 ~ run effect give @a[gamemode=adventure,scores={player=1..},distance=..5] instant_damage 1 1 true
					kill @s
				}
			}
			
			folder sikigami()
			{
				func cast()
				{
					# 延後下次技能時間，給玩家一點時間清分身
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(__PATH)loop 14s
					execute if score #DIFFICULTY number matches 2 run schedule function ARG(__PATH)loop 13s
					execute if score #DIFFICULTY number matches 3 run schedule function ARG(__PATH)loop 12s
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(__PATH)loop 11s
					
					scoreboard players set #SIKIGAMI_USED number 1
					scoreboard players set #SIKIGAMI_CD number 1
					schedule function ARG(_PATH)reset_cd 90s
					
					scoreboard players set #ANIM_LOCK number 1
					function miko:animations/animation.miko.sikigami/play
					
					scoreboard players reset #TAIJI_SIZE number
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:22,CustomPotionColor:0}}],Tags:["object","effect"],UUID:[I;9,9,9,9]}
					
					schedule function ARG(_PATH)call_effect 30t
				}
				
				folder taiji()
				{
					func main()
					{
						scoreboard players add @s life 1
						execute if score @s life matches 11..25 run function ARG(_PATH)expand
						execute if score @s life matches 30.. run function ARG(_PATH)shrink
						
						kill @s[scores={life=35}]
					}
					
					func expand()
					{
						execute if score @s life matches 11 at @s run playsound minecraft:enemy.miko.sikigami_prepare player @a ~ ~ ~ 3 1 0
						scoreboard players add #TAIJI_SIZE number 1
						execute store result entity @s ArmorItems[3].tag.CustomPotionColor int 4358 run scoreboard players get #TAIJI_SIZE number
					}
					
					func shrink()
					{
						execute if score @s life matches 30 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 23
						scoreboard players set #TAIJI_SIZE number 35
						execute if score @s life matches 31.. store result entity @s ArmorItems[3].tag.CustomPotionColor int 18 run scoreboard players operation #TAIJI_SIZE number -= @s life
					}
				}
				
				func call_effect()
				{
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)effect
				}
				
				func effect()
				{
					function miko:animations/animation.miko.sikigami/stop
					particle minecraft:cloud ~ ~ ~ 0.3 1.2 0.3 0.27 300
					playsound minecraft:enemy.miko.smoke_burst player @a ~ ~ ~ 3 1 0
					
					schedule clear skill:boss_hurt_reset
					# 隱形，暫時給 effect tag 避免被當攻擊對象
					function miko:set_variant/invisible
					tag @s add effect
					
					# 先清掉既有的屏障
					kill @e[type=area_effect_cloud,tag=barrier]
					
					scoreboard players set #EXIST_CLONE number 0
					function ARG(_PATH)summon_clone
					execute as @e[type=armor_stand,tag=new_miko] run function ARG(_PATH)clone_setting
					
					schedule function ARG(_PATH)call_show_up 20t
				}
				
				func summon_clone()
				{
					# 特別處理 boss 要傳送的位置
					summon marker ~ ~ ~ {Tags:["object","clone_summon","boss_position"],UUID:[I;8,8,8,8]}
					```
					for i in range(3):
						print('summon marker ~ ~ ~ {Tags:["object","clone_summon"]}')
					print(f"spreadplayers {STAGE_CENTER[1][0]} {STAGE_CENTER[1][1]} 10 15 under {int(GROUND_Y[1] + 3)} false @e[type=marker,tag=clone_summon]")
					```
					execute at @e[type=marker,tag=clone_summon,tag=!boss_position] facing entity @p[scores={player=1..},gamemode=adventure] feet run function miko:summon/invisible
					execute at 00000008-0000-0008-0000-000800000008 facing entity @p[scores={player=1..},gamemode=adventure] feet run tp @s ~ ~ ~ ~ 0
					execute at @s run function miko:move
					kill @e[type=marker,tag=clone_summon]
				}
				
				func clone_setting()
				{
					tag @s add effect
					tag @s add dead_event
					
					tag @s add clone
					tag @s remove new_miko
					execute store result score @s number run scoreboard players add #EXIST_CLONE number 1
					scoreboard players add @s number 2
					
					scoreboard players set @s bullet_barrier 1
				}
				
				func call_show_up()
				{
					scoreboard players reset #ANIM_LOCK number
					execute as @e[type=armor_stand,tag=miko] at @s run function ARG(_PATH)show_up
					
					execute store result bossbar minecraft:bullet_barrier value run scoreboard players operation 00000001-0000-0001-0000-000100000001 bullet_barrier = #BULLET_BARRIER_MAX number
					bossbar set minecraft:bullet_barrier visible true
					
					schedule function ARG(_PATH)call_sanka 10t
				}
				
				func show_up()
				{
					# 不知道為什麼距離遠的時候 move 會錯位，再跑一次 move 才會正常
					execute if entity @s[tag=boss] run function miko:move
					function miko:set_variant/default
					tag @s remove effect
					
					particle minecraft:cloud ~ ~ ~ 0.3 1.2 0.3 0.27 100
					playsound minecraft:enemy.miko.smoke_burst2 player @a ~ ~ ~ 1 1 0
					
					# 生成防護罩特效
					summon area_effect_cloud ~ ~0.3 ~ {Duration:2147483647,Tags:["object","barrier"],CustomNameVisible:1b,CustomName:'{"text":"4","font":"skill:main"}'}
				}
				
				func call_sanka()
				{
					execute at @e[type=armor_stand,tag=miko] run function ARG(_PATH)do_sanka
				}
				
				func do_sanka()
				{
					scoreboard players set #BULLET_SPLIT number 1
					execute positioned ~ ~0.7 ~ run function ARG(__PATH)sanka/bullet/explode
				}
				
				func reset_cd()
				{
					scoreboard players reset #SIKIGAMI_CD number
				}
				
				func hit_check()
				{
					execute if score #GUN_WEAK_ATTACK number matches 1 run function ARG(_PATH)block_damage
					execute unless score #GUN_WEAK_ATTACK number matches 1 if entity @s[tag=boss] run function ARG(_PATH)barrier_check
				}
				
				func block_damage()
				{
					playsound minecraft:enemy.miko.block_shoot player @a ~ ~ ~ 3 1 0
					execute positioned ^ ^0.5 ^0.7 run function ARG(_PATH)block_effect
					scoreboard players set #IGNORE_DAMAGE number 1
				}
				
				func block_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:burn_bullet/0"}',CustomNameVisible:1,Duration:4,Tags:['{"text":"","font":"font_animation:burn_bullet/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func clone_break()
				{
					playsound minecraft:enemy.miko.laugh player @a ~ ~ ~ 3 1 1
					execute positioned ~ ~0.5 ~ run function ARG(_PATH)clone_break_effect
					scoreboard players remove #EXIST_CLONE number 1
					# 清掉屏障跟火環，每個分身一定會分很開，直接抓最近的清掉就好
					kill @e[type=area_effect_cloud,tag=barrier,sort=nearest,limit=1]
					kill @e[type=armor_stand,tag=flame_circle,sort=nearest,limit=1]
					function miko:remove/this
				}
				
				func clone_break_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:sikigami_break/0"}',CustomNameVisible:1,Duration:8,Tags:['{"text":"","font":"font_animation:sikigami_break/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 7
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func barrier_check()
				{
					execute store result bossbar minecraft:bullet_barrier value run scoreboard players operation @s bullet_barrier -= @s display_number
					execute if score @s bullet_barrier matches ..0 run function ARG(_PATH)clear_barrier
				}
				
				func clear_barrier()
				{
					playsound minecraft:enemy.miko.barrier_break player @a ~ ~ ~ 3 1 0
					particle flash ~ ~1 ~ 0 0 0 1 1
					bossbar set minecraft:bullet_barrier visible false
					# 清掉屏障，每個分身一定會分很開，直接抓最近的清掉就好
					kill @e[type=area_effect_cloud,tag=barrier,sort=nearest,limit=1]
				}
			}
		}
	}
	
	# 深淵恐懼
	folder stage3()
	{
		func unlock()
		{
			scoreboard players set #UNLOCK_STAGE number 3
			data modify storage lobby:main abyss set value "4"
		}
		
		folder start_anim()
		{
			func play()
			{
				```
				print(f"execute positioned {STAGE_CENTER[2][0] + 5} {GROUND_Y[2] + 0.5} {STAGE_CENTER[2][1]} rotated 0 0 run function ARG(_PATH)summon")
				```
				scoreboard players reset #START_ANIM number
				function ARG(_PATH)loop
			}
			
			func loop()
			{
				scoreboard players add #START_ANIM number 1
				spectate 000000c8-0000-00c8-0000-00c8000000c8 @a
				
				execute if score #START_ANIM number matches 5 as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.march/play
				execute if score #START_ANIM number matches 24.. as 000000c8-0000-00c8-0000-00c8000000c8 at @s run tp @s ~-0.25 ~ ~0.5
				execute if score #START_ANIM number matches 45 run function ARG(_PATH)fade
				execute unless score #START_ANIM number matches 65 run schedule function ARG(_PATH)loop
				execute if score #START_ANIM number matches 65 run function ARG(_PATH)end
			}
			
			func end()
			{
				execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.march_loop/stop
				execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.loop/play
				kill 000000c8-0000-00c8-0000-00c8000000c8
				execute as @a[scores={player=1},limit=1] run function stage:start
			}
			
			func fade()
			{
				title @a times 10 10 5
				title @a title {"text":"0","font":"ui:main"}
			}
			
			func summon()
			{
				function abyss:summon/default
				
				# 攝影機
				summon armor_stand ~6 ~-0.4 ~ {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,Tags:["object"],Rotation:[-90f,-5f],UUID:[I;200,200,200,200]}
				spectate 000000c8-0000-00c8-0000-00c8000000c8 @a
			}
		}
		
		func start()
		{
			bossbar set minecraft:boss max 50000
			execute store result bossbar minecraft:boss value run bossbar get minecraft:boss max
			bossbar set minecraft:boss name {"translate":"Root of Fear","color":"white","bold":true}
			```
			print(f"execute positioned {STAGE_CENTER[2][0] + 5} {GROUND_Y[2] + 0.5} {STAGE_CENTER[2][1]} rotated 0 0 run function ARG(_PATH)summon_boss")
			# 碰撞用，不清除，所以不加 object 標籤
			print('execute unless entity 7fffffff-7fff-ffff-7fff-ffff7fffffff run summon slime %.2f %.2f %.2f {Invulnerable:1b,NoAI:1b,Size:8.0,Silent:1b,UUID:[I;2147483647,2147483647,2147483647,2147483647],ActiveEffects:[{Id:14b,Amplifier:0b,Duration:1000000000,Ambient:0b,ShowParticles:0b}],Tags:["effect"]}' %(STAGE_CENTER[2][0] + 7, GROUND_Y[2] + 1, STAGE_CENTER[2][1]))
			print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0] + 7} {GROUND_Y[2] + 1} {STAGE_CENTER[2][1]}")
			
			print(f"tp @a {STAGE_CENTER[2][0]} {GROUND_Y[2]} {STAGE_CENTER[2][1]} -90 40")
			```
			
			schedule function ARG(_PATH)skills/loop 5s
			schedule function ARG(_PATH)skills/sting/spawn 3s
			schedule function ARG(_PATH)skills/hunt/start 12s
		}
		
		func summon_boss()
		{
			# function abyss:summon/default
			scoreboard players set 00000001-0000-0001-0000-000100000001 number 1
			execute store result score 00000001-0000-0001-0000-000100000001 health run bossbar get minecraft:boss max
			execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.loop/play
			
			tag 00000001-0000-0001-0000-000100000001 remove effect
			scoreboard players set #GLOBAL number 1
		}
		
		func end()
		{
			```
			print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0]} {GROUND_Y[2] - 20} {STAGE_CENTER[2][1]}")
			```
			scoreboard players reset #BOSS_IN number
			scoreboard players reset #HUNT_STING number
			scoreboard players reset #SWALLOW_CD number
			scoreboard players reset #RESONANCE_CD number
			scoreboard players reset #SHAKING number
			scoreboard players reset #SWALLOW number
			scoreboard players reset #EYE_ANIM number
			scoreboard players reset #BLIND number
			
			schedule clear ARG(_PATH)skills/loop
			schedule clear ARG(_PATH)skills/boss_in/start
			schedule clear ARG(_PATH)skills/sting/spawn
			schedule clear ARG(_PATH)skills/sting/mark/start
			schedule clear ARG(_PATH)skills/sting/call_retrieve
			schedule clear ARG(_PATH)skills/hunt/start
			schedule clear ARG(_PATH)skills/hunt/summon_sting
			schedule clear ARG(_PATH)skills/raid/call_shoot
			schedule clear ARG(_PATH)skills/raid/reset
			schedule clear ARG(_PATH)skills/march/call_shoot
			schedule clear ARG(_PATH)skills/fear/tiny/spawn
			schedule clear ARG(_PATH)skills/swallow/reset_cd
			schedule clear ARG(_PATH)skills/swallow/set_pos
			schedule clear ARG(_PATH)skills/swallow/stop_shake
			schedule clear ARG(_PATH)skills/swallow/start_move
			schedule clear ARG(_PATH)skills/swallow/set_idle
			schedule clear ARG(_PATH)skills/resonance/reset_cd
			schedule clear ARG(_PATH)skills/resonance/start_effect
			schedule clear ARG(_PATH)skills/resonance/eye/activate
			schedule clear ARG(_PATH)skills/resonance/eye/rotate
			
			data modify storage skill:main blind set value ""
			title @a title ""
			
			function abyss:remove/all
			function abyss_head:remove/all
		}
		
		func main()
		{
			execute if score #BOSS_IN number matches 1.. run function ARG(_PATH)skills/boss_in/main
			execute as 00000002-0000-0002-0000-000200000002 run function ARG(_PATH)skills/sneak/portal
			execute as 00000003-0000-0003-0000-000300000003 run function ARG(_PATH)skills/sneak/river/main
			execute as 00000009-0000-0009-0000-000900000009 run function ARG(_PATH)skills/stare/portal
			execute as 0000000A-0000-000A-0000-000A0000000A if score @s life matches 1.. at @s run function ARG(_PATH)skills/stare/eye
			execute as 0000000C-0000-000C-0000-000C0000000C at @s run function ARG(_PATH)skills/fear/tiny/move
			execute as @e[type=armor_stand] run function ARG(_PATH)armor_stand_main
			execute as @e[type=zombie,tag=tiny_abyss] at @s run function ARG(_PATH)skills/march/move
			execute if score #SWALLOW number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/swallow/move
			execute if score #EYE_ANIM number matches 0.. run function ARG(_PATH)skills/resonance/anim/run
			execute as @e[type=zombie,tag=active_eye] at @s run function ARG(_PATH)skills/resonance/eye/move
		}
		
		func armor_stand_main()
		{
			execute at @s[tag=sting_summon] run function ARG(_PATH)skills/sting/ring
			execute if entity @s[tag=raid_beam] run function ARG(_PATH)skills/raid/beam
		}
		
		folder skills()
		{
			func loop()
			{
				# 每招的動作時間不同，個別決定下一招多久出
				# schedule function ARG(_PATH)loop 7s
				
				scoreboard players set #USE_RANDOM_SKILL number 1
				execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)check_resonance
			}
			
			func check_resonance()
			{
				execute if score @s health matches ..15000 unless score #RESONANCE_CD number matches 1.. run function ARG(_PATH)resonance/cast
				execute if score #USE_RANDOM_SKILL number matches 1 run function ARG(_PATH)check_swallow
			}
			
			func check_swallow()
			{
				# CD 到的時候有 50% 機率會放
				execute if score @s health matches ..35000 unless score #SWALLOW_CD number matches 1.. if predicate stage:swallow_rate run function ARG(_PATH)swallow/cast
				execute if score #USE_RANDOM_SKILL number matches 1 run function ARG(_PATH)rand_skill
			}
			
			func rand_skill()
			{
				function random:rand_rate
				
				execute if score #RAND_RESULT number matches 0..24 run function ARG(_PATH)sneak/cast
				execute if score #RAND_RESULT number matches 25..49 run function ARG(_PATH)raid/cast
				execute if score #RAND_RESULT number matches 50..74 run function ARG(_PATH)march/cast
				execute if score #RAND_RESULT number matches 75..84 run function ARG(_PATH)stare/cast
				execute if score #RAND_RESULT number matches 85..99 run function ARG(_PATH)fear/cast
			}
			
			folder boss_in()
			{
				func start()
				{
					scoreboard players set #BOSS_IN number 1
					schedule function ARG(__PATH)loop 5s
					
					execute as 00000001-0000-0001-0000-000100000001 run function abyss:set_variant/default
					execute as 00000001-0000-0001-0000-000100000001 at @s run tp @s ~-0.5 ~ ~
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.loop/play
				}
				
				func main()
				{
					scoreboard players add #BOSS_IN number 1
					
					execute if score #BOSS_IN number matches 2..36 as 00000001-0000-0001-0000-000100000001 at @s run tp @s ~-0.5 ~ ~
					execute if score #BOSS_IN number matches 36 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)reset_stat
				}
				
				func reset_stat()
				{
					tag @s remove effect
					```
					# 恢復 BOSS 所在地點的屏障
					# print(f"fill {int(STAGE_CENTER[2][0] + 5)} {int(GROUND_Y[2])} {int(STAGE_CENTER[2][1] - 2)} {int(STAGE_CENTER[2][0] + 5)} {int(GROUND_Y[2] + 22)} {int(STAGE_CENTER[2][1] + 1)} minecraft:barrier")
					
					# 不用屏障，改用史萊姆碰撞推擠
					print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0] + 7} {GROUND_Y[2] + 1} {STAGE_CENTER[2][1]}")
					```
					# execute as @a[gamemode=adventure,scores={player=1..}] at @s if block ~ ~ ~ minecraft:barrier run function ARG(_PATH)leave_barrier
					
					scoreboard players reset #BOSS_IN number
				}
				
				func leave_barrier()
				{
					tp @s ~-0.5 ~ ~
					execute at @s if block ~ ~ ~ minecraft:barrier run function ARG(_PATH)leave_barrier
				}
			}
			
			folder sting()
			{
				func spawn()
				{
					execute if score 00000001-0000-0001-0000-000100000001 health matches 25001.. run schedule function ARG(_PATH)spawn 3.5s
					execute if score 00000001-0000-0001-0000-000100000001 health matches ..25000 run schedule function ARG(_PATH)spawn 2.5s
					
					execute as @a[gamemode=adventure,scores={player=1..}] at @s run function ARG(_PATH)sting_pos/summon
					schedule function ARG(_PATH)mark/start 1t
				}
				
				folder sting_pos()
				{
					func summon()
					{
						summon marker ^ ^ ^-0.001 {Tags:["sting_pos","object","new_pos"]}
						scoreboard players operation #TEMP number = @s player
						execute as @e[type=marker,tag=new_pos] run function ARG(_PATH)setting
					}
					
					func setting()
					{
						scoreboard players operation @s number = #TEMP number
						tag @s remove new_pos
					}
				}
				
				folder mark()
				{
					func start()
					{
						execute as @a[gamemode=adventure,scores={player=1..}] at @s run function ARG(_PATH)find_pos
					}
					
					func find_pos()
					{
						scoreboard players operation #TEMP number = @s player
						execute as @e[type=marker,tag=sting_pos] if score @s number = #TEMP number run function ARG(_PATH)forward
					}
					
					func forward()
					{
						```
						print(f'execute facing entity @s eyes positioned ~ {int(GROUND_Y[2])} ~ rotated ~ 0 positioned ^ ^ ^-3 run function ARG(_PATH)check_floor')
						```
						kill @s
					}
					
					func check_floor()
					{
						execute unless block ^ ^-1 ^-0.5 #skill:canpass run function ARG(_PATH)summon
						execute if block ^ ^-1 ^-0.5 #skill:canpass positioned ^ ^ ^1 run function ARG(_PATH)check_floor
					}
					
					func summon()
					{
						playsound minecraft:enemy.abyss.summon_sting player @a ~ ~ ~ 1 1 0
						summon armor_stand ~ ~ ~ {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:24,CustomPotionColor:0}}],Tags:["object","effect","sting_summon"]}
					}
				}
				
				func ring()
				{
					scoreboard players add @s life 1
					# 旋轉縮放
					execute if score @s life matches ..10 run scoreboard players add @s number 1537
					
					# 難度反應時間差異
					execute if score #DIFFICULTY number matches 2..3 if score @s life matches 10 run scoreboard players add @s life 2
					execute if score #DIFFICULTY number matches 4 if score @s life matches 10 run scoreboard players add @s life 4
					
					execute if score @s life matches 39..47 run scoreboard players remove @s number 1537
					execute store result entity @s ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players get @s number
					
					execute if score @s life matches 28 run function ARG(_PATH)attack
					execute if score @s life matches 48 run kill @s
				}
				
				func attack()
				{
					playsound minecraft:enemy.abyss.sting player @a ~ ~ ~ 1 1 0
					summon armor_stand ~ ~-5 ~ {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:59}}],Tags:["object","effect","sting","new_sting"]}
					execute as @e[type=armor_stand,tag=new_sting,limit=1] run function ARG(_PATH)sting_setting
					
					execute positioned ~-0.5 ~ ~-0.5 as @a[gamemode=adventure,scores={player=1..},dx=0,dy=3,dz=0] unless score @s beamtime matches 0.. run effect give @s minecraft:instant_damage 1 0 true
				}
				
				func sting_setting()
				{
					execute store result score @s number run schedule function ARG(_PATH)call_retrieve 10t append
					tp @s ~ ~ ~
					tag @s remove new_sting
				}
				
				func call_retrieve()
				{
					execute store result score #CURR_TIME number run time query gametime
					execute as @e[type=armor_stand,tag=sting] if score @s number = #CURR_TIME number run function ARG(_PATH)retrieve
				}
				
				func retrieve()
				{
					scoreboard players add @s life 1
					# 第一次被 call 到要縮回去，然後再 schedule 第二次
					execute if score @s life matches 1 at @s run tp @s ~ ~-5 ~
					execute if score @s life matches 1 store result score @s number run schedule function ARG(_PATH)call_retrieve 10t append
					# 第二次被 call 到要清除
					execute if score @s life matches 2 run kill @s
				}
			}
			
			folder hunt()
			{
				func start()
				{
					schedule function ARG(_PATH)start 15s
					
					execute at @a[gamemode=adventure,scores={player=1..}] positioned ~ ~1.5 ~ run function ARG(_PATH)mark_effect
					
					execute if score #DIFFICULTY number matches 1 run scoreboard players set #HUNT_STING number 5
					execute if score #DIFFICULTY number matches 2..3 run scoreboard players set #HUNT_STING number 6
					execute if score #DIFFICULTY number matches 4 run scoreboard players set #HUNT_STING number 7
					
					schedule function ARG(_PATH)summon_sting 40t
				}
				
				func mark_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:mark_purple/1"}',CustomNameVisible:1,Duration:11,Tags:['{"text":"","font":"font_animation:mark_purple/1"}', '{"text":"","font":"font_animation:mark_purple/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 10
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func summon_sting()
				{
					scoreboard players remove #HUNT_STING number 1
					execute if score #HUNT_STING number matches 1.. run schedule function ARG(_PATH)summon_sting 10t
					```
					print("execute at @a[gamemode=adventure,scores={player=1..}] positioned ~ %d ~ run function ARG(__PATH)sting/mark/summon" %GROUND_Y[2])
					```
				}
			}
			
			folder sneak()
			{
				func cast()
				{
					playsound minecraft:enemy.abyss.portal player @a ~ ~ ~ 3 1 1
					```
					print('summon armor_stand %.2f %.2f %.2f {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:26,CustomPotionColor:0}}],Tags:["object","effect"],UUID:[I;2,2,2,2]}' %(STAGE_CENTER[2][0], GROUND_Y[2], STAGE_CENTER[2][1]))
					# 移走碰撞用的史萊姆
					print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0]} {GROUND_Y[2] - 20} {STAGE_CENTER[2][1]}")
					```
					function abyss:animations/animation.model.loop/stop
					function abyss:animations/animation.model.sink/play
					
					# 動畫過程無敵狀態
					tag @s add effect
				}
				
				func portal()
				{
					scoreboard players add @s life 1
					scoreboard players add @s number 512
					execute if score @s life matches ..10 run scoreboard players add @s number 5
					execute if score @s life matches 31..40 run scoreboard players remove @s number 5
					execute store result entity @s ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players get @s number
					
					execute if score @s life matches 33 run function ARG(_PATH)river/summon
					kill @s[scores={life=40}]
				}
				
				folder river()
				{
					func summon()
					{
						execute if score #DIFFICULTY number matches ..2 run function ARG(_PATH)fixed_summon
						execute if score #DIFFICULTY number matches 3.. run function ARG(_PATH)rand_summon
					}
					
					func fixed_summon()
					{
						```
						print('summon armor_stand %.2f %.2f %.2f {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:25,CustomPotionColor:0}}],Rotation:[180f,0f],Tags:["object","effect"],UUID:[I;3,3,3,3]}' %(STAGE_CENTER[2][0], GROUND_Y[2], STAGE_CENTER[2][1] + 5))
						```
					}
					
					func rand_summon()
					{
						function random:rand_rate
						```
						print('execute if score #RAND_RESULT number matches ..49 run summon armor_stand %.2f %.2f %.2f {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:25,CustomPotionColor:0}}],Rotation:[180f,0f],Tags:["object","effect"],UUID:[I;3,3,3,3]}' %(STAGE_CENTER[2][0], GROUND_Y[2], STAGE_CENTER[2][1] + 5))
						print('execute if score #RAND_RESULT number matches 50.. run summon armor_stand %.2f %.2f %.2f {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:25,CustomPotionColor:0}}],Tags:["object","effect"],UUID:[I;3,3,3,3]}' %(STAGE_CENTER[2][0], GROUND_Y[2], STAGE_CENTER[2][1] - 5))
						```
					}
					
					func main()
					{
						scoreboard players add @s life 1
						execute if score @s life matches 1 run scoreboard players add @s number 8
						execute if score @s life matches 2..8 run scoreboard players add @s number 7
						execute if score @s life matches 69..75 at @s run scoreboard players remove @s number 7
						execute if score @s life matches 76 at @s run scoreboard players remove @s number 8
						execute store result entity @s ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players get @s number
						
						execute if score @s life matches 19..68 at @s run function ARG(_PATH)judge
						
						# BOSS 回歸位置
						execute if score @s life matches 81 run function stage:stage3/skills/boss_in/start
						
						execute if score @s life matches 81 run kill @s
					}
				
					func judge()
					{
						execute if score #DIFFICULTY number matches 1 positioned ~-8 ~-0.5 ~-3 as @a[gamemode=adventure,scores={player=1..},dx=15,dy=0,dz=5,limit=1] at 00000003-0000-0003-0000-000300000003 positioned ~ ~-0.5 ~ rotated 0 0 run function ARG(_PATH)check_pos
						execute if score #DIFFICULTY number matches 2.. positioned ~-8 ~-0.5 ~-3 as @a[gamemode=adventure,scores={player=1..},dx=15,dy=1,dz=5,limit=1] at 00000003-0000-0003-0000-000300000003 positioned ~ ~-0.5 ~ rotated 0 0 run function ARG(_PATH)check_pos
						
						execute if score @s life matches ..58 run tp @s ^ ^ ^0.25
					}
					
					func check_pos()
					{
						execute store result score #BITE_POS_X number run data get entity @s Pos[0]
						```
						print(f"execute if score #BITE_POS_X number matches ..{int(STAGE_CENTER[2][0] - 6)} positioned {STAGE_CENTER[2][0] - 6} ~ ~ run function ARG(_PATH)bite")
						print(f"execute if score #BITE_POS_X number matches {int(STAGE_CENTER[2][0] + 5)}.. positioned {STAGE_CENTER[2][0] + 6} ~ ~ run function ARG(_PATH)bite")
						print(f"execute if score #BITE_POS_X number matches {int(STAGE_CENTER[2][0] - 5)}..{int(STAGE_CENTER[2][0] + 4)} run function ARG(_PATH)set_pos")
						```
					}
					
					func set_pos()
					{
						summon marker ~ ~ ~ {UUID:[I;13,13,13,13]}
						
						data modify entity 0000000D-0000-000D-0000-000D0000000D Pos[0] set from entity @s Pos[0]
						execute at 0000000D-0000-000D-0000-000D0000000D run function ARG(_PATH)bite
						
						kill 0000000D-0000-000D-0000-000D0000000D
					}
					
					func bite()
					{
						playsound minecraft:enemy.abyss.bite player @a ~ ~ ~ 1 1 0
						function abyss_head:summon/default
						execute as 00000004-0000-0004-0000-000400000004 run function abyss_head:animations/animation.model.bite/play
						# 設成一個不會執行到其他功能的 life 分數，咬人動畫執行完會自己設回 68 作結束
						execute as 00000003-0000-0003-0000-000300000003 run scoreboard players set @s life 1000
						
						execute positioned ~-3 ~ ~-3 run execute as @a[gamemode=adventure,scores={player=1..},dx=5,dy=3,dz=5] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 1 true
					}
					
					func bite_end()
					{
						scoreboard players set @s life 68
						function abyss_head:remove/all
					}
				}
				
				func set_idle()
				{
					execute as 00000001-0000-0001-0000-000100000001 run function abyss:set_variant/invisible
					```
					print(f"tp 00000001-0000-0001-0000-000100000001 {STAGE_CENTER[2][0] + 23} {GROUND_Y[2] + 0.5} {STAGE_CENTER[2][1]}")
					```
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.sink/stop
				}
			}
			
			folder raid()
			{
				func cast()
				{
					function abyss:animations/animation.model.loop/stop
					
					# 施放時無敵
					tag @s add effect
					```
					print("execute at @r[gamemode=adventure,scores={player=1..}] run tp @s %.2f %.2f ~" %(STAGE_CENTER[2][0] + 5, GROUND_Y[2] + 0.5))
					# 移走碰撞用的史萊姆
					print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0]} {GROUND_Y[2] - 20} {STAGE_CENTER[2][1]}")
					```
					execute at @s run function ARG(_PATH)cast_effect
				}
				
				func cast_effect()
				{
					function abyss:animations/animation.model.raid/play
					execute positioned ~3 ~1.2 ~ run function ARG(_PATH)raid_effect
					playsound minecraft:enemy.abyss.beam_prepare player @a ~ ~ ~ 3 1 1
					```
					# 攻擊範圍提示
					print('summon armor_stand %.2f %.2f ~ {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:27,CustomPotionColor:57}}],Tags:["object","effect"],UUID:[I;5,5,5,5]}' %(STAGE_CENTER[2][0] + 4, GROUND_Y[2]))
					print('summon armor_stand %.2f %.2f ~ {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:28,CustomPotionColor:57}}],Tags:["object","effect"],UUID:[I;6,6,6,6]}' %(STAGE_CENTER[2][0] - 4, GROUND_Y[2]))
					```
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)call_shoot 30t
					execute if score #DIFFICULTY number matches 2 run schedule function ARG(_PATH)call_shoot 28t
					execute if score #DIFFICULTY number matches 3 run schedule function ARG(_PATH)call_shoot 27t
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)call_shoot 25t
				}
				
				func call_shoot()
				{
					# 清除提示物件
					kill 00000005-0000-0005-0000-000500000005
					kill 00000006-0000-0006-0000-000600000006
					```
					print("execute at 00000001-0000-0001-0000-000100000001 positioned %.2f %.2f ~ run function ARG(_PATH)shoot" %(STAGE_CENTER[2][0], GROUND_Y[2]))
					```
					schedule function ARG(_PATH)reset 15t
				}
				
				func shoot()
				{
					execute positioned ~-16 ~ ~-2.5 as @a[gamemode=adventure,scores={player=1..},dx=23,dy=4,dz=4] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 1 true
					
					# 雷射的衝擊波模型
					summon armor_stand ~8 ~-0.3 ~ {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:64}}],Tags:["object","effect"],UUID:[I;7,7,7,7]}
					tp 00000007-0000-0007-0000-000700000007 ~-24 ~-0.3 ~
					
					# 雷射特效模型
					```
					for i in range(-14, 7, 4):
						print('summon armor_stand ~%d ~-0.3 ~ {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:26}}],Tags:["object","effect","raid_beam"]}' %i)
					```
					playsound minecraft:enemy.abyss.beam player @a ~ ~ ~ 3 1 1
				}
				
				func beam()
				{
					scoreboard players add @s life 1
					execute if score @s life matches 1 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 63
					execute if score @s life matches 2 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 61
					execute if score @s life matches 4 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 62
					execute if score @s life matches 6 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 61
					execute if score @s life matches 8 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 63
					execute if score @s life matches 9 run kill @s
				}
				
				func reset()
				{
					kill 00000007-0000-0007-0000-000700000007
					
					execute as 00000001-0000-0001-0000-000100000001 run function abyss:animations/animation.model.raid/stop
					```
					print(f"tp 00000001-0000-0001-0000-000100000001 {STAGE_CENTER[2][0] + 5} {GROUND_Y[2] + 0.5} {STAGE_CENTER[2][1]}")
					```
					# 回復動作，動畫塞事件接 switch_base
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.raid/stop
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.raid_end/play
					
					schedule function ARG(__PATH)loop 3s
				}
				
				func switch_base()
				{
					```
					# 碰撞用的史萊姆移回來
					print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0] + 7} {GROUND_Y[2] + 1} {STAGE_CENTER[2][1]}")
					```
					function abyss:animations/animation.model.raid_end/pause
					function abyss:animations/animation.model.loop/play
					tag @s remove effect
				}
				
				func raid_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:raid/2"}',CustomNameVisible:1,Duration:22,Tags:['{"text":"","font":"font_animation:raid/2"}', '{"text":"","font":"font_animation:raid/1"}', '{"text":"","font":"font_animation:raid/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 21
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
			}
			
			folder march()
			{
				func cast()
				{
					# 施放中無敵
					tag @s add effect
					
					playsound minecraft:enemy.abyss.march_prepare player @a ~ ~ ~ 3 1 0
					function abyss:animations/animation.model.loop/stop
					function abyss:animations/animation.model.march/play
					```
					# 生成點
					print('summon marker %.2f %.2f %.2f {UUID:[I;8,8,8,8],Tags:["object"]}' %(STAGE_CENTER[2][0] + 14, GROUND_Y[2], STAGE_CENTER[2][1]))
					# 移走碰撞用的史萊姆
					print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0]} {GROUND_Y[2] - 20} {STAGE_CENTER[2][1]}")
					```
				}
				
				func call_shoot()
				{
					execute as 00000008-0000-0008-0000-000800000008 at @s run function ARG(_PATH)shoot
				}
				
				func shoot()
				{
					scoreboard players add @s life 1
					execute if score @s life matches ..10 run schedule function ARG(_PATH)call_shoot 6t
					```
					for i in range(1, 10):
						print(f"execute if score @s life matches {i} run function ARG(_PATH)spawn/{i}") 
					```
					execute if score @s life matches ..9 as @e[type=zombie,tag=new_tiny] run function ARG(_PATH)tiny_setting
					
					execute if score @s life matches 11 run function ARG(_PATH)reset
				}
				
				folder spawn()
				{
					```
					for i in range(1, 10):
						print(f"func {i}()")
						print("{")
						for j in range(4):
							print('summon zombie ~ ~ ~ {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:60}}],Team:"enemy",Health:15.0,Tags:["object","dead_event","knockback_resist","tiny","tiny_abyss","new_tiny"],Rotation:[%df,0f]}' %(70 + j * 30 - i * 5))
						print("}")
					```
				}
				
				func tiny_setting()
				{
					execute store result score @s number run scoreboard players add #GLOBAL number 1
					tag @s remove new_tiny
				}
				
				func move()
				{
					tp @s ^ ^ ^0.4
					execute store result score #TEMP number unless block ~ ~ ~ #skill:canpass
					execute positioned ~-0.5 ~ ~-0.5 as @a[gamemode=adventure,scores={player=1..},dx=0,dy=1,dz=0] run function ARG(_PATH)damage
					execute if score #TEMP number matches 1 run function ARG(_PATH)destroy
				}
				
				func destroy()
				{
					item replace entity @s armor.head with air
					tp @s ~ -128 ~
					kill @s
				}
				
				func damage()
				{
					scoreboard players set #TEMP number 1
					execute positioned ~ ~0.5 ~ run function ARG(_PATH)bitten_effect
					
					execute unless score @s beamtime matches 0.. run effect give @s instant_damage 1 0 true
				}
				
				func reset()
				{
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)reset_boss
					kill @s
					schedule function ARG(__PATH)loop 3s
				}
				
				func reset_boss()
				{
					function abyss:animations/animation.model.march_loop/stop
					# 回復動作，動畫塞事件接 switch_base
					function abyss:animations/animation.model.march_end/play
				}
				
				func switch_loop()
				{
					function abyss:animations/animation.model.march/pause
					function abyss:animations/animation.model.march_loop/play
					playsound minecraft:enemy.abyss.march player @a ~ ~ ~ 3 1 0
					function ARG(_PATH)call_shoot
				}
				
				func switch_base()
				{
					```
					# 碰撞用的史萊姆移回來
					print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0] + 7} {GROUND_Y[2] + 1} {STAGE_CENTER[2][1]}")
					```
					function abyss:animations/animation.model.march_end/pause
					function abyss:animations/animation.model.loop/play
					tag @s remove effect
				}
				
				func bitten_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:bitten/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:bitten/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 4
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
			}
			
			folder stare()
			{
				func cast()
				{
					playsound minecraft:enemy.abyss.portal player @a ~ ~ ~ 3 1 1
					```
					# 移走碰撞用的史萊姆
					print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0]} {GROUND_Y[2] - 20} {STAGE_CENTER[2][1]}")
					
					# 傳送門
					print('summon armor_stand %.2f %.2f %.2f {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:26,CustomPotionColor:0}}],Tags:["object","effect"],UUID:[I;9,9,9,9]}' %(STAGE_CENTER[2][0], GROUND_Y[2], STAGE_CENTER[2][1]))
					# 眼睛
					print('summon armor_stand %.2f %.2f %.2f {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:65}}],Tags:["object","effect"],UUID:[I;10,10,10,10]}' %(STAGE_CENTER[2][0], GROUND_Y[2] - 1.2, STAGE_CENTER[2][1]))
					# 衝擊波
					print('summon armor_stand %.2f %.2f %.2f {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:29,CustomPotionColor:0}}],Tags:["object","effect"],UUID:[I;11,11,11,11]}' %(STAGE_CENTER[2][0], GROUND_Y[2] + 1, STAGE_CENTER[2][1]))
					# 光束
					for i in range(3):
						print('summon armor_stand %.2f %.2f %.2f {Invulnerable:1b,Invisible:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:26}}],Tags:["object","effect","big_beam"]}' %(STAGE_CENTER[2][0], GROUND_Y[2] + i * 7, STAGE_CENTER[2][1]))
					```
					function abyss:animations/animation.model.loop/stop
					function abyss:animations/animation.model.sink/play
					
					# 動畫過程無敵狀態
					tag @s add effect
				}
				
				func portal()
				{
					scoreboard players add @s life 1
					scoreboard players add @s number 512
					execute if score @s number matches 65536.. run scoreboard players remove @s number 65536
					
					execute if score @s life matches ..10 run scoreboard players add @s number 5
					
					# 眼睛特效開始運作
					execute if score @s life matches 21 as 0000000A-0000-000A-0000-000A0000000A run scoreboard players set @s life 1
					
					# 難度反應時間差異設定 (跟 eye function 對應)
					execute if score #DIFFICULTY number matches 2..3 if score @s life matches 21 run scoreboard players add @s life 5
					execute if score #DIFFICULTY number matches 4 if score @s life matches 21 run scoreboard players add @s life 10
					
					# 借用計時器做玩家傷害(對應光束特效期間)
					execute if score @s life matches 56..115 at @s positioned ~-3.5 ~ ~-3.5 as @a[gamemode=adventure,scores={player=1..},dx=6,dy=20,dz=6] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 1 true
					
					execute if score @s life matches 136..145 run scoreboard players remove @s number 5
					execute store result entity @s ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players get @s number
					
					execute if score @s life matches 136 run function stage:stage3/skills/boss_in/start
					kill @s[scores={life=145}]
				}
				
				func eye()
				{
					# 對應 portal 時間 + 20
					
					# 眼睛升起
					execute if score @s life matches 1..10 run tp @s ~ ~0.12 ~
					
					# 難度反應時間差異設定
					execute if score #DIFFICULTY number matches 2..3 if score @s life matches 10 run scoreboard players add @s life 5
					execute if score #DIFFICULTY number matches 4 if score @s life matches 10 run scoreboard players add @s life 10
					
					# 眼睛微縮(準備發射)
					execute if score @s life matches 29 run tp @s ~ ~-0.2 ~
					
					# 發射光束音效
					execute if score @s life matches 35 at @s run playsound minecraft:enemy.abyss.big_beam player @a ~ ~ ~ 3 1 0
					# 設定光束
					execute if score @s life matches 35 as @e[type=armor_stand,tag=big_beam] run data modify entity @s ArmorItems[3].tag.CustomModelData set value 68
					execute if score @s life matches 36 run tp @s ~ ~0.2 ~
					
					# 衝擊波特效
					execute if score @s life matches 36..40 as 0000000B-0000-000B-0000-000B0000000B store result entity @s ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players add @s number 20
					execute if score @s life matches 41 run kill 0000000B-0000-000B-0000-000B0000000B
					
					# 光束特效(傷害由 portal 統一做)
					execute if score @s life matches 36..95 as @e[type=armor_stand,tag=big_beam] run function ARG(_PATH)beam
					execute if score @s life matches 96 as @e[type=armor_stand,tag=big_beam] run data modify entity @s ArmorItems[3].tag.CustomModelData set value 68
					execute if score @s life matches 97 run kill @e[type=armor_stand,tag=big_beam]
					
					# 眼睛下沉
					execute if score @s life matches 106..115 run tp @s ~ ~-0.12 ~
					
					scoreboard players add @s life 1
					execute if score @s life matches 116 run kill @s
				}
				
				func beam()
				{
					scoreboard players add @s number 1
					execute if score @s number matches 2 run scoreboard players set @s number 0
					
					execute if score @s number matches 1 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 66
					execute if score @s number matches 0 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 67
				}
			}
			
			folder fear()
			{
				func cast()
				{
					# 鑽洞動作
					function abyss:animations/animation.model.loop/stop
					function abyss:animations/animation.model.fear/play
					```
					# 移走碰撞用的史萊姆
					print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0]} {GROUND_Y[2] - 20} {STAGE_CENTER[2][1]}")
					```
					# 動畫過程無敵狀態
					tag @s add effect
					
					scoreboard players set #FEAR_TIMES number 4
					schedule function ARG(_PATH)tiny/spawn 3s
				}
				
				folder tiny()
				{
					func spawn()
					{
						scoreboard players remove #FEAR_TIMES number 1
						
						# 隨機四個方位
						function random:rand_rate
						```
						print('execute if score #RAND_RESULT number matches 0..24 run summon zombie %.2f %.2f %.2f {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:60}}],Team:"enemy",Health:15.0,Tags:["object","dead_event","knockback_resist","tiny"],Rotation:[90f,0f],UUID:[I;12,12,12,12]}' %(STAGE_CENTER[2][0] + 24, GROUND_Y[2], STAGE_CENTER[2][1]))
						print('execute if score #RAND_RESULT number matches 25..49 run summon zombie %.2f %.2f %.2f {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:60}}],Team:"enemy",Health:15.0,Tags:["object","dead_event","knockback_resist","tiny"],Rotation:[270f,0f],UUID:[I;12,12,12,12]}' %(STAGE_CENTER[2][0] - 24, GROUND_Y[2], STAGE_CENTER[2][1]))
						print('execute if score #RAND_RESULT number matches 50..74 run summon zombie %.2f %.2f %.2f {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:60}}],Team:"enemy",Health:15.0,Tags:["object","dead_event","knockback_resist","tiny"],Rotation:[180f,0f],UUID:[I;12,12,12,12]}' %(STAGE_CENTER[2][0], GROUND_Y[2], STAGE_CENTER[2][1] + 24))
						print('execute if score #RAND_RESULT number matches 75..99 run summon zombie %.2f %.2f %.2f {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:60}}],Team:"enemy",Health:15.0,Tags:["object","dead_event","knockback_resist","tiny"],Rotation:[0f,0f],UUID:[I;12,12,12,12]}' %(STAGE_CENTER[2][0], GROUND_Y[2], STAGE_CENTER[2][1] - 24))
						```
						execute at 0000000C-0000-000C-0000-000C0000000C run playsound minecraft:enemy.abyss.fear player @a ~ ~ ~ 5 1 1
						execute as 0000000C-0000-000C-0000-000C0000000C store result score @s number run scoreboard players add #GLOBAL number 1
					}
					
					func check_next()
					{
						execute if score #FEAR_TIMES number matches 1.. run schedule function ARG(_PATH)spawn 2s
						execute if score #FEAR_TIMES number matches 0 run schedule function stage:stage3/skills/boss_in/start 2s
					}
					
					func move()
					{
						execute if score #DIFFICULTY number matches 1 facing entity @p[gamemode=adventure,scores={player=1..}] feet run tp @s ^ ^ ^0.5
						execute if score #DIFFICULTY number matches 2..3 facing entity @p[gamemode=adventure,scores={player=1..}] feet run tp @s ^ ^ ^0.55
						execute if score #DIFFICULTY number matches 4 facing entity @p[gamemode=adventure,scores={player=1..}] feet run tp @s ^ ^ ^0.6
						
						scoreboard players set #TEMP number 0
						execute unless score @s health matches ..0 positioned ~-0.5 ~ ~-0.5 as @a[gamemode=adventure,scores={player=1..},dx=0,dy=1,dz=0] run function ARG(_PATH)damage
						execute if score #TEMP number matches 1 run function ARG(_PATH)destroy
					}
					
					func destroy()
					{
						function ARG(_PATH)check_next
						
						item replace entity @s armor.head with air
						tp @s ~ -128 ~
						
						scoreboard players set @s health 0
						kill @s
					}
					
					func damage()
					{
						scoreboard players set #TEMP number 1
						execute positioned ~ ~0.5 ~ run function stage:stage3/skills/march/bitten_effect
						
						execute unless score @s beamtime matches 0.. run effect give @s instant_damage 1 0 true
					}
					
					func hurt()
					{
						execute store result entity @s Health float 0.1 run scoreboard players get @s health
						execute if score @s health matches ..0 run function ARG(_PATH)dead
					}
					
					func dead()
					{
						execute if entity @s[tag=!tiny_abyss] run function ARG(_PATH)check_next
						
						playsound minecraft:enemy.abyss.tiny_dead player @a ~ ~ ~ 3 1 0
						kill @s
					}
				}
				
				func set_idle()
				{
					execute as 00000001-0000-0001-0000-000100000001 run function abyss:set_variant/invisible
					```
					print(f"tp 00000001-0000-0001-0000-000100000001 {STAGE_CENTER[2][0] + 23} {GROUND_Y[2] + 0.5} {STAGE_CENTER[2][1]}")
					```
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.fear/stop
				}
			}
			
			folder swallow()
			{
				func cast()
				{
					scoreboard players set #USE_RANDOM_SKILL number 0
					scoreboard players set #SWALLOW_CD number 1
					schedule function ARG(_PATH)reset_cd 40s
					
					# 鑽洞動作
					function abyss:animations/animation.model.loop/stop
					function abyss:animations/animation.model.fear/play
					```
					# 移走碰撞用的史萊姆
					print(f"tp 7fffffff-7fff-ffff-7fff-ffff7fffffff {STAGE_CENTER[2][0]} {GROUND_Y[2] - 20} {STAGE_CENTER[2][1]}")
					```
					# 動畫過程無敵狀態
					tag @s add effect
					
					# 暫停圍堵跟捕食
					schedule clear ARG(__PATH)sting/spawn
					schedule clear ARG(__PATH)hunt/start
					schedule clear ARG(__PATH)hunt/summon_sting
					
					schedule function ARG(_PATH)set_pos 30t
				}
				
				func set_pos()
				{
					# 隨機四個方位
					function random:rand_rate
					```
					print('execute if score #RAND_RESULT number matches 0..24 positioned %.2f %.2f %.2f rotated 0 0 as 00000001-0000-0001-0000-000100000001 run function abyss:move' %(STAGE_CENTER[2][0] + 24, GROUND_Y[2] + 0.5, STAGE_CENTER[2][1] - 4))
					print('execute if score #RAND_RESULT number matches 25..49 positioned %.2f %.2f %.2f rotated 180 0 as 00000001-0000-0001-0000-000100000001 run function abyss:move' %(STAGE_CENTER[2][0] - 24, GROUND_Y[2] + 0.5, STAGE_CENTER[2][1] + 4))
					print('execute if score #RAND_RESULT number matches 50..74 positioned %.2f %.2f %.2f rotated 90 0 as 00000001-0000-0001-0000-000100000001 run function abyss:move' %(STAGE_CENTER[2][0] + 4, GROUND_Y[2] + 0.5, STAGE_CENTER[2][1] + 24))
					print('execute if score #RAND_RESULT number matches 75..99 positioned %.2f %.2f %.2f rotated 270 0 as 00000001-0000-0001-0000-000100000001 run function abyss:move' %(STAGE_CENTER[2][0] - 4, GROUND_Y[2] + 0.5, STAGE_CENTER[2][1] - 24))
					```
					execute if score #RAND_RESULT number matches 0..24 run scoreboard players set #ABYSS_FACING number 0
					execute if score #RAND_RESULT number matches 25..49 run scoreboard players set #ABYSS_FACING number 1
					execute if score #RAND_RESULT number matches 50..74 run scoreboard players set #ABYSS_FACING number 2
					execute if score #RAND_RESULT number matches 75..99 run scoreboard players set #ABYSS_FACING number 3
					
					schedule function ARG(_PATH)stop_shake 3s
					# 開始震動特效
					scoreboard players set #SHAKING number 1
					execute as @a[scores={player=1..}] run function ARG(_PATH)set_shake
					```
					print(f"playsound minecraft:enemy.abyss.shake player @a {STAGE_CENTER[2][0]} {GROUND_Y[2]} {STAGE_CENTER[2][1]} 5 1 1")
					```
				}
				
				func set_shake()
				{
					effect give @s glowing 1000000 0 true
					execute unless score @s beamtime matches 0..2 run team join green
				}
				
				func stop_shake()
				{
					# 避免距離太遠失效，重新呼叫一次移動
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:move
					execute at 00000001-0000-0001-0000-000100000001 run playsound minecraft:enemy.abyss.swallow player @a ~ ~ ~ 5 1 1
					
					scoreboard players reset #SHAKING number
					execute as @a[scores={player=1..}] unless score @s beamtime matches 0..2 run function ARG(_PATH)reset_shake
					schedule function ARG(_PATH)start_move 1s
				}
				
				func reset_shake()
				{
					effect clear @s glowing
					team leave @s
				}
				
				func start_move()
				{
					scoreboard players set #SWALLOW number 1
					
					execute as 00000001-0000-0001-0000-000100000001 run function abyss:set_variant/default
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.swallow/play
					tag 00000001-0000-0001-0000-000100000001 remove effect
					
					schedule function ARG(_PATH)set_idle 102t
				}
				
				func switch_keep()
				{
					scoreboard players set #SWALLOW number 2
					
					function abyss:animations/animation.model.swallow/pause
					function abyss:animations/animation.model.swallow_loop/play
				}
				
				func set_idle()
				{
					function ARG(_PATH)stop_move
					
					execute as 00000001-0000-0001-0000-000100000001 run function abyss:set_variant/invisible
					```
					print(f"tp 00000001-0000-0001-0000-000100000001 {STAGE_CENTER[2][0] + 23} {GROUND_Y[2] + 0.5} {STAGE_CENTER[2][1]} 0 0")
					```
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:move
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.swallow_loop/stop
					
					schedule function stage:stage3/skills/boss_in/start 1s
				}
				
				func stop_move()
				{
					scoreboard players reset #SWALLOW number
					
					# 重新加回無敵
					tag 00000001-0000-0001-0000-000100000001 add effect
					
					# 重啟圍堵跟捕食
					schedule function ARG(__PATH)sting/spawn 3s
					schedule function ARG(__PATH)hunt/start 15s
				}
				
				func move()
				{
					# 場地 48 格，身體長度 22 格，總共大約要移動 70 格
					tp @s ^-0.7 ^ ^
					
					execute if score #ABYSS_FACING number matches 0 positioned ~ ~-0.5 ~-4.25 as @a[gamemode=adventure,scores={player=1..},dx=21,dy=7,dz=7.5] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 2 true
					execute if score #ABYSS_FACING number matches 1 positioned ~-22 ~-0.5 ~-4.25 as @a[gamemode=adventure,scores={player=1..},dx=21,dy=7,dz=7.5] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 2 true
					execute if score #ABYSS_FACING number matches 2 positioned ~-4.25 ~-0.5 ~ as @a[gamemode=adventure,scores={player=1..},dx=7.5,dy=7,dz=21] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 2 true
					execute if score #ABYSS_FACING number matches 3 positioned ~-4.25 ~-0.5 ~-22 as @a[gamemode=adventure,scores={player=1..},dx=7.5,dy=7,dz=21] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 2 true
				}
				
				# 被光束炮打到，中斷技能
				func shut()
				{
					scoreboard players operation @s display_number *= #2 number
					
					execute if score #SWALLOW number matches 1 run function abyss:animations/animation.model.swallow/pause
					execute if score #SWALLOW number matches 2 run function abyss:animations/animation.model.swallow_loop/pause
					function abyss:animations/animation.model.swallow_shut/play
					
					schedule clear ARG(_PATH)set_idle
					function ARG(_PATH)stop_move
				}
				
				func set_idle_shut()
				{
					execute as 00000001-0000-0001-0000-000100000001 run function abyss:set_variant/invisible
					```
					print(f"tp 00000001-0000-0001-0000-000100000001 {STAGE_CENTER[2][0] + 23} {GROUND_Y[2] + 0.5} {STAGE_CENTER[2][1]} 0 0")
					```
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:move
					execute as 00000001-0000-0001-0000-000100000001 at @s run function abyss:animations/animation.model.swallow_shut/stop
					
					stopsound @a player minecraft:enemy.abyss.shake
					schedule function stage:stage3/skills/boss_in/start 1s
				}
				
				func reset_cd()
				{
					scoreboard players reset #SWALLOW_CD number
				}
			}
			
			folder resonance()
			{
				func cast()
				{
					scoreboard players set #USE_RANDOM_SKILL number 0
					scoreboard players set #RESONANCE_CD number 1
					schedule function ARG(_PATH)reset_cd 90s
					
					# 暫停圍堵跟捕食
					schedule clear ARG(__PATH)sting/spawn
					schedule clear ARG(__PATH)hunt/start
					schedule clear ARG(__PATH)hunt/summon_sting
					
					schedule function ARG(_PATH)start_effect 1s
				}
				
				func start_effect()
				{
					# 動畫過程無敵狀態
					tag @s add effect
					```
					print(f"playsound minecraft:enemy.abyss.open_eye player @a {STAGE_CENTER[2][0]} {GROUND_Y[2]} {STAGE_CENTER[2][1]} 5 1 1")
					```
					execute as @e[type=zombie,tag=active_eye] run function ARG(_PATH)eye/pause
					title @a times 3 100 0
					title @a title {"text":"0","font":"ui:main"}
					
					scoreboard players set #EYE_ANIM number 0
				}
				
				folder anim()
				{
					func run()
					{
						scoreboard players add #EYE_ANIM number 1
						
						execute if score #EYE_ANIM number matches 20 run title @a times 0 100 0
						execute if score #EYE_ANIM number matches 21 run title @a title [{"text":"FF","font":"space:default"},{"text":"0","font":"ui:main"},{"text":"FF","font":"space:default"},{"text":"98","font":"space:default"},{"text":"2","font":"ui:main"},{"text":"98","font":"space:default"}]
						execute if score #EYE_ANIM number matches 22 run title @a title [{"text":"FF","font":"space:default"},{"text":"0","font":"ui:main"},{"text":"FF","font":"space:default"},{"text":"98","font":"space:default"},{"text":"3","font":"ui:main"},{"text":"98","font":"space:default"}]
						execute if score #EYE_ANIM number matches 23 run title @a title [{"text":"FF","font":"space:default"},{"text":"0","font":"ui:main"},{"text":"FF","font":"space:default"},{"text":"98","font":"space:default"},{"text":"4","font":"ui:main"},{"text":"98","font":"space:default"}]
						execute if score #EYE_ANIM number matches 24 run title @a title [{"text":"FF","font":"space:default"},{"text":"0","font":"ui:main"},{"text":"FF","font":"space:default"},{"text":"98","font":"space:default"},{"text":"5","font":"ui:main"},{"text":"98","font":"space:default"}]
						```
						# 雜訊音效
						print(f"execute if score #EYE_ANIM number matches 44 run playsound minecraft:enemy.abyss.noise player @a {STAGE_CENTER[2][0]} {GROUND_Y[2]} {STAGE_CENTER[2][1]} 5 1 1")
						```
						execute if score #EYE_ANIM number matches 44 run title @a title [{"text":"FF","font":"space:default"},{"text":"0","font":"ui:main"},{"text":"FF","font":"space:default"},{"text":"98","font":"space:default"},{"text":"6","font":"ui:main"},{"text":"98","font":"space:default"}]
						execute if score #EYE_ANIM number matches 46 run title @a title [{"text":"FF","font":"space:default"},{"text":"0","font":"ui:main"},{"text":"FF","font":"space:default"},{"text":"98","font":"space:default"},{"text":"5","font":"ui:main"},{"text":"98","font":"space:default"}]
						
						execute if score #EYE_ANIM number matches 60 run function ARG(_PATH)end
					}
					
					func end()
					{
						```
						print(f"playsound minecraft:enemy.abyss.summon_eye player @a {STAGE_CENTER[2][0]} {GROUND_Y[2]} {STAGE_CENTER[2][1]} 5 1 1")
						
						print(f"execute positioned {STAGE_CENTER[2][0] + 6} {GROUND_Y[2]} {STAGE_CENTER[2][1] + 6} run function ARG(__PATH)eye/summon")
						print(f"execute positioned {STAGE_CENTER[2][0] + 6} {GROUND_Y[2]} {STAGE_CENTER[2][1] - 6} run function ARG(__PATH)eye/summon")
						print(f"execute positioned {STAGE_CENTER[2][0] - 6} {GROUND_Y[2]} {STAGE_CENTER[2][1] + 6} run function ARG(__PATH)eye/summon")
						print(f"execute positioned {STAGE_CENTER[2][0] - 6} {GROUND_Y[2]} {STAGE_CENTER[2][1] - 6} run function ARG(__PATH)eye/summon")
						```
						function ARG(__PATH)eye/aim
						schedule function ARG(__PATH)eye/activate 1s
						
						title @a times 0 0 3
						title @a title [{"text":"7","font":"ui:main"}]
						
						# 結束無敵
						tag 00000001-0000-0001-0000-000100000001 remove effect
						
						# 重啟圍堵跟捕食
						schedule function stage:stage3/skills/sting/spawn 3s
						schedule function stage:stage3/skills/hunt/start 15s
						schedule function stage:stage3/skills/loop 4s
					}
				}
				
				folder eye()
				{
					func summon()
					{
						summon zombie ~ ~ ~ {Invulnerable:1b,Silent:1b,NoAI:1b,Glowing:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:69}}],Attributes:[{Name:"minecraft:generic.max_health",Base:1024.0}],Health:100.0f,Team:"enemy",Tags:["object","dead_event","eye","new_eye","effect"]}
						execute positioned ~ ~-1 ~ run function ARG(__PATH)thunder_effect
					}
					
					func aim()
					{
						execute as @e[type=zombie,tag=eye] at @s facing entity @p[gamemode=adventure,scores={player=1..}] feet run tp @s ~ ~ ~ ~ 0
						schedule function ARG(_PATH)rotate 5s
					}
					
					func rotate()
					{
						execute as @e[type=zombie,tag=eye] at @s run tp @s ~ ~ ~ ~120 0
						schedule function ARG(_PATH)rotate 5s
					}
					
					func activate()
					{
						execute as @e[type=zombie,tag=pause_eye] run function ARG(_PATH)resume
						execute as @e[type=zombie,tag=new_eye] run function ARG(_PATH)start_move
					}
					
					func start_move()
					{
						tag @s add active_eye
						tag @s remove new_eye
						tag @s remove effect
						execute store result score @s number run scoreboard players add #GLOBAL number 1
					}
					
					func move()
					{
						tp @s ^ ^ ^0.15
						execute positioned ~-0.5 ~0.8 ~-0.5 as @a[gamemode=adventure,scores={player=1..},dx=0,dy=0,dz=0] unless score @s beamtime matches 0.. run effect give @s instant_damage 1 0 true
					}
					
					func pause()
					{
						tag @s remove active_eye
						tag @s add pause_eye
						tag @s add effect
					}
					
					func resume()
					{
						tag @s add active_eye
						tag @s remove pause_eye
						tag @s remove effect
					}
					
					func hurt()
					{
						execute store result entity @s Health float 0.1 run scoreboard players get @s health
						execute if score #GAME_START number matches 1 if score @s health matches ..0 run function ARG(_PATH)dead
					}
					
					func dead()
					{
						```
						print(f"playsound minecraft:enemy.abyss.eye_dead player @a {STAGE_CENTER[2][0]} {GROUND_Y[2]} {STAGE_CENTER[2][1]} 5 1 1")
						```
						title @a[gamemode=adventure,scores={player=1..}] times 0 0 3
						title @a[gamemode=adventure,scores={player=1..}] title {"text":"7","font":"ui:main"}
						
						execute unless score #BLIND number matches 9 run scoreboard players add #BLIND number 1
						data modify storage skill:main blind set value '[{"text":"FF","font":"space:default"},{"score":{"name":"#BLIND","objective":"number"},"font":"ui:blind"},{"text":"FF","font":"space:default"}]'
						kill @s
					}
				}
				
				func thunder_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:thunder_purple/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:thunder_purple/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func reset_cd()
				{
					scoreboard players reset #RESONANCE_CD number
				}
			}
		}
	}
}