import utils, loop

```
GROUND_Y = [4.0, 4.0, 4.0]
STAGE_CENTER = [(128, 0), (128, 0), (128, 0)]
```

namespace stage()
{
	func install()
	{
		scoreboard objectives add death deathCount
		scoreboard objectives add game_id dummy
		
		scoreboard players set #PLAYER_MODE number 1
		scoreboard players set #SELECTED_STAGE number 1
		scoreboard players set #ZONE_MAX number 10000
		# 破壞後5秒回復
		scoreboard players operation #ZONE_RECOVER number = #ZONE_MAX number
		scoreboard players operation #ZONE_RECOVER number /= #100 number
		
		data modify storage stage:stage1 DefaultPose set value [324f,80f,0f]
		data modify storage stage:stage1 ZonePose set value [[280f,350f,40f],[280f,15f,80f],[300f,20f,130f]]
		
		bossbar add boss "BOSS"
		bossbar set minecraft:boss color red
		bossbar add zone {"translate":"Zone"}
		execute store result bossbar minecraft:zone max run scoreboard players get #ZONE_MAX number
		
		# animated java 模型
		function miko:install
	}
	
	func main()
	{
		execute as @a[scores={player=1..,death=1..}] run function ARG(_PATH)check_end
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/main
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/main
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/main
	}
	
	func check_end()
	{
		scoreboard players set @s player 0
		scoreboard players reset @s death
		gamemode spectator @s
		execute unless entity @a[scores={player=1..},gamemode=adventure] run function ARG(_PATH)end
	}
	
	# 點告示牌觸發，需先決定好2P(只要設定player分數為2就好)
	func start()
	{
		# 人數模式設定成指定模式
		scoreboard players operation #PLAYER_MODE number = #PLAYER_MODE_SET number
		
		clear @s
		function main:effect
		function ARG(_PATH)difficulty_effect
		
		scoreboard players set @s player 1
		scoreboard players set @s weapon_mode 1
		item replace entity @s weapon.mainhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		item replace entity @s weapon.offhand with minecraft:carrot_on_a_stick{display:{Name:'{"translate":"Immortal Edge","bold":true,"italic":false}'},CustomModelData:1,weapon:1b}
		
		gamemode spectator @a[scores={player=0}]
		execute unless score #PLAYER_MODE number matches 3 run gamemode spectator @a[scores={player=2}]
		# reset beam CD
		data modify storage skill:main beam_bar set value ["4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4","4"]
		scoreboard players set #BEAM_CD number 10
		schedule function skill:gun/beam/timer/countdown 3s
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/start
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/start
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/start
		
		bossbar set minecraft:boss players @a
		bossbar set minecraft:boss visible true
		
		effect give @a instant_health 1 5 true
		execute as @a[scores={player=2}] run function ARG(_PATH)set_player2
		execute store result score @a[scores={player=1..}] game_id run scoreboard players add #GLOBAL game_id 1
	}
	
	func set_player2()
	{
		clear @s
		function main:effect
		function ARG(_PATH)difficulty_effect
		
		scoreboard players set @s weapon_mode -1
		item replace entity @s weapon.mainhand with minecraft:spyglass{display:{Name:'{"translate":"Supreme Pulse","bold":true,"italic":false}'},weapon:1b}
	}
	
	func difficulty_effect()
	{
		attribute @s minecraft:generic.max_health modifier remove c655e15b-1539-43df-80c9-55feb92b4e2b
		effect clear @s minecraft:regeneration
		
		execute if score #DIFFICULTY number matches 1 run attribute @s minecraft:generic.max_health modifier add c655e15b-1539-43df-80c9-55feb92b4e2b difficulty 10 add
		execute if score #DIFFICULTY number matches ..2 run effect give @s minecraft:regeneration 1000000 1 true
		execute if score #DIFFICULTY number matches 4 run attribute @s minecraft:generic.max_health modifier add c655e15b-1539-43df-80c9-55feb92b4e2b difficulty -14 add
	}
	
	func end()
	{
		bossbar set minecraft:boss visible false
		kill @e[tag=object]
		clear @a
		scoreboard players set @a weapon_mode 1
		scoreboard players set @a player 0
		title @a actionbar ""
		
		execute if score #SELECTED_STAGE number matches 1 run function ARG(_PATH)stage1/end
		execute if score #SELECTED_STAGE number matches 2 run function ARG(_PATH)stage2/end
		execute if score #SELECTED_STAGE number matches 3 run function ARG(_PATH)stage3/end
	}
	
	# 真白之刃
	folder stage1()
	{
		func start()
		{
			bossbar set minecraft:boss max 5000
			execute store result bossbar minecraft:boss value run bossbar get minecraft:boss max
			bossbar set minecraft:boss name {"translate":"Sacred Knight","color":"white","bold":true}
			
			bossbar set minecraft:zone players @a
			bossbar set minecraft:zone visible true
			function ARG(_PATH)skills/zone/reset
			```
			print(f"tp @a {STAGE_CENTER[0][0]} {GROUND_Y[0]} {STAGE_CENTER[0][1]} 180 50")
			print(f"execute positioned {STAGE_CENTER[0][0]} {GROUND_Y[0]} {STAGE_CENTER[0][1] - 8} run function ARG(_PATH)summon_boss")
			```
			
			schedule function ARG(_PATH)skills/loop 7s
			schedule function ARG(_PATH)skills/fly_sword/start 2s
		}
		
		func summon_boss()
		{
			summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,ShowArms:1b,DisabledSlots:4144959,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:2,CustomPotionColor:-1}}],HandItems:[{id:"minecraft:potion",Count:1b,tag:{CustomModelData:14,CustomPotionColor:-1}},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:4,CustomPotionColor:-1}}],Pose:{RightArm:[324f,80f,0f],LeftArm:[355f,0f,350f]},Tags:["boss","knockback_resist","object"],UUID:[I;1,1,1,1]}
			summon armor_stand ~ ~0.8 ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,CustomNameVisible:1b,CustomName:'{"text":"0","font":"skill:zone"}',Tags:["effect","object"],UUID:[I;18,18,18,18]}
			summon marker ~ ~ ~ {Tags:["object"],UUID:[I;16,16,16,16]}
			scoreboard players set 00000001-0000-0001-0000-000100000001 number 1
			execute store result score 00000001-0000-0001-0000-000100000001 health run bossbar get minecraft:boss max
			# 飛劍
			summon armor_stand ~2 ~ ~ {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Tags:["effect","object"],UUID:[I;2,2,2,2],Rotation:[180f,0f]}
			# 翅膀裝飾
			summon husk ~1.63135 ~-1.2 ~-0.76071 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;4,4,4,4]}
			summon husk ~1.13278 ~-1.1 ~-1.39886 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;5,5,5,5]}
			summon husk ~0.40491 ~-1 ~-1.75387 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;6,6,6,6]}
			summon husk ~-0.40491 ~-1 ~-1.75387 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;7,7,7,7]}
			summon husk ~-1.13278 ~-1.1 ~-1.39886 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;8,8,8,8]}
			summon husk ~-1.63135 ~-1.2 ~-0.76071 {Silent:1b,Invulnerable:1b,NoAI:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:5}}],Rotation:[0f,-60f],Tags:["object","wing"],Team:"enemy",UUID:[I;9,9,9,9]}
			effect give @e[type=husk,tag=wing] invisibility 1000000 0 true
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
			schedule clear ARG(_PATH)skills/zone/auto_reset_pose
			schedule clear ARG(_PATH)skills/fly_sword/stop
			schedule clear ARG(_PATH)skills/fly_sword/aim
			schedule clear ARG(_PATH)skills/fly_sword/start
			schedule clear ARG(_PATH)skills/revert_action
			function ARG(_PATH)skills/sword_dance/shutdown
			function ARG(_PATH)skills/flaw/shutdown
			function ARG(_PATH)skills/chop/shutdown
			function ARG(_PATH)skills/sanction/shutdown
			
			bossbar set minecraft:zone visible false
			scoreboard players set #ZONE number 0
			scoreboard players set #ZONE_ACTIVE number 0
			scoreboard players set #ZONE_BREAK number 0
			scoreboard players set #FLYING_SWORD number 0
			scoreboard players set #SWORD_DANCE number 0
			scoreboard players set #WING_OPEN number 0
			scoreboard players set #SLASH_COUNTDOWN number 0
			scoreboard players set #CHOP_COUNTDOWN number 0
			scoreboard players set #BACKSTEP number 0
			scoreboard players set #ANIM_LOCK number 0
			scoreboard players set #REVERT_ACTION number 0
			
			kill 00000013-0000-0013-0000-001300000013
			
			effect give @a instant_health 1 10 true
		}
		
		func main()
		{
			execute at 00000001-0000-0001-0000-000100000001 positioned ~ ~0.8 ~ run tp 00000012-0000-0012-0000-001200000012 ~ ~ ~
			execute unless score #ANIM_LOCK number matches 1 unless score #REVERT_ACTION number matches 1 unless score #ZONE_BREAK number matches 1 as 00000001-0000-0001-0000-000100000001 at @s facing entity @p[scores={player=1..},gamemode=adventure] feet run function ARG(_PATH)face_player
			
			execute if score #ZONE number < #ZONE_MAX number run function ARG(_PATH)skills/zone/recover
			execute if score #FLYING_SWORD number matches 1 as 00000002-0000-0002-0000-000200000002 at @s run function ARG(_PATH)skills/fly_sword/move
			
			execute if score #SWORD_DANCE number matches 1.. as 00000003-0000-0003-0000-000300000003 at @a[tag=boss_target,limit=1] facing entity 00000001-0000-0001-0000-000100000001 eyes run tp @s ~ ~ ~ ~180 0
			execute if score #SWORD_DANCE number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/sword_dance/trace
			
			execute if score #SLASH_COUNTDOWN number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/slash/slash_countdown
			execute if score #CHOP_COUNTDOWN number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/chop/chop_countdown
			
			execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)wing/check
			execute if score #WING_OPEN number matches 1..10 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)skills/sword_dance/open_wing/anim
			
			execute if score #WING_OPEN number matches -1 as 0000000e-0000-000e-0000-000e0000000e at @s run function ARG(_PATH)skills/sanction/follow
			execute if score #SANCTION_ANIM number matches 0.. as 00000001-0000-0001-0000-000100000001 run function ARG(_PATH)skills/sanction/hand_anim/run
		}
		
		func face_player()
		{
			tp 00000010-0000-0010-0000-001000000010 ~ ~ ~ ~ 0
			data modify entity @s Rotation set from entity 00000010-0000-0010-0000-001000000010 Rotation
			execute if score #BACKSTEP number matches 1 at @s rotated ~ 0 run function ARG(_PATH)backstep
		}
		
		func backstep()
		{
			execute if block ^ ^ ^-3 #skill:canpass run tp @s ^ ^ ^-3
			execute unless block ^ ^ ^-3 #skill:canpass if block ^ ^ ^-2 #skill:canpass run tp @s ^ ^ ^-2
			execute unless block ^ ^ ^-3 #skill:canpass unless block ^ ^ ^-2 #skill:canpass if block ^ ^ ^-1 #skill:canpass run tp @s ^ ^ ^-1
			
			scoreboard players set #BACKSTEP number 0
		}
		
		folder wing()
		{
			func check()
			{
				execute if score #WING_OPEN number matches 0 run function ARG(_PATH)normal
				execute if score #WING_OPEN number matches 11.. run function ARG(_PATH)fly
			}
			
			func normal()
			{
				execute rotated ~65 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000004-0000-0004-0000-000400000004 ~ ~-0.5 ~ ~ -60
				execute rotated ~39 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000005-0000-0005-0000-000500000005 ~ ~-0.4 ~ ~ -60
				execute rotated ~13 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000006-0000-0006-0000-000600000006 ~ ~-0.3 ~ ~ -60
				execute rotated ~-13 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000007-0000-0007-0000-000700000007 ~ ~-0.3 ~ ~ -60
				execute rotated ~-39 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000008-0000-0008-0000-000800000008 ~ ~-0.4 ~ ~ -60
				execute rotated ~-65 ~ positioned ^ ^ ^-1.8 facing entity @s feet run tp 00000009-0000-0009-0000-000900000009 ~ ~-0.5 ~ ~ -60
			}
			
			func fly()
			{
				execute rotated ~60 ~ positioned ^ ^ ^-0.7 facing entity @s feet run tp 00000004-0000-0004-0000-000400000004 ~ ~1.3 ~ ~ 50
				execute rotated ~45 ~ positioned ^ ^ ^-1.3 facing entity @s feet run tp 00000005-0000-0005-0000-000500000005 ~ ~0.5 ~ ~ 10
				execute rotated ~30 ~ positioned ^ ^ ^-1.6 facing entity @s feet run tp 00000006-0000-0006-0000-000600000006 ~ ~0.2 ~ ~ -10
				execute rotated ~-30 ~ positioned ^ ^ ^-1.6 facing entity @s feet run tp 00000007-0000-0007-0000-000700000007 ~ ~0.2 ~ ~ -10
				execute rotated ~-45 ~ positioned ^ ^ ^-1.3 facing entity @s feet run tp 00000008-0000-0008-0000-000800000008 ~ ~0.5 ~ ~ 10
				execute rotated ~-60 ~ positioned ^ ^ ^-0.7 facing entity @s feet run tp 00000009-0000-0009-0000-000900000009 ~ ~1.3 ~ ~ 50
			}
		}
		
		folder skills()
		{
			func loop()
			{
				# 有要後退先後退
				execute if score #BACKSTEP number matches 1 as 00000001-0000-0001-0000-000100000001 at @s rotated ~ 0 run function ARG(__PATH)backstep
				execute if score #REVERT_ACTION number matches 1 run function ARG(_PATH)revert_action
				
				schedule function ARG(_PATH)loop 5s
				execute if score #ZONE_ACTIVE number matches 1 unless score #SWORD_DANCE number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)rand_skill
			}
			
			func rand_skill()
			{
				function random:rand_rate
				
				scoreboard players set #NEAR_SKILL number 0
				execute if entity @a[scores={player=1..},gamemode=adventure,distance=..5,limit=1] run scoreboard players add #NEAR_SKILL number 1
				execute if entity @a[scores={player=1..},gamemode=adventure,distance=5.01..,limit=1] run scoreboard players add #NEAR_SKILL number 2
				
				execute if score #NEAR_SKILL number matches 1 run function ARG(_PATH)near_judge
				execute if score #NEAR_SKILL number matches 2 run function ARG(_PATH)far_judge
				execute unless score #NEAR_SKILL number matches 1..2 run function ARG(_PATH)rand_range
			}
			
			func rand_range()
			{
				execute if score #RAND_RESULT number matches ..49 run function ARG(_PATH)near_judge
				execute if score #RAND_RESULT number matches 50.. run function ARG(_PATH)far_judge
				# 重骰一次避免隨機距離跟放的招相關
				function random:rand_rate
			}
			
			func near_judge()
			{
				execute if score @s health matches ..2500 if score #RAND_RESULT number matches 40..59 run function ARG(_PATH)slash/cast
				# function ARG(_PATH)slash/cast
				
				# 近距離有機率3秒就放下一招
				execute if score #RAND_RESULT number matches 30..69 run schedule function ARG(_PATH)loop 3s
				
				execute if score #RAND_RESULT number matches 0..49 run function ARG(_PATH)flaw/cast
				execute if score #RAND_RESULT number matches 50..99 run function ARG(_PATH)chop/cast
			}
			
			func far_judge()
			{
				execute if score @s health matches ..2500 if score #RAND_RESULT number matches 24..43 run function ARG(_PATH)slash/cast
				# function ARG(_PATH)slash/cast
				
				execute if score #RAND_RESULT number matches 0..29 run function ARG(_PATH)sword_dance/cast
				execute if score #RAND_RESULT number matches 30..99 run function ARG(_PATH)sanction/cast
			}
			
			func reset_pose()
			{
				data modify entity @s ArmorItems[3].tag.CustomModelData set value 2
				data modify entity @s HandItems[0].tag.CustomModelData set value 14
				data modify entity @s Pose.RightArm set from storage stage:stage1 DefaultPose
				data modify entity @s Pose.LeftArm set value [355f,0f,350f]
			}
				
			func revert_action()
			{
				execute if score #ZONE_ACTIVE number matches 1 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)inner_revert_action
				
				scoreboard players set #REVERT_ACTION number 0
				scoreboard players set #WING_OPEN number 0
				schedule clear ARG(_PATH)revert_action
			}
			
			func inner_revert_action()
			{
				```
				print(f"execute positioned ~ 0.0 ~ if entity @s[dx=0,dy={GROUND_Y[0] - 1},dz=0] run tp @s ~ {GROUND_Y[0]} ~")
				```
				function ARG(_PATH)reset_pose
				data modify entity @s NoGravity set value 0b
				
				# particle minecraft:cloud ~ ~1 ~ 0.7 0.7 0.7 0.1 100
				particle minecraft:flash ~ ~1 ~ 0 0 0 1 1
			}
			
			folder dodge()
			{
				func try()
				{
					scoreboard players set #DODGE_SUCCESS number 0
					function ARG(_PATH)rand_side
					
					execute if score #DODGE_SIDE number matches 0 positioned ^1.8 ^ ^ if block ~ ~1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SIDE number matches 1 positioned ^-1.8 ^ ^ if block ~ ~1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SIDE number matches 0 if score #DODGE_SUCCESS number matches 0 positioned ^1.8 ^ ^ if block ~ ~1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SIDE number matches 1 if score #DODGE_SUCCESS number matches 0 positioned ^-1.8 ^ ^ if block ~ ~1 ~ #skill:canpass run function ARG(_PATH)tp
					execute if score #DODGE_SUCCESS number matches 0 run function ARG(_PATH)random_tp
					
					scoreboard players set #IGNORE_DAMAGE number 1
				}
				func rand_side() from random().generate(0, 1, "#DODGE_SIDE number");
				
				func tp()
				{
					scoreboard players set #DODGE_SUCCESS number 1
					tp @s ~ ~ ~
				}
				
				func random_tp()
				{
					```
					print(f"spreadplayers ~ ~ 0 7 under {int(GROUND_Y[0] + 3)} false @s")
					```
					execute at @s unless entity @e[type=armor_stand,tag=blast_target,distance=..2,limit=1] unless entity @a[distance=..5,limit=1] run scoreboard players set #DODGE_SUCCESS number 1
					execute if score #DODGE_SUCCESS number matches 0 run function ARG(_PATH)random_tp
				}
			}
			
			folder zone()
			{
				func recover()
				{
					execute if score #FAST_RECOVER number matches 1.. run scoreboard players remove #FAST_RECOVER number 1
					
					execute if score #ZONE_BREAK number matches 1 run scoreboard players operation #ZONE number += #ZONE_RECOVER number
					execute unless score #ZONE_BREAK number matches 1 run scoreboard players add #ZONE number 3
					execute unless score #ZONE_BREAK number matches 1 unless score #FAST_RECOVER number matches 1.. run scoreboard players add #ZONE number 22
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number < #ZONE_MAX number
					
					execute if score #ZONE_BREAK number matches 1 if score #ZONE number >= #ZONE_MAX number run function ARG(_PATH)reset
				}
				
				func reset()
				{
					bossbar set minecraft:zone color white
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number = #ZONE_MAX number
					
					execute if score #ZONE_BREAK number matches 1 as 00000001-0000-0001-0000-000100000001 at @s run function ARG(__PATH)inner_revert_action
					execute if score #ZONE_BREAK number matches 1 run function ARG(__PATH)fly_sword/aim
					data modify entity 00000012-0000-0012-0000-001200000012 CustomNameVisible set value 1b
					
					scoreboard players set #ZONE_ACTIVE number 1
					scoreboard players set #ZONE_BREAK number 0
				}
				
				func hit()
				{
					execute if score #DIFFICULTY number matches 1 run scoreboard players set #FAST_RECOVER number 100
					execute if score #DIFFICULTY number matches 2 run scoreboard players set #FAST_RECOVER number 60
					execute if score #DIFFICULTY number matches 3 run scoreboard players set #FAST_RECOVER number 40
					execute if score #DIFFICULTY number matches 4 run scoreboard players set #FAST_RECOVER number 30
					# 子彈影響快速回復的時間較少
					execute if score @s display_number matches ..50 run scoreboard players operation #FAST_RECOVER number /= #2 number
					
					execute facing entity @p[scores={player=1..},gamemode=adventure] feet rotated ~ 0 run tp @s ~ ~ ~ ~ ~
					execute if score #REVERT_ACTION number matches 1 run function ARG(__PATH)revert_action
					
					execute if entity @s[tag=beam_hit] at @s run function ARG(__PATH)dodge/try
					execute if entity @s[tag=beam_hit] run function ARG(__PATH)reset_pose
					execute if entity @s[tag=!beam_hit] at @s run function ARG(_PATH)decrease
					scoreboard players set #IGNORE_DAMAGE number 1
				}
				
				func decrease()
				{
					execute store result bossbar minecraft:zone value run scoreboard players operation #ZONE number -= @s display_number
					execute if score @s display_number matches ..110 run playsound minecraft:enemy.sword.strike_small player @a ~ ~ ~ 3 1 0
					execute if score @s display_number matches 111.. run playsound minecraft:enemy.sword.strike_big player @a ~ ~ ~ 3 1 0
					
					data modify entity @s HandItems[0].tag.CustomModelData set value 3
					data modify entity @s Pose.RightArm set from storage stage:stage1 ZonePose[0]
					data modify storage stage:stage1 ZonePose append from storage stage:stage1 ZonePose[0]
					data remove storage stage:stage1 ZonePose[0]
					
					execute positioned ^ ^0.5 ^0.5 run function ARG(_PATH)strike_effect
					execute if score #ZONE number matches ..0 run function ARG(_PATH)break
					
					execute if score #ZONE_ACTIVE number matches 1 run schedule function ARG(_PATH)auto_reset_pose 2s
				}
				
				func auto_reset_pose()
				{
					execute if score #ZONE_ACTIVE number matches 1 unless score #ANIM_LOCK number matches 1 as 00000001-0000-0001-0000-000100000001 run function ARG(__PATH)reset_pose
				}
				
				func break()
				{
					```
					print(f"tp @s ~ {GROUND_Y[0] - 0.45} ~")
					print(f"execute positioned ~ {GROUND_Y[0] + 0.35} ~ run function ARG(_PATH)zone_break_effect")
					print(f"execute positioned ~ {GROUND_Y[0]} ~ run function ARG(_PATH)zone_expand_effect")
					```
					data modify entity 00000012-0000-0012-0000-001200000012 CustomNameVisible set value 0b
					
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 9
					data modify entity @s Pose.RightArm set value [87f,150f,0f]
					data modify entity @s Pose.LeftArm set value [95f,210f,0f]
					data modify entity @s NoGravity set value 1b
					particle flash ~ ~1 ~ 0 0 0 1 2
					playsound minecraft:enemy.sword.strike_big player @a ~ ~ ~ 3 1 0
					
					bossbar set minecraft:zone color blue
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ZONE number 0
					scoreboard players set #ZONE_BREAK number 1
					
					schedule clear ARG(_PATH)auto_reset_pose
					function ARG(__PATH)fly_sword/pause
					function ARG(__PATH)sanction/shutdown
				}
				
				func strike_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:strike/0"}',CustomNameVisible:1,Duration:4,Tags:['{"text":"","font":"font_animation:strike/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func zone_break_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:zone_break/0"}',CustomNameVisible:1,Duration:4,Tags:['{"text":"","font":"font_animation:zone_break/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func zone_expand_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {UUID:[I;19,19,19,19],CustomName:'{"text":"0","font":"font_animation:zone_expand/9"}',CustomNameVisible:1,Duration:101,Tags:['{"text":"","font":"font_animation:zone_expand/9"}', '{"text":"","font":"font_animation:zone_expand/8"}', '{"text":"","font":"font_animation:zone_expand/7"}', '{"text":"","font":"font_animation:zone_expand/6"}', '{"text":"","font":"font_animation:zone_expand/5"}', '{"text":"","font":"font_animation:zone_expand/4"}', '{"text":"","font":"font_animation:zone_expand/3"}', '{"text":"","font":"font_animation:zone_expand/2"}', '{"text":"","font":"font_animation:zone_expand/1"}', '{"text":"","font":"font_animation:zone_expand/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 100
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
			}
			
			folder fly_sword()
			{
				func move()
				{
					# tp 看起來會有延遲，讓他攻擊晚一點到
					execute at @s as @a[scores={player=1..},gamemode=adventure,distance=..1,tag=!sword_hit] unless score @s beamtime matches 0.. run function ARG(_PATH)hit
					tp @s ^ ^ ^-0.5
					execute at @s unless block ^ ^ ^-0.5 #skill:canpass run function ARG(_PATH)stop
				}
				
				func hit()
				{
					effect give @s minecraft:instant_damage 1 0 true
					tag @s add sword_hit
				}
				
				func stop()
				{
					schedule clear ARG(_PATH)stop
					tag @a remove sword_hit
					
					scoreboard players set #FLYING_SWORD number 0
					schedule function ARG(_PATH)aim 5t
				}
				
				func aim()
				{
					execute at 00000002-0000-0002-0000-000200000002 as @p[scores={player=1..},gamemode=adventure] run function ARG(_PATH)aim_effect
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)start 15t
					execute if score #DIFFICULTY number matches 2.. run schedule function ARG(_PATH)start 10t
				}
				
				func aim_effect()
				{
					execute facing entity @s eyes run tp 00000002-0000-0002-0000-000200000002 ~ ~ ~ ~180 0
					# 一次移動最多10格，有在範圍內再提示就好
					execute at @s[distance=..10] run function ARG(_PATH)summon_hint
					execute as 0000000f-0000-000f-0000-000f0000000f at @s facing entity 00000002-0000-0002-0000-000200000002 feet run tp @s ~ ~ ~ ~ 0
				}
				
				func summon_hint()
				{
					summon marker ~ ~ ~ {UUID:[I;21,21,21,21]}
					execute as 00000015-0000-0015-0000-001500000015 facing entity 00000002-0000-0002-0000-000200000002 feet run tp @s ~ ~ ~ ~ 0
					execute store result score #ROTATION_X number run data get entity 00000015-0000-0015-0000-001500000015 Rotation[0]
					kill 00000015-0000-0015-0000-001500000015
					```
					for i in range(-7, 8):
						print('execute if score #ROTATION_X number matches %d..%d run summon armor_stand ~ %.2f ~ {Marker:1b,Invisible:1b,Invulnerable:1b,NoGravity:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:17}}],Tags:["object","effect"],UUID:[I;15,15,15,15],Rotation:[%df,0f]}' %(i*24-11, i*24+12, GROUND_Y[0], i*24+60))
					```
				}
				
				func start()
				{
					kill 0000000f-0000-000f-0000-000f0000000f
					scoreboard players set #FLYING_SWORD number 1
					schedule function ARG(_PATH)stop 1s
				}
				
				# 暫時中斷飛劍使用
				func pause()
				{
					kill 0000000f-0000-000f-0000-000f0000000f
					scoreboard players set #FLYING_SWORD number 0
					
					schedule clear ARG(_PATH)start
					schedule clear ARG(_PATH)aim
					schedule clear ARG(_PATH)stop
				}
			}
			
			folder sword_dance()
			{
				func cast()
				{
					# 暫時關閉ZONE，延後快速回復
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players add #FAST_RECOVER number 15
					execute as @r[scores={player=1..},gamemode=adventure,limit=1,distance=5..] at @s run function ARG(_PATH)mark
					execute facing entity @a[tag=boss_target,limit=1] feet rotated ~ 0 run tp @s ~ ~ ~ ~ ~
					
					scoreboard players set #ANIM_LOCK number 1
					function ARG(_PATH)open_wing/start
				}
				
				func mark()
				{
					# 頭上標記特效
					execute positioned ~ ~1.5 ~ run function ARG(_PATH)mark_effect
					summon minecraft:armor_stand ~ ~ ~ {UUID:[I;3,3,3,3],Invisible:1,NoGravity:1,Invulnerable:1,Marker:1,Fire:100s,Tags:["object","effect","slash","skill_effect"],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:26}}]}
					scoreboard players set 00000003-0000-0003-0000-000300000003 number 32
					scoreboard players set 00000003-0000-0003-0000-000300000003 life -100000
					tag @s add boss_target
				}
				
				func mark_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:mark/1"}',CustomNameVisible:1,Duration:11,Tags:['{"text":"","font":"font_animation:mark/1"}', '{"text":"","font":"font_animation:mark/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 10
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				folder open_wing()
				{
					func start()
					{
						scoreboard players set #WING_OPEN number 1
						function stage:stage1/wing/fly
						tp @s ~ ~-0.45 ~
						# 計算右手姿勢
						execute store result score #R_ARM_X number run data get entity @s Pose.RightArm[0] 10
						execute store result score #R_ARM_Y number run data get entity @s Pose.RightArm[1] 10
						execute store result score #R_ARM_Z number run data get entity @s Pose.RightArm[2] 10
						scoreboard players set #R_ARM_DIFF_X number 2400
						scoreboard players set #R_ARM_DIFF_Y number 3500
						scoreboard players set #R_ARM_DIFF_Z number 800
						scoreboard players operation #R_ARM_DIFF_X number -= #R_ARM_X number
						scoreboard players operation #R_ARM_DIFF_Y number -= #R_ARM_Y number
						scoreboard players operation #R_ARM_DIFF_Z number -= #R_ARM_Z number
						scoreboard players operation #R_ARM_DIFF_X number /= #5 number
						scoreboard players operation #R_ARM_DIFF_Y number /= #5 number
						scoreboard players operation #R_ARM_DIFF_Z number /= #5 number
						# 計算左手姿勢 (-5->70, 0->30, --)
						scoreboard players set #L_ARM_X number -5
						scoreboard players set #L_ARM_Y number 0
						scoreboard players set #L_ARM_DIFF_X number 15
						scoreboard players set #L_ARM_DIFF_Y number 6
						
						function ARG(_PATH)anim
					}
					
					func anim()
					{
						execute if score #WING_OPEN number matches 1..5 run function ARG(_PATH)calc_pose
						execute if score #WING_OPEN number matches 2 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 6
						execute if score #WING_OPEN number matches 4 run data modify entity @s ArmorItems[3].tag.CustomModelData set value 7
						execute if score #WING_OPEN number matches 10 run function ARG(_PATH)end
						
						scoreboard players add #WING_OPEN number 1
					}
					
					func calc_pose()
					{
						# 右手
						execute store result entity @s Pose.RightArm[0] float 0.1 run scoreboard players operation #R_ARM_X number += #R_ARM_DIFF_X number
						execute store result entity @s Pose.RightArm[1] float 0.1 run scoreboard players operation #R_ARM_Y number += #R_ARM_DIFF_Y number
						execute store result entity @s Pose.RightArm[2] float 0.1 run scoreboard players operation #R_ARM_Z number += #R_ARM_DIFF_Z number
						# 左手
						execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
						execute store result entity @s Pose.LeftArm[1] float 1 run scoreboard players operation #L_ARM_Y number += #L_ARM_DIFF_Y number
					}
					
					func end()
					{
						particle minecraft:explosion ~ ~ ~ 0 0 0 3 5
						playsound minecraft:entity.wither.shoot player @a ~ ~ ~ 3 1.3 0
						
						data modify entity @s ArmorItems[3].tag.CustomModelData set value 8
						data modify entity @s NoGravity set value 1
						scoreboard players set #SWORD_DANCE number 1
					}
				}
				
				func trace()
				{
					execute if score #DIFFICULTY number matches 1 facing entity @a[tag=boss_target,limit=1] feet run tp @s ^ ^ ^0.55 ~ ~
					execute if score #DIFFICULTY number matches 2..3 facing entity @a[tag=boss_target,limit=1] feet run tp @s ^ ^ ^0.7 ~ ~
					execute if score #DIFFICULTY number matches 4 facing entity @a[tag=boss_target,limit=1] feet run tp @s ^ ^ ^0.8 ~ ~
					
					# 約 8 tick 的反應時間
					execute if score #SWORD_DANCE number matches 2 at @s if entity @a[tag=boss_target,distance=..1,limit=1] run function ARG(_PATH)attack
					execute if score #SWORD_DANCE number matches 1 at @s if entity @a[tag=boss_target,distance=..7,limit=1] run function ARG(_PATH)prepare
					
					# 追蹤過程持續延後快速回復
					scoreboard players add #FAST_RECOVER number 1
				}
				
				func prepare()
				{
					# 反擊ICON
					title @a[tag=boss_target] times 0 10000 0
					title @a[tag=boss_target] title {"text":"1","font":"ui:main"}
					# 切換攻擊預備動作模型
					data modify entity @s Pose.RightArm set value [260f,350f,100f]
					particle flash ~ ~1 ~ 0 0 0 1 1
					
					scoreboard players set #SWORD_DANCE number 2
				}
				
				func attack()
				{
					execute as @a[tag=boss_target,limit=1] unless score @s beamtime matches 0.. run function ARG(_PATH)damage
					# 切換揮刀模型
					function ARG(_PATH)sword_wave/start
					
					scoreboard players set 00000003-0000-0003-0000-000300000003 life 0
					execute as 00000003-0000-0003-0000-000300000003 store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get @s number
					playsound minecraft:enemy.sword.dance_attack player @a ~ ~ ~ 3 1 0
					
					function ARG(_PATH)end
				}
				
				func damage()
				{
					# 讓 BOSS 穿過玩家 1.5 格
					execute facing entity @s feet positioned as @s run function ARG(_PATH)set_boss_motion
					effect give @s minecraft:instant_damage 1 2 true
					execute at @s run particle flash ~ ~1 ~ 0 0 0 1 1
				}
				
				func set_boss_motion()
				{
					# 設定穿過去後的動能
					execute positioned 0 0 0 run summon marker ^ ^ ^1.5 {UUID:[I;0,3,0,3]}
					data modify entity 00000001-0000-0001-0000-000100000001 Motion set from entity 00000000-0000-0003-0000-000000000003 Pos
					kill 00000000-0000-0003-0000-000000000003
					
					execute if block ^ ^ ^1.5 #skill:canpass run tp 00000001-0000-0001-0000-000100000001 ^ ^ ^1.5 ~ ~
					execute unless block ^ ^ ^1.5 #skill:canpass if block ^ ^ ^1 #skill:canpass run tp 00000001-0000-0001-0000-000100000001 ^ ^ ^1 ~ ~
					execute unless block ^ ^ ^1.5 #skill:canpass unless block ^ ^ ^1 #skill:canpass if block ^ ^ ^0.5 #skill:canpass run tp 00000001-0000-0001-0000-000100000001 ^ ^ ^0.5 ~ ~
				}
				
				func counter()
				{
					# 切換揮刀模型
					execute at @a[tag=boss_target,limit=1] facing entity @s feet rotated ~ 0 run function ARG(_PATH)set_counter_effect
					function ARG(_PATH)sword_wave/start
					schedule function stage:stage1/skills/loop 11t
					
					scoreboard players set 00000003-0000-0003-0000-000300000003 life 0
					scoreboard players set 00000003-0000-0003-0000-000300000003 number 23
					execute as 00000003-0000-0003-0000-000300000003 store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get @s number
				}
				
				folder sword_wave()
				{
					func start()
					{
						data modify entity @s ArmorItems[3].tag.CustomModelData set value 7
						data modify entity @s HandItems[0].tag.CustomModelData set value 15
						data modify entity @s Pose.RightArm set value [30f,350f,100f]
					
						title @a[tag=boss_target] title ""
						tag @a remove boss_target
						
						schedule function ARG(_PATH)anim1 2t
					}
					
					func anim1()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 16
						schedule function ARG(_PATH)anim2 2t
					}
					
					func anim2()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 14
						schedule function ARG(__PATH)end 1t
					}
				}
				
				func set_counter_effect()
				{
					tp @s ^ ^ ^1 ~180 ~
					execute positioned ^ ^1 ^0.5 run function ARG(_PATH)counter_effect
					execute positioned ^ ^1 ^0.5 run playsound minecraft:enemy.sword.clash player @a ~ ~ ~ 3 1 0
				}
				
				func counter_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:sword_clash/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:sword_clash/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 4
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					
					data modify entity 00000001-0000-0001-0000-000100000001 NoGravity set value 0b
					```
					print(f"execute as 00000001-0000-0001-0000-000100000001 at @s positioned ~ 0.0 ~ if entity @s[dx=0,dy={GROUND_Y[0] - 1},dz=0] run tp @s ~ {GROUND_Y[0]} ~")
					```
					scoreboard players set #REVERT_ACTION number 1
					schedule function ARG(__PATH)revert_action 1s
					schedule clear ARG(__PATH)zone/auto_reset_pose
					scoreboard players set #SWORD_DANCE number 0
					scoreboard players set #BACKSTEP number 1
					scoreboard players set #ANIM_LOCK number 0
				}
				
				func shutdown()
				{
					schedule clear ARG(_PATH)sword_wave/anim1
					schedule clear ARG(_PATH)sword_wave/anim2
					schedule clear ARG(_PATH)end
				}
			}
			
			folder slash()
			{
				func cast()
				{
					scoreboard players set #RAND_RESULT number -1
					# 暫時關閉ZONE
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ANIM_LOCK number 1
					scoreboard players set #WING_OPEN number 11
					# 準備姿勢
					data modify entity @s HandItems[0].tag.CustomModelData set value 3
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 10
					data modify entity @s Pose.RightArm set value [300f,330f,346f]
					data modify entity @s Pose.LeftArm set value [45f,205f,15f]
					data modify entity @s NoGravity set value 1
					# 腳邊旋風 & 身邊閃電特效
					playsound minecraft:enemy.sword.slash_wind player @a ~ ~ ~ 3 1 0
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,UUID:[I;11,11,11,11],ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:13}}],Tags:["object","effect"]}
					
					# 計算右手姿勢 (300->260, --, 346->367)
					scoreboard players set #R_ARM_X number 3000
					scoreboard players set #R_ARM_DIFF_X number -80
					scoreboard players set #R_ARM_Z number 3460
					scoreboard players set #R_ARM_DIFF_Z number 42
					# 計算左手姿勢 (45->70, --, --)
					scoreboard players set #L_ARM_X number 45
					scoreboard players set #L_ARM_DIFF_X number 5
					
					execute as @r[scores={player=1..},gamemode=adventure] at @s run function ARG(_PATH)mark
					function ARG(_PATH)rand_slash_time
					# 過程中不要快速回復
					scoreboard players operation #FAST_RECOVER number += #SLASH_COUNTDOWN number
					
					execute if score #DIFFICULTY number matches 1..2 run scoreboard players set #SLASH_HINT_TIME number 18
					execute if score #DIFFICULTY number matches 3 run scoreboard players set #SLASH_HINT_TIME number 16
					execute if score #DIFFICULTY number matches 4 run scoreboard players set #SLASH_HINT_TIME number 14
				}
				
				func rand_slash_time() from random().generate(40, 50, "#SLASH_COUNTDOWN number");
				
				func mark()
				{
					tag @s add boss_target
					```
					print('summon armor_stand ~ %.1f ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,UUID:[I;10,10,10,10],Tags:["object","effect"],Passengers:[{id:"minecraft:item",Item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:11,CustomPotionColor:-1}},PickupDelay:32767,HasVisualFire:1b,UUID:[I;17,17,17,17],Tags:["object","effect"]}]}' %GROUND_Y[0])
					```
				}
				
				func slash_countdown()
				{
					execute facing entity @a[tag=boss_target,limit=1] feet run function stage:stage1/face_player
					
					execute if score #SLASH_COUNTDOWN number matches 2..6 run function ARG(_PATH)calc_pose
					execute if score #SLASH_COUNTDOWN number = #SLASH_HINT_TIME number run function ARG(_PATH)set_hint
					```
					print(f"execute if score #SLASH_COUNTDOWN number > #SLASH_HINT_TIME number at @a[tag=boss_target,limit=1] positioned ~ {GROUND_Y[0]} ~ run tp 0000000a-0000-000a-0000-000a0000000a ~ ~ ~ ~ 0")
					```
					
					scoreboard players remove #SLASH_COUNTDOWN number 1
					execute if score #SLASH_COUNTDOWN number matches 0 run function ARG(_PATH)end
				}
				
				func set_hint()
				{
					data modify entity 00000011-0000-0011-0000-001100000011 Item.tag.CustomPotionColor set value 16711680
					playsound minecraft:enemy.sword.slash_hint player @a ~ ~ ~ 3 1 0
				}
				
				func calc_pose()
				{
					# 右手
					execute store result entity @s Pose.RightArm[0] float 0.1 run scoreboard players operation #R_ARM_X number += #R_ARM_DIFF_X number
					execute store result entity @s Pose.RightArm[2] float 0.1 run scoreboard players operation #R_ARM_Z number += #R_ARM_DIFF_Z number
					# 左手
					execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					scoreboard players set #ANIM_LOCK number 0
					scoreboard players set #REVERT_ACTION number 1
					schedule function ARG(__PATH)revert_action 1s
					schedule clear ARG(__PATH)zone/auto_reset_pose
					
					# 更換劈砍姿勢模型
					data modify entity @s HandItems[0].tag.CustomModelData set value 14
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 12
					data modify entity @s Pose.RightArm set value [350f,350f,320f]
					data modify entity @s Pose.LeftArm set value [350f,200f,30f]
					
					particle minecraft:explosion ~ ~ ~ 0 0 0 3 5
					kill 0000000b-0000-000b-0000-000b0000000b
					execute as 0000000a-0000-000a-0000-000a0000000a at @s run function ARG(_PATH)slash
					execute facing entity 0000000a-0000-000a-0000-000a0000000a feet rotated ~ 0 positioned as 0000000a-0000-000a-0000-000a0000000a run tp @s ~ ~ ~ ~ ~
					execute at @s run function ARG(_PATH)move/run
					
					kill 0000000a-0000-000a-0000-000a0000000a
					kill 00000011-0000-0011-0000-001100000011
					tag @a remove boss_target
				}
			
				folder move() from for_loop().for(8, "if block ^ ^ ^0.5 #skill:canpass positioned ^ ^ ^0.5")
				{
					func execute()
					{
						tp @s ~ ~ ~
					}
				}
				
				func slash()
				{
					playsound minecraft:enemy.sword.slash player @a ~ ~ ~ 3 1 0
					execute positioned ~ ~1 ~ run function ARG(_PATH)slash_effect
					execute as @a[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. if predicate stage:slash_range run function ARG(_PATH)damage
					
					schedule function ARG(_PATH)activate_shader/run 3t
				}
				
				func slash_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:slash/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:slash/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 4
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func damage()
				{
					function skill:reset_action
					# scoreboard players reset @s player
					# clear @s
					
					team join blue @s
					effect give @s minecraft:glowing 1000000 0 true
					tag @s add already_dead
				}
				
				folder activate_shader()
				{
					func run()
					{
						execute as @a[tag=already_dead] run function ARG(_PATH)set
						schedule function ARG(_PATH)reset 1s
					}
					
					func set()
					{
						effect give @s minecraft:night_vision 1000000 0 true
						team leave @s
						# tp @s 小黑屋
					}
					
					func reset()
					{
						execute as @a[tag=already_dead] run function ARG(_PATH)player_reset
					}
					
					func player_reset()
					{
						effect clear @s minecraft:night_vision
						effect clear @s minecraft:glowing
						effect give @s instant_damage 1 2 true
						tag @s remove already_dead
					}
				}
			}
			
			folder flaw()
			{
				func cast()
				{
					# 暫時關閉ZONE
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ANIM_LOCK number 1
					
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.RightArm set value [240f,350f,80f]
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.LeftArm set value [70f,30f,0f]
					data modify entity 00000001-0000-0001-0000-000100000001 ArmorItems[3].tag.CustomModelData set value 7
					data modify entity 00000001-0000-0001-0000-000100000001 NoGravity set value 1
					```
					print('execute at @r[scores={player=1..},gamemode=adventure,distance=..5,limit=1] positioned ~ %.2f ~ run function ARG(_PATH)positioned_effect' %GROUND_Y[0])
					```
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)attack 17t
					execute if score #DIFFICULTY number matches 2..3 run schedule function ARG(_PATH)attack 15t
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)attack 13t
				}
				
				func positioned_effect()
				{
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,UUID:[I;12,12,12,12],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:35}}],Tags:["object","effect","slash","skill_effect"]}
					scoreboard players set 0000000c-0000-000c-0000-000c0000000c number 36
					scoreboard players set 0000000c-0000-000c-0000-000c0000000c life -100000
					
					execute rotated ~ 0 run function ARG(_PATH)tp_back
					tp 0000000c-0000-000c-0000-000c0000000c ~ ~ ~ ~180 0
					# 嘗試強迫刷新 tp 顯示
					schedule function ARG(_PATH)update_display_rotation 1t
					schedule function ARG(_PATH)update_display_rotation 2t append
					schedule function ARG(_PATH)update_display_rotation 3t append
				}
				
				func tp_back()
				{
					execute if block ^ ^ ^-3 #skill:canpass run tp @s ^ ^ ^-3 ~ 0
					execute unless block ^ ^ ^-3 #skill:canpass if block ^ ^ ^-2 #skill:canpass run tp @s ^ ^ ^-2 ~ 0
					execute unless block ^ ^ ^-3 #skill:canpass unless block ^ ^ ^-2 #skill:canpass if block ^ ^ ^-1 #skill:canpass run tp @s ^ ^ ^-1 ~ 0
					execute unless block ^ ^ ^-3 #skill:canpass unless block ^ ^ ^-2 #skill:canpass unless block ^ ^ ^-1 #skill:canpass run tp @s ^ ^ ^-0.5 ~ 0
				}
				
				func update_display_rotation()
				{
					execute as 00000001-0000-0001-0000-000100000001 at @s run tp @s ~ ~ ~ ~1 ~
				}
				
				func attack()
				{
					scoreboard players set 0000000c-0000-000c-0000-000c0000000c life 0
					execute as 0000000c-0000-000c-0000-000c0000000c store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get @s number
					
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)attack_effect
					execute as 00000001-0000-0001-0000-000100000001 at 0000000c-0000-000c-0000-000c0000000c facing entity @s feet rotated ~ 0 run function ARG(_PATH)attack_move
					execute as @a[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. at 0000000c-0000-000c-0000-000c0000000c if predicate stage:flaw_range run effect give @s instant_damage 1 0 true
				}
				
				func attack_move()
				{
					execute if block ^ ^ ^0.8 #skill:canpass run tp @s ^ ^ ^0.8 ~180 ~
					execute unless block ^ ^ ^0.8 #skill:canpass if block ^ ^ ^0.4 #skill:canpass run tp @s ^ ^ ^0.4 ~180 ~
					execute unless block ^ ^ ^0.8 #skill:canpass unless block ^ ^ ^0.4 #skill:canpass run tp @s ~ ~ ~ ~180 ~
				}
				
				func attack_effect()
				{
					function ARG(_PATH)sword_wave/start
					playsound minecraft:skill.rush_attack player @a ~ ~ ~ 3 1 0
				}
				
				folder sword_wave()
				{
					func start()
					{
						data modify entity @s ArmorItems[3].tag.CustomModelData set value 7
						data modify entity @s HandItems[0].tag.CustomModelData set value 15
						data modify entity @s Pose.RightArm set value [30f,350f,100f]
						
						schedule function ARG(_PATH)anim1 2t
					}
					
					func anim1()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 16
						schedule function ARG(_PATH)anim2 2t
					}
					
					func anim2()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 14
						schedule function ARG(__PATH)end 1t
					}
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					scoreboard players set #REVERT_ACTION number 1
					scoreboard players set #BACKSTEP number 1
					schedule function ARG(__PATH)revert_action 1s
					schedule clear ARG(__PATH)zone/auto_reset_pose
					scoreboard players set #ANIM_LOCK number 0
				}
				
				func shutdown()
				{
					schedule clear ARG(_PATH)attack
					schedule clear ARG(_PATH)update_display_rotation
					schedule clear ARG(_PATH)sword_wave/anim1
					schedule clear ARG(_PATH)sword_wave/anim2
					schedule clear ARG(_PATH)end
				}
			}
			
			folder chop()
			{
				func cast()
				{
					# 劈砍有機率接破綻
					execute if score #RAND_RESULT number matches 50..74 run function ARG(_PATH)follow_skill
					
					# 暫時關閉ZONE
					scoreboard players set #ZONE_ACTIVE number 0
					scoreboard players set #ANIM_LOCK number 1
					# 準備姿勢
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 10
					data modify entity @s HandItems[0].tag.CustomModelData set value 3
					data modify entity @s Pose.RightArm set value [300f,330f,346f]
					data modify entity @s Pose.LeftArm set value [45f,205f,15f]
					data modify entity @s NoGravity set value 1
					# 計算右手姿勢 (300->260, --, 346->367)
					scoreboard players set #R_ARM_X number 3000
					scoreboard players set #R_ARM_DIFF_X number -80
					scoreboard players set #R_ARM_Z number 3460
					scoreboard players set #R_ARM_DIFF_Z number 42
					# 計算左手姿勢 (45->70, --, --)
					scoreboard players set #L_ARM_X number 45
					scoreboard players set #L_ARM_DIFF_X number 5
					
					execute if score #DIFFICULTY number matches 1 run scoreboard players set #CHOP_COUNTDOWN number 16
					execute if score #DIFFICULTY number matches 2..3 run scoreboard players set #CHOP_COUNTDOWN number 14
					execute if score #DIFFICULTY number matches 4 run scoreboard players set #CHOP_COUNTDOWN number 12
					
					# 攻擊半徑只有三格，所以三格內沒玩家就要往前一點再攻擊(近距離最遠5格)
					execute unless entity @a[scores={player=1..},gamemode=adventure,distance=..3,limit=1] facing entity @p[scores={player=1..},gamemode=adventure] feet rotated ~ 0 positioned ^ ^ ^2 run function ARG(_PATH)set_target
					execute unless entity 0000000d-0000-000d-0000-000d0000000d facing entity @p[scores={player=1..},gamemode=adventure] feet rotated ~ 0 run function ARG(_PATH)set_target
				}
				
				func set_target()
				{
					execute positioned as @s run tp @s ~ ~ ~ ~ ~
					summon armor_stand ^ ^ ^1.5 {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,UUID:[I;13,13,13,13],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:39}}],Tags:["object","effect"]}
				}
				
				func follow_skill()
				{
					execute if score #DIFFICULTY number matches 1 run schedule function ARG(_PATH)cast_follow_skill 22t
					execute if score #DIFFICULTY number matches 2..3 run schedule function ARG(_PATH)cast_follow_skill 20t
					execute if score #DIFFICULTY number matches 4 run schedule function ARG(_PATH)cast_follow_skill 18t
					
					schedule function ARG(__PATH)loop 120t
					schedule clear ARG(__PATH)revert_action
					scoreboard players set #REVERT_ACTION number 0
				}
				
				func cast_follow_skill()
				{
					execute as 00000001-0000-0001-0000-000100000001 at @s if entity @a[scores={player=1..},gamemode=adventure,distance=..5,limit=1] run function ARG(__PATH)flaw/cast
				}
				
				func chop_countdown()
				{
					execute if score #CHOP_COUNTDOWN number matches 2..6 run function ARG(_PATH)calc_pose
					
					scoreboard players remove #CHOP_COUNTDOWN number 1
					execute if score #CHOP_COUNTDOWN number matches 0 run function ARG(_PATH)attack
				}
				
				func calc_pose()
				{
					# 右手
					execute store result entity @s Pose.RightArm[0] float 0.1 run scoreboard players operation #R_ARM_X number += #R_ARM_DIFF_X number
					execute store result entity @s Pose.RightArm[2] float 0.1 run scoreboard players operation #R_ARM_Z number += #R_ARM_DIFF_Z number
					# 左手
					execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
				}
				
				func attack()
				{
					execute at 0000000d-0000-000d-0000-000d0000000d facing entity @s feet rotated ~180 0 run tp @s ^ ^-0.15 ^-1
					# 更換劈砍姿勢模型
					function ARG(_PATH)sword_wave/start
					playsound minecraft:skill.basic2 player @a ~ ~ ~ 3 1 0
					
					execute at 0000000d-0000-000d-0000-000d0000000d as @a[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. if predicate stage:chop_range run effect give @s instant_damage 1 1 true
					kill 0000000d-0000-000d-0000-000d0000000d
				}
				
				folder sword_wave()
				{
					func start()
					{
						data modify entity @s ArmorItems[3].tag.CustomModelData set value 18
						data modify entity @s HandItems[0].tag.CustomModelData set value 15
						data modify entity @s Pose.RightArm set value [330f,350f,310f]
						data modify entity @s Pose.LeftArm set value [100f,220f,300f]
						
						schedule function ARG(_PATH)anim1 2t
					}
					
					func anim1()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 16
						schedule function ARG(_PATH)anim2 2t
					}
					
					func anim2()
					{
						data modify entity 00000001-0000-0001-0000-000100000001 HandItems[0].tag.CustomModelData set value 14
						schedule function ARG(__PATH)end 1t
					}
				}
				
				func end()
				{
					# 繼續啟用ZONE的效果
					scoreboard players set #ZONE_ACTIVE number 1
					scoreboard players set #ANIM_LOCK number 0
					scoreboard players set #BACKSTEP number 1
					scoreboard players set #REVERT_ACTION number 1
					schedule function ARG(__PATH)revert_action 1s
					schedule clear ARG(__PATH)zone/auto_reset_pose
				}
				
				func shutdown()
				{
					schedule clear ARG(_PATH)cast_follow_skill
					schedule clear ARG(_PATH)sword_wave/anim1
					schedule clear ARG(_PATH)sword_wave/anim2
					schedule clear ARG(_PATH)end
				}
			}
			
			folder sanction()
			{
				func cast()
				{
					function ARG(_PATH)flip
					```
					print(f"execute if score #SANCTION_FLIP number matches 0 as @r[scores={{player=1..}}] at @s positioned ~ {GROUND_Y[0] + 0.7} ~ run function ARG(_PATH)surround")
					print(f"execute if score #SANCTION_FLIP number matches 1 as @r[scores={{player=1..}}] at @s positioned ~ {GROUND_Y[0] - 0.7} ~ run function ARG(_PATH)surround")
					```
					execute if score #DIFFICULTY number matches 1 run function ARG(_PATH)schedule_sting/easy
					execute if score #DIFFICULTY number matches 2..3 run function ARG(_PATH)schedule_sting/normal
					execute if score #DIFFICULTY number matches 4 run function ARG(_PATH)schedule_sting/hell
					
					function ARG(_PATH)hand_anim/raise/start
				}
				
				folder schedule_sting()
				{
					func easy()
					{
						execute if score #SANCTION_FLIP number matches 0 run schedule function ARG(__PATH)sting 25t
						execute if score #SANCTION_FLIP number matches 1 run schedule function ARG(__PATH)sting 23t
					}
					
					func normal()
					{
						execute if score #SANCTION_FLIP number matches 0 run schedule function ARG(__PATH)sting 20t
						execute if score #SANCTION_FLIP number matches 1 run schedule function ARG(__PATH)sting 18t
					}
					
					func hell()
					{
						execute if score #SANCTION_FLIP number matches 0 run schedule function ARG(__PATH)sting 19t
						execute if score #SANCTION_FLIP number matches 1 run schedule function ARG(__PATH)sting 17t
					}
				}
				
				# 原則上左手平常都是擺著(放技能才會動，但是技能之間不重疊)
				folder hand_anim()
				{
					func run()
					{
						scoreboard players add #SANCTION_ANIM number 1
						
						execute if score #SANCTION_ANIM number matches 1..5 run function ARG(_PATH)raise/loop
						execute if score #SANCTION_ANIM number matches 11..12 run function ARG(_PATH)swing/loop
					}
					
					folder raise()
					{
						func start()
						{
							# 5 tick (355->265, -, 350->360)
							scoreboard players set #SANCTION_ANIM number 0
							
							scoreboard players set #L_ARM_X number 355
							scoreboard players set #L_ARM_Z number 350
							
							scoreboard players set #L_ARM_DIFF_X number -18
							scoreboard players set #L_ARM_DIFF_Z number 2
						}
						
						func loop()
						{
							execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
							execute store result entity @s Pose.LeftArm[2] float 1 run scoreboard players operation #L_ARM_Z number += #L_ARM_DIFF_Z number
							
							execute if score #SANCTION_ANIM number matches 5 run scoreboard players reset #SANCTION_ANIM number
						}
					}
					
					folder swing()
					{
						func start()
						{
							# 2 tick (265->275, 360->300, -)
							scoreboard players set #SANCTION_ANIM number 10
							
							scoreboard players set #L_ARM_X number 265
							scoreboard players set #L_ARM_Y number 360
							
							scoreboard players set #L_ARM_DIFF_X number 5
							scoreboard players set #L_ARM_DIFF_Y number -30
						}
						
						func loop()
						{
							execute store result entity @s Pose.LeftArm[0] float 1 run scoreboard players operation #L_ARM_X number += #L_ARM_DIFF_X number
							execute store result entity @s Pose.LeftArm[1] float 1 run scoreboard players operation #L_ARM_Y number += #L_ARM_DIFF_Y number
							
							execute if score #SANCTION_ANIM number matches 12 run scoreboard players reset #SANCTION_ANIM number
						}
					}
				}
				
				func flip() from random().generate(0, 1, "#SANCTION_FLIP number");
				
				func surround()
				{
					tag @s add boss_target
					
					playsound minecraft:enemy.sword.sanction_prepare player @a ~ ~ ~ 3 1 0
					scoreboard players set #WING_OPEN number -1
					summon marker ~ ~0.7 ~ {UUID:[I;14,14,14,14],Tags:["object"]}
					
					execute as 0000000e-0000-000e-0000-000e0000000e at @s run function ARG(_PATH)set_sword
				}
				
				func set_sword()
				{
					execute rotated ~30 0 run tp 00000004-0000-0004-0000-000400000004 ^ ^ ^4 ~ ~
					execute rotated ~90 0 run tp 00000005-0000-0005-0000-000500000005 ^ ^ ^4 ~ ~
					execute rotated ~150 0 run tp 00000006-0000-0006-0000-000600000006 ^ ^ ^4 ~ ~
					execute rotated ~210 0 run tp 00000007-0000-0007-0000-000700000007 ^ ^ ^4 ~ ~
					execute rotated ~270 0 run tp 00000008-0000-0008-0000-000800000008 ^ ^ ^4 ~ ~
					execute rotated ~330 0 run tp 00000009-0000-0009-0000-000900000009 ^ ^ ^4 ~ ~
					tp @s ~ ~ ~ ~4 ~
				}
				
				func follow()
				{
					execute as @a[tag=boss_target,limit=1] run function ARG(_PATH)horizonal_move
					execute at @s run function ARG(_PATH)set_sword
				}
				
				func horizonal_move()
				{
					data modify entity 0000000e-0000-000e-0000-000e0000000e Pos[0] set from entity @s Pos[0]
					data modify entity 0000000e-0000-000e-0000-000e0000000e Pos[2] set from entity @s Pos[2]
				}
				
				func sting()
				{
					execute as @e[type=husk,tag=wing] at @s run tp @s ^ ^ ^-8
					execute as 0000000e-0000-000e-0000-000e0000000e at @s run function ARG(_PATH)effect
					scoreboard players set #WING_OPEN number -2
					tag @a remove boss_target
					
					# 制裁有機率連放下一招
					function random:rand_rate
					execute if score #RAND_RESULT number matches ..49 run schedule function ARG(__PATH)loop 21t
					
					function ARG(_PATH)hand_anim/swing/start
					schedule function ARG(_PATH)reset_wing 1s
				}
				
				func effect()
				{
					playsound minecraft:enemy.sword.sanction player @a ~ ~ ~ 3 1 0
					execute as @a[scores={player=1..},gamemode=adventure] unless score @s beamtime matches 0.. if predicate stage:sanction_range positioned ~-10 ~0.2 ~-10 if entity @s[dx=20,dy=0,dz=20] positioned ~ ~-0.8 ~ if entity @s[dx=20,dy=0,dz=20] run effect give @s instant_damage 1 1 true
					kill @s
				}
				
				func reset_wing()
				{
					scoreboard players set #WING_OPEN number 0
					# 順便重置左手位置
					data modify entity 00000001-0000-0001-0000-000100000001 Pose.LeftArm set value [355f,0f,350f]
				}
				
				func shutdown()
				{
					kill 0000000e-0000-000e-0000-000e0000000e
					tag @a remove boss_target
					
					scoreboard players reset #SANCTION_ANIM number
					scoreboard players set #WING_OPEN number 0
					schedule clear ARG(_PATH)sting
					schedule clear ARG(_PATH)reset_wing
				}
			}
		}
	}
	
	# 呪縛巫女
	folder stage2()
	{
		func start()
		{
			bossbar set minecraft:boss max 50000
			execute store result bossbar minecraft:boss value run bossbar get minecraft:boss max
			bossbar set minecraft:boss name {"translate":"Cursed Miko","color":"white","bold":true}
			```
			print(f"tp @a {STAGE_CENTER[1][0]} {GROUND_Y[1]} {STAGE_CENTER[1][1]} 180 50")
			print(f"execute positioned {STAGE_CENTER[1][0]} {GROUND_Y[1]} {STAGE_CENTER[1][1] - 8} run function ARG(_PATH)summon_boss")
			```
			schedule function ARG(_PATH)skills/loop 7s
			schedule function ARG(_PATH)skills/dust/summon_loop 10s
			schedule function ARG(_PATH)skills/dust/shoot 11s
			
			# 可攻擊的子彈編號從 10 開始，前面保留給骷髏跟分身
			scoreboard players set #BULLET_ID number 10
		}
		
		func summon_boss()
		{
			function miko:summon/default
			tag 00000001-0000-0001-0000-000100000001 add boss
			tag 00000001-0000-0001-0000-000100000001 remove new_miko
			tag 00000001-0000-0001-0000-000100000001 remove effect
			
			scoreboard players set 00000001-0000-0001-0000-000100000001 number 1
			execute store result score 00000001-0000-0001-0000-000100000001 health run bossbar get minecraft:boss max
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
			schedule clear ARG(_PATH)skills/dust/summon_loop
			schedule clear ARG(_PATH)skills/dust/shoot
			schedule clear ARG(_PATH)skills/dust/real_summon
			schedule clear ARG(_PATH)skills/flame/circle/summon
			schedule clear ARG(_PATH)skills/flame/bullet/shoot
			schedule clear ARG(_PATH)skills/thunder/strike
			schedule clear ARG(_PATH)skills/sacrifice/clear_levitation
			schedule clear ARG(_PATH)skills/sanka/sakura_draw
			schedule clear ARG(_PATH)skills/sanka/call_teleport
			schedule clear ARG(_PATH)skills/sikigami/reset_cd
			
			scoreboard players reset #SIKIGAMI_CD number
			scoreboard players reset #FLAME_BULLET number
			scoreboard players reset #ANIM_LOCK number
			scoreboard players reset #FACING_TIMER number
			
			function miko:remove/all
		}
		
		func main()
		{
			execute as @e[type=armor_stand] run function ARG(_PATH)armor_stand_main
			execute as 00000004-0000-0004-0000-000400000004 at @s run function ARG(_PATH)skills/sacrifice/check
			execute as 00000007-0000-0007-0000-000700000007 at @s run function ARG(_PATH)skills/hell/check
		}
		
		func armor_stand_main()
		{
			execute if entity @s[tag=miko] at @s facing entity @p[scores={player=1..},gamemode=adventure] feet run function ARG(_PATH)face_player
			execute at @s[tag=dust_bullet] run function ARG(_PATH)skills/dust/bullet/move
			execute if entity @s[tag=flame_circle] run function ARG(_PATH)skills/flame/circle/spin
			execute at @s[tag=flame_bullet] run function ARG(_PATH)skills/flame/bullet/move
			execute at @s[tag=sanka_bullet] run function ARG(_PATH)skills/sanka/bullet/move
		}
		
		func face_player()
		{
			scoreboard players add #FACING_TIMER number 1
			tp @s ~ ~ ~ ~ 0
			execute if score #FACING_TIMER number matches 2 run function ARG(_PATH)facing_anim
		}
		
		func facing_anim()
		{
			execute unless score #ANIM_LOCK number matches 1.. run function miko:move
			execute if score #ANIM_LOCK number matches 2 run function miko:animations/animation.miko.flame_loop/play
			scoreboard players reset #FACING_TIMER number
		}
		
		folder skills()
		{
			func loop()
			{
				schedule function ARG(_PATH)loop 5s
				execute if score #EXIST_CLONE number matches 1.. as @e[type=armor_stand,tag=miko] at @s run function ARG(_PATH)flame/cast
				execute unless score #EXIST_CLONE number matches 1.. as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)check_sikigami
			}
			
			func check_sikigami()
			{
				scoreboard players set #SIKIGAMI_USED number 0
				execute unless score #SIKIGAMI_CD number matches 1 if score @s health matches ..15000 run function ARG(_PATH)sikigami/cast
				execute unless score #SIKIGAMI_USED number matches 1 run function ARG(_PATH)rand_skill
			}
			
			func rand_skill()
			{
				function random:rand_rate
				
				scoreboard players set #NEAR_SKILL number 0
				execute if entity @a[scores={player=1..},gamemode=adventure,distance=..5,limit=1] run scoreboard players add #NEAR_SKILL number 1
				execute if entity @a[scores={player=1..},gamemode=adventure,distance=5.01..,limit=1] run scoreboard players add #NEAR_SKILL number 2
				
				execute if score #NEAR_SKILL number matches 1 run function ARG(_PATH)near_judge
				execute if score #NEAR_SKILL number matches 2 run function ARG(_PATH)far_judge
				execute unless score #NEAR_SKILL number matches 1..2 run function ARG(_PATH)rand_range
			}
			
			func rand_range()
			{
				execute if score #RAND_RESULT number matches ..49 run function ARG(_PATH)near_judge
				execute if score #RAND_RESULT number matches 50.. run function ARG(_PATH)far_judge
				# 重骰一次避免隨機距離跟放的招相關
				function random:rand_rate
			}
			
			func near_judge()
			{
				execute if score @s health matches ..35000 if score #RAND_RESULT number matches 40..59 unless entity 00000007-0000-0007-0000-000700000007 run function ARG(_PATH)hell/cast
				
				execute if score #RAND_RESULT number matches 0..49 run function ARG(_PATH)sacrifice/cast
				execute if score #RAND_RESULT number matches 50..99 run function ARG(_PATH)sanka/cast
			}
			
			func far_judge()
			{
				execute if score @s health matches ..35000 if score #RAND_RESULT number matches 40..59 unless entity 00000007-0000-0007-0000-000700000007 run function ARG(_PATH)hell/cast
				
				execute if score #RAND_RESULT number matches 0..49 run function ARG(_PATH)flame/cast
				execute if score #RAND_RESULT number matches 50..99 run function ARG(_PATH)thunder/cast
			}
			
			folder dust()
			{
				func summon_loop()
				{
					```
					print(f"execute at @r positioned ~ {GROUND_Y[1] + 0.55} ~ run function ARG(_PATH)summon")
					```
					schedule function ARG(_PATH)summon_loop 10s
				}
				
				func summon()
				{
					# 向上飄出的生成特效
					summon armor_stand ~ ~-3 ~ {DisabledSlots:4144959,Invulnerable:1b,Invisible:1b,NoGravity:1b,CustomNameVisible:1b,CustomName:'{"text":"1","font":"skill:main"}',Tags:["object","effect"],UUID:[I;2,2,2,2]}
					tp 00000002-0000-0002-0000-000200000002 ~ ~-1.5 ~
					schedule function ARG(_PATH)real_summon 1s
				}
				
				func real_summon()
				{
					execute at 00000002-0000-0002-0000-000200000002 run summon area_effect_cloud ~ ~1.5 ~ {Duration:2100000000,CustomNameVisible:1b,CustomName:'{"text":"1","font":"skill:main"}',Tags:["object","dust","flame_target"]}
					kill 00000002-0000-0002-0000-000200000002
				}
				
				func shoot()
				{
					execute as @e[type=area_effect_cloud,tag=dust] at @s run function ARG(_PATH)bullet/summon
					schedule function ARG(_PATH)shoot 2s
				}
				
				folder bullet()
				{
					func summon()
					{
						summon armor_stand ~ ~0.8 ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:40}}],Tags:["object","effect","dust_bullet","new_bullet"]}
						execute as @e[type=armor_stand,tag=new_bullet,limit=1] at @s run function ARG(_PATH)setting
					}
					
					func setting()
					{
						execute facing entity @p[scores={player=1..},gamemode=adventure] feet run tp @s ~ ~ ~ ~ 0
						tag @s remove new_bullet
					}
					
					func move()
					{
						tp @s ^ ^ ^0.3
						execute positioned ~-0.75 ~-0.75 ~-0.75 as @a[scores={player=1..},gamemode=adventure,dx=0,dy=0,dz=0] positioned ~0.5 ~0.5 ~0.5 if entity @s[dx=0,dy=0,dz=0] run effect give @s blindness 2 0 true
						execute unless block ~ ~ ~ #skill:canpass run kill @s
					}
				}
			}
			
			folder flame()
			{
				func cast()
				{
					# 總動作時間比較長，延後下次技能時間
					schedule function ARG(__PATH)loop 7s
					
					scoreboard players set #ANIM_LOCK number 1
					function miko:animations/animation.miko.flame/play
					playsound minecraft:enemy.miko.flame_cast player @a ~ ~ ~ 3 1 0
					function ARG(_PATH)circle/summon
					
					scoreboard players set #FLAME_BULLET number 20
					schedule function ARG(_PATH)bullet/shoot 1s
				}
				
				folder circle()
				{
					func summon()
					{
						```
						for i in range(-7, 8):
							print('execute if entity @s[y_rotation=%d..%d] run summon armor_stand ~ ~ ~ {Invisible:1b,NoGravity:1b,Invulnerable:1b,Marker:1b,HasVisualFire:1b,Pose:{Head:[90f,0f,0f]},ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:19,CustomPotionColor:0}}],Tags:["object","effect","flame_circle"],Rotation:[%df,0f]}' %(i*24-11, i*24+12, i*24))
						```
					}
					
					func spin()
					{
						execute at @e[type=armor_stand,tag=boss,limit=1,sort=nearest] rotated ~ 0 run tp @s ^ ^ ^1 ~ 0
						
						# shader 抓 CustomPotionColor 控制[放大、透明](藍色)、當前面向(綠色)，並自動旋轉
						execute store result score #SHADER_INFO_COLOR number run data get entity @s Rotation[0] 0.71111
						scoreboard players add #SHADER_INFO_COLOR number 128
						scoreboard players operation #SHADER_INFO_COLOR number *= #256 number
						
						execute unless score @s life matches 250.. unless score @s number matches 1 run scoreboard players add @s life 25
						execute if score @s number matches 1 run scoreboard players remove @s life 50
						execute store result entity @s ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players operation #SHADER_INFO_COLOR number += @s life
						
						execute if score @s life matches 0 run kill @s
					}
				}
				
				folder bullet()
				{
					func shoot()
					{
						scoreboard players remove #FLAME_BULLET number 1
						execute as @e[tag=boss] at @s run function ARG(_PATH)summon
						execute if score #FLAME_BULLET number matches 1.. run schedule function ARG(_PATH)shoot 4t
						execute if score #FLAME_BULLET number matches 0 run function ARG(__PATH)end
					}
					
					func summon()
					{
						summon armor_stand ^ ^0.7 ^0.5 {Invisible:1b,NoGravity:1b,Invulnerable:1b,Marker:1b,Health:6.6f,CustomNameVisible:1b,CustomName:'{"text":"2","font":"skill:main"}',Tags:["object","flame_bullet","dead_event","new_flame"]}
						execute as @e[type=armor_stand,tag=new_flame,limit=1] run function ARG(_PATH)setting
					}
					
					func setting()
					{
						execute store result score @s number run scoreboard players add #BULLET_ID number 1
						scoreboard players set @s health 66
						tp @s ^ ^0.7 ^0.5 ~ 0
						tag @s remove new_flame
					}
					
					func move()
					{
						tp @s ^ ^ ^1
						scoreboard players set #EXPLODE number 0
						execute unless block ~ ~ ~ #skill:canpass run scoreboard players set #EXPLODE number 1
						execute unless score #EXPLODE number matches 1 positioned ~-0.75 ~-0.25 ~-0.75 if entity @e[type=#stage:flame_target,limit=1,tag=flame_target,dx=0.5,dy=0.5,dz=0.5] run scoreboard players set #EXPLODE number 1
						
						execute if score #EXPLODE number matches 1 run function ARG(_PATH)explode
					}
					
					func explode()
					{
						function ARG(_PATH)burst_effect
						playsound minecraft:enemy.miko.flame_burst player @a ~ ~ ~ 3 1 0
						execute as @e[type=#stage:flame_target,tag=flame_target,distance=..2] run function ARG(_PATH)damage
						kill @s
					}
					
					func burst_effect()
					{
						summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:flame/0"}',CustomNameVisible:1,Duration:11,Tags:['{"text":"","font":"font_animation:flame/0"}', '{"text":"","font":"font_animation:flame/1"}', 'new_font_anim']}
						scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 9
						tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
					}
					
					func damage()
					{
						execute if entity @s[scores={player=1..},gamemode=adventure] run effect give @s instant_damage 1 0 true
						kill @s[tag=dust]
					}
					
					func bullet_hurt()
					{
						execute store result entity @s Health float 0.1 run scoreboard players get @s health
						execute if score @s health matches ..0 run function ARG(_PATH)explode
					}
				}
				
				func end()
				{
					scoreboard players set @e[type=armor_stand,tag=flame_circle] number 1
					scoreboard players set #ANIM_LOCK number 1
					execute as @e[type=armor_stand,tag=miko] run function miko:animations/animation.miko.flame_loop/stop
					schedule function ARG(_PATH)end_anim 5t
				}
				
				func end_anim()
				{
					execute as @e[type=armor_stand,tag=miko] run function miko:animations/animation.miko.flame_end/play
				}
			}
			
			folder thunder()
			{
				func cast()
				{
					scoreboard players set #ANIM_LOCK number 1
					function miko:animations/animation.miko.thunder/play
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:42}}],Tags:["object","effect"],UUID:[I;3,3,3,3]}
					playsound minecraft:enemy.miko.thunder_prepare player @a ~ ~ ~ 3 1 0
					execute at @a[scores={player=1..},gamemode=adventure] run function ARG(_PATH)summon
					schedule function ARG(_PATH)strike 20t
				}
				
				func summon()
				{
					```
					for i in range(5):
						print('summon marker ~ ~ ~ {Tags:["object","thunder_summon"]}')
					print(f"spreadplayers ~ ~ 5 5 under {int(GROUND_Y[1] + 3)} false @e[type=marker,tag=thunder_summon]")
					```
					execute at @e[tag=thunder_summon] run summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:41}}],Tags:["object","effect","thunder"]}
					kill @e[type=marker,tag=thunder_summon]
				}
				
				func strike()
				{
					kill 00000003-0000-0003-0000-000300000003
					execute as @e[type=armor_stand,tag=thunder] at @s run function ARG(_PATH)attack
					execute at 00000001-0000-0001-0000-000100000001 run playsound minecraft:enemy.miko.thunder_strike player @a ~ ~ ~ 16 1 1
					execute as 00000001-0000-0001-0000-000100000001 run function miko:animations/animation.miko.thunder_end/play
				}
				
				func attack()
				{
					execute positioned ~ ~-1 ~ run function ARG(_PATH)thunder_effect
					particle flash ~ ~ ~ 0 0 0 1 1
					execute as @e[type=#stage:flame_target,tag=flame_target] if predicate stage:thunder_range run function ARG(_PATH)damage
					kill @s
				}
					
				func damage()
				{
					execute if entity @s[scores={player=1..},gamemode=adventure] run effect give @s instant_damage 1 1 true
					kill @s[tag=dust]
				}
				
				func thunder_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:thunder/0"}',CustomNameVisible:1,Duration:5,Tags:['{"text":"","font":"font_animation:thunder/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
					
					summon armor_stand ~ ~ ~ {Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,HasVisualFire:1,Tags:["object","effect","slash","skill_effect"],ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:43}}]}
				}
			}
			
			folder sacrifice()
			{
				func cast()
				{
					scoreboard players set #ANIM_LOCK number 1
					function miko:animations/animation.miko.sacrifice/play
					```
					print(f"execute at @p[gamemode=adventure,scores={{player=1..}}] positioned ~ {GROUND_Y[1]} ~ run function ARG(_PATH)knock_up")
					```
				}
				
				func knock_up()
				{
					playsound minecraft:enemy.miko.sacrifice player @a ~ ~ ~ 3 1 0
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:20,CustomPotionColor:29}}],Tags:["object","effect"],UUID:[I;4,4,4,4]}
					summon armor_stand ~ ~-1.9 ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:52}}],Tags:["object","effect"],UUID:[I;5,5,5,5]}
					
					execute as @a[gamemode=adventure,scores={player=1..},predicate=stage:sacrifice_range] run function ARG(_PATH)fly
					schedule function ARG(_PATH)clear_levitation 1t
				}
				
				func fly()
				{
					tp @s @s
					effect give @s levitation 1 110 true
				}
				
				func clear_levitation()
				{
					effect clear @a levitation
				}
				
				func check()
				{
					scoreboard players add @s life 1
					execute if score @s life matches ..19 as 00000005-0000-0005-0000-000500000005 at @s run tp @s ~ ~0.1 ~
					execute if score @s life matches 20..99 if entity @a[gamemode=adventure,scores={player=1..},predicate=stage:sacrifice_range,limit=1] run function ARG(_PATH)bite
					execute if score @s life matches 100.. run function ARG(_PATH)sink
					
					execute if score @s life matches 100 as 00000001-0000-0001-0000-000100000001 run function miko:animations/animation.miko.sacrifice_end/play
				}
				
				func bite()
				{
					playsound minecraft:enemy.miko.skeleton_bite player @a ~ ~ ~ 3 1 0
					data modify entity 00000005-0000-0005-0000-000500000005 ArmorItems[3].tag.CustomModelData set value 53
					execute as 00000005-0000-0005-0000-000500000005 at @s run tp @s ~ ~0.5 ~
					execute at 00000005-0000-0005-0000-000500000005 run particle minecraft:crit ~ ~1.2 ~-1.7 1 0 0 1.7 50
					
					effect give @a[gamemode=adventure,scores={player=1..},predicate=stage:sacrifice_range] instant_damage 1 1 true
					
					scoreboard players set @s life 99
				}
				
				func sink()
				{
					tp @s ~ ~-0.1 ~
					execute as 00000005-0000-0005-0000-000500000005 at @s run tp @s ~ ~-0.1 ~
					
					execute if score @s life matches 160 run function ARG(_PATH)clear_effect
				}
				
				func clear_effect()
				{
					kill 00000004-0000-0004-0000-000400000004
					kill 00000005-0000-0005-0000-000500000005
				}
			}
			
			folder sanka()
			{
				func cast()
				{
					scoreboard players set #ANIM_LOCK number 1
					playsound minecraft:enemy.miko.sanka_prepare player @a ~ ~ ~ 3 1 0
					function miko:animations/animation.miko.sanka/play
					```
					for i in range(5):
						print('summon armor_stand ~ ~ ~ {Rotation:[%df,0f],Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:46}}],Tags:["object","effect","sakura"]}' %(i * 72))
					```
					scoreboard players set #SAKURA_MODEL number 46
					schedule function ARG(_PATH)sakura_draw 1t
					
					schedule function ARG(_PATH)call_teleport 20t
				}
				
				func sakura_draw()
				{
					scoreboard players add #SAKURA_MODEL number 1
					execute as @e[type=armor_stand,tag=sakura] store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get #SAKURA_MODEL number
					
					execute if score #SAKURA_MODEL number matches ..50 run schedule function ARG(_PATH)sakura_draw 1t
				}
				
				func call_teleport()
				{
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)teleport
				}
				
				func teleport()
				{
					# 隱形，暫時給 effect tag 避免被當攻擊對象
					function miko:set_variant/invisible
					tag @s add effect
					
					kill @e[type=armor_stand,tag=sakura]
					
					scoreboard players set #BULLET_SPLIT number 1
					execute positioned ~ ~0.7 ~ run function ARG(_PATH)bullet/explode
					function ARG(_PATH)random_tp
					execute at @s run function miko:move
					
					schedule function ARG(_PATH)call_show_up 10t
				}
				
				func random_tp()
				{
					```
					print(f"spreadplayers {STAGE_CENTER[1][0]} {STAGE_CENTER[1][1]} 0 15 under {int(GROUND_Y[1] + 3)} false @s")
					```
					execute at @s if entity @a[gamemode=adventure,scores={player=1..},distance=..5,limit=1] run function ARG(_PATH)random_tp
				}
				
				func call_show_up()
				{
					execute as 00000001-0000-0001-0000-000100000001 at @s run function ARG(_PATH)show_up
				}
				
				func show_up()
				{
					# 不知道為什麼距離遠的時候 move 會錯位，再跑一次 move 才會正常
					function miko:move
					function miko:set_variant/default
					tag @s remove effect
					
					particle firework ~ ~1 ~ 0 0 0 0.2 30
					particle flash ~ ~1 ~ 0 0 0 1 1
				}
				
				folder bullet()
				{
					func explode()
					{
						function ARG(_PATH)explode_effect
						execute if score #BULLET_SPLIT number matches 1.. run function ARG(_PATH)spawn
					}
					
					func explode_effect()
					{
						particle firework ~ ~1 ~ 0 0 0 0.2 30
						particle flash ~ ~1 ~ 0 0 0 1 1
						playsound minecraft:enemy.miko.sanka_shoot player @a ~ ~ ~ 3 1 0
					}
					
					func spawn()
					{
						```
						for i in range(8):
							print('''summon armor_stand ~ ~ ~ {Rotation:[%df,0f],Invisible:1b,Invulnerable:1b,NoGravity:1b,HasVisualFire:1b,Marker:1b,CustomNameVisible:1b,CustomName:'{"text":"3","font":"skill:main"}',Tags:["object","effect","sanka_bullet","new_sanka"]}''' %(i * 45))
						```
						execute as @e[type=armor_stand,tag=new_sanka] run function ARG(_PATH)setting
					}
					
					func setting()
					{
						tag @s remove new_sanka
						scoreboard players operation @s number = #BULLET_SPLIT number
						scoreboard players set @s life 100
					}
					
					func move()
					{
						tp @s ^ ^ ^0.3
						scoreboard players remove @s life 1
						
						scoreboard players set #SANKA_EXPLODED number 0
						execute if predicate stage:sanka_split positioned ^ ^ ^-1 run function ARG(_PATH)split
						# 沒分裂才檢查有沒有打到玩家
						execute if score #SANKA_EXPLODED number matches 0 positioned ~-0.75 ~-0.25 ~-0.75 as @a[gamemode=adventure,scores={player=1..},dx=0.5,dy=0.5,dz=0.5,limit=1] at @s run function ARG(_PATH)hit
						# 有打到玩家就自我清除
						execute if score #SANKA_EXPLODED number matches 2 run kill @s
					}
					
					func split()
					{
						scoreboard players set #SANKA_EXPLODED number 1
						
						scoreboard players operation #BULLET_SPLIT number = @s number
						scoreboard players remove #BULLET_SPLIT number 1
						function ARG(_PATH)explode
						kill @s
					}
					
					func hit()
					{
						scoreboard players set #SANKA_EXPLODED number 2
						
						function ARG(_PATH)explode_effect
						effect give @s instant_damage 1 0 true
					}
				}
			}
			
			folder hell()
			{
				func cast()
				{
					scoreboard players set #ANIM_LOCK number 1
					function miko:animations/animation.miko.hell/play
					scoreboard players reset #RAND_RESULT number
					execute positioned ^ ^ ^2 run function ARG(_PATH)summon
				}
				
				func summon()
				{
					playsound minecraft:enemy.miko.hell_summon player @a ~ ~ ~ 3 1 0
					
					summon armor_stand ~ ~ ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:21,CustomPotionColor:0}}],Tags:["object","effect"],UUID:[I;6,6,6,6]}
					```
					for i in range(-7, 8):
						print('execute if entity @s[y_rotation=%d..%d] run summon armor_stand ~ ~-3.2 ~ {Invisible:1b,Invulnerable:1b,NoGravity:1b,Marker:1b,HasVisualFire:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:54}}],Tags:["object","effect","hell_skeleton","dead_event"],UUID:[I;7,7,7,7],Attributes:[{Name:"minecraft:generic.max_health",Base:1024.0}],Health:23.0f,Pose:{Head:[0.1f,0f,0f]},Rotation:[%df,0f]}' %(i*24-11, i*24+12, i*24))
					```
					scoreboard players set #SKULL_ANGLE number 0
					scoreboard players set #SWAMP_SIZE number 0
				}
				
				func check()
				{
					execute unless score @s life matches 40.. run function ARG(_PATH)float
					execute if score @s life matches 40.. run function ARG(_PATH)chase
				}
				
				func float()
				{
					scoreboard players add @s life 1
					execute if score @s life matches ..8 store result entity 00000006-0000-0006-0000-000600000006 ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players add #SWAMP_SIZE number 3
					execute if score @s life matches ..20 run tp @s ~ ~0.2 ~ ~ 0
					execute if score @s life matches 6..20 store result entity @s Pose.Head[0] float 1 run scoreboard players add #SKULL_ANGLE number 6
					execute if score @s life matches 25..36 store result entity 00000006-0000-0006-0000-000600000006 ArmorItems[3].tag.CustomPotionColor int 1 run scoreboard players remove #SWAMP_SIZE number 2
					execute if score @s life matches 40 run function ARG(_PATH)start_chase
				}
				
				func start_chase()
				{
					execute as 00000001-0000-0001-0000-000100000001 run function miko:animations/animation.miko.hell_end/play
					playsound minecraft:enemy.miko.scream player @a ~ ~ ~ 3 1 0
					
					data modify entity @s ArmorItems[3].tag.CustomModelData set value 55
					tag @s remove effect
					scoreboard players set @s number 2
				}
				
				func chase()
				{
					execute facing entity @p[gamemode=adventure,scores={player=1..}] feet rotated ~ 0 run tp @s ^ ^ ^0.5 ~ 0
					execute positioned ~-1.5 ~-1 ~-1.5 run effect give @a[gamemode=adventure,scores={player=1..},dx=2,dy=3,dz=2] instant_damage 1 1 true
				}
				
				func skull_hurt()
				{
					tp @s ^ ^ ^-0.5
					execute if score @s health matches 1.. store result entity @s Health float 0.1 run scoreboard players get @s health
					execute if score @s health matches ..0 run function ARG(_PATH)explode
				}
				
				func explode()
				{
					particle minecraft:explosion_emitter ~ ~ ~ 0 0 0 1 1
					particle minecraft:item minecraft:bone_block ~ ~1 ~ 0 0 0 0.4 100
					playsound minecraft:enemy.miko.skull_break player @a ~ ~ ~ 3 1 0
					
					execute positioned ~ ~-0.8 ~ run effect give @a[gamemode=adventure,scores={player=1..},distance=..5] instant_damage 1 1 true
					kill @s
				}
			}
			
			folder sikigami()
			{
				func cast()
				{
					scoreboard players set #SIKIGAMI_USED number 1
					scoreboard players set #SIKIGAMI_CD number 1
					schedule function ARG(_PATH)reset_cd 90s
					
					particle minecraft:cloud ~ ~ ~ 0.3 1.2 0.3 0.27 300
					playsound minecraft:enemy.miko.smoke_burst player @a ~ ~ ~ 3 1 0
					
					scoreboard players set #EXIST_CLONE number 0
					function miko:summon/default
					function miko:summon/default
					function miko:summon/default
					execute as @e[type=armor_stand,tag=new_miko] run function ARG(_PATH)clone_setting
					```
					print(f"spreadplayers {STAGE_CENTER[1][0]} {STAGE_CENTER[1][1]} 10 15 under {GROUND_Y[1] + 3} false @e[type=armor_stand,tag=miko]")
					```
					# 在各個分身位置生成防護罩特效
					execute at @e[type=armor_stand,tag=miko] run summon area_effect_cloud ~ ~0.3 ~ {Duration:2147483647,Tags:["object","barrier"],CustomNameVisible:1b,CustomName:'{"text":"4","font":"skill:main"}'}
				}
				
				func clone_setting()
				{
					tag @s add clone
					tag @s remove new_miko
					execute store result score @s number run scoreboard players add #EXIST_CLONE number 1
					scoreboard players add @s number 2
				}
				
				func reset_cd()
				{
					scoreboard players reset #SIKIGAMI_CD number
				}
				
				func hit_check()
				{
					execute if score #GUN_WEAK_ATTACK number matches 1 run function ARG(_PATH)block_damage
					execute unless score #GUN_WEAK_ATTACK number matches 1 if entity @s[tag=clone] run function ARG(_PATH)clone_break
					
					# 被有效攻擊打到就清掉屏障，每個分身一定會分很開，直接抓最近的清掉就好
					execute unless score #GUN_WEAK_ATTACK number matches 1 run kill @e[type=area_effect_cloud,tag=barrier,sort=nearest,limit=1]
				}
				
				func block_damage()
				{
					playsound minecraft:enemy.miko.block_shoot player @a ~ ~ ~ 3 1 0
					execute positioned ~ ~0.5 ~ run function ARG(_PATH)block_effect
					scoreboard players set #IGNORE_DAMAGE number 1
				}
				
				func block_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:burn_bullet/0"}',CustomNameVisible:1,Duration:4,Tags:['{"text":"","font":"font_animation:burn_bullet/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 3
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
				
				func clone_break()
				{
					playsound minecraft:enemy.miko.smoke_burst player @a ~ ~ ~ 1 1 0
					execute positioned ~ ~0.5 ~ run function ARG(_PATH)clone_break_effect
					scoreboard players remove #EXIST_CLONE number 1
				}
				
				func clone_break_effect()
				{
					summon minecraft:area_effect_cloud ~ ~ ~ {CustomName:'{"text":"0","font":"font_animation:sikigami_break/0"}',CustomNameVisible:1,Duration:8,Tags:['{"text":"","font":"font_animation:sikigami_break/0"}', 'new_font_anim']}
					scoreboard players set @e[type=area_effect_cloud,tag=new_font_anim,limit=1] font_animation 7
					tag @e[type=area_effect_cloud,tag=new_font_anim,limit=1] remove new_font_anim
				}
			}
		}
	}
	
	# 深淵恐懼
	folder stage3()
	{
		func start()
		{
			bossbar set minecraft:boss name {"translate":"Root of Fear","color":"white","bold":true}
			# tp @a x y z rx ry
			# summon ...
			schedule function ARG(_PATH)skills/loop 7s
		}
		
		func end()
		{
			schedule clear ARG(_PATH)skills/loop
		}
		
		func main()
		{
			
		}
		
		folder skills()
		{
			func loop()
			{
				schedule function ARG(_PATH)loop 7s
			}
		}
	}
}